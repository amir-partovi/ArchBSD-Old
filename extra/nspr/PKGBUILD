# $Id: PKGBUILD 173931 2012-12-28 07:40:52Z andyrtr $
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Alexander Baldeck <alexander@archlinux.org>
pkgname=nspr
pkgver=4.9.6
pkgrel=1
pkgdesc="Netscape Portable Runtime"
arch=(i686 x86_64)
url="http://www.mozilla.org/projects/nspr/"
license=('MPL' 'GPL')
makedepends=('zip')
options=(!emptydirs)
source=(ftp://ftp.mozilla.org/pub/mozilla.org/nspr/releases/v${pkgver}/src/${pkgname}-${pkgver}.tar.gz
        nspr.pc.in
        patch-..__config__autoconf.mk.in
        patch-..__configure
        patch-bug301986
        patch-bug782109
        patch-bug782111
        patch-bug782113
        patch-bug782124
        patch-config-nsprc.pc.in
        patch-libtests
        patch-tests
        patch-warnings
)
md5sums=('62b1e9d376d503d972f90c3c0031d879'
         'bce1611f3117b53fc904cab549c09967'
         'c1da66e41aa3d6fa167953e941556b3c'
         '5d9212facb8b3653820d47ea74bff2e3'
         'db2afdf4826c3cebde395170487603c9'
         '890770ea83985561f6a02200c27142aa'
         '3d12d7cf2bb10c10ee9effe90c66915a'
         'e33ee4288afc4ca37614a3b1604a7dce'
         '5f73219f66e5132ec8dc9729d189626c'
         '4193286e736c535a1069f08945a0397d'
         'ac30b066d851395cb4048c00ce6bc2d6'
         '23e15afd1e9780f8171faba661a90e5d'
         'b25ffe8536358df955a6e177a770eb03')

prepare() {
  cd "${pkgname}-${pkgver}/mozilla/nsprpub/config"
  for i in \
    patch-..__config__autoconf.mk.in \
    patch-..__configure \
    patch-bug301986 \
    patch-bug782109 \
    patch-bug782111 \
    patch-bug782113 \
    patch-bug782124 \
    patch-config-nsprc.pc.in \
    patch-libtests \
    patch-tests \
    patch-warnings
  do
    patch -p0 -i "${srcdir}/$i"
  done

  cd "${srcdir}/${pkgname}-${pkgver}"

  sed -i '' -e 's/\$(MKSHLIB) \$(OBJS)/\$(MKSHLIB) \$(LDFLAGS) \$(OBJS)/g' \
      mozilla/nsprpub/config/rules.mk

  sed -i '' -e 's,-lpthread,-pthread,g ; s,echo aout,echo elf,g' mozilla/nsprpub/configure
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  if [ "$CARCH" = "x86_64" ]; then
    confflags="--enable-64bit"
  else
    confflags=""
  fi

  PTHREAD_LIBS=-pthread BSD_PTHREAD_LIBS=-pthread ./mozilla/nsprpub/configure \
      --prefix=/usr \
      --libdir=/usr/lib \
      --includedir=/usr/include/nspr \
      --enable-optimize \
      --with-pthreads \
      --disable-debug ${confflags} \
      --disable-static \
      $CHOST
  gmake
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  gmake DESTDIR="${pkgdir}" install

  NSPR_LIBS=`./config/nspr-config --libs`
  NSPR_CFLAGS=`./config/nspr-config --cflags`
  NSPR_VERSION=`./config/nspr-config --version`
  sed -e "s,%libdir%,/usr/lib," \
    -e "s,%prefix%,/usr," \
    -e "s,%exec_prefix%,/usr/bin," \
    -e "s,%includedir%,/usr/include/nspr," \
    -e "s,%NSPR_VERSION%,${NSPR_VERSION}," \
    -e "s,%FULL_NSPR_LIBS%,${NSPR_LIBS}," \
    -e "s,%FULL_NSPR_CFLAGS%,${NSPR_CFLAGS}," \
    "${srcdir}/nspr.pc.in" > "${pkgdir}/usr/lib/pkgconfig/nspr.pc"
  chmod 644 "${pkgdir}/usr/lib/pkgconfig/nspr.pc"
  ln -sf nspr.pc "${pkgdir}/usr/lib/pkgconfig/mozilla-nspr.pc"

  chmod 644 ${pkgdir}/usr/lib/*.a

  rm -rf "${pkgdir}/usr/bin/compile-et.pl" \
    "${pkgdir}/usr/bin/prerr.properties" \
    "${pkgdir}/usr/share/aclocal/nspr.m4" \
    "${pkgdir}/usr/include/nspr/md"

  cd "${pkgdir}/usr/lib/"
  for i in libnspr4 libplc4 libplds4; do
    ln -sf ${i}.so{.1,}
  done
}
