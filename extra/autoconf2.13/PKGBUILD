# Maintainer: Jan de Groot <jgc@archlinux.org
# Contributor: Chaiwat Suttipongsakul <cwt at bashell dot com>
# Contributor: Tanguy Bouz√©loc <the.zauron at gmail dot com>
# Contributor: Devin Cofer <ranguvar{AT]archlinux[DOT}us>

pkgname=autoconf2.13
pkgver=2.13
pkgrel=3
pkgdesc="A GNU tool for automatically configuring source code (Legacy 2.1x version)"
arch=(any)
url="http://www.gnu.org/software/autoconf/"
license=('GPL2')
depends=('perl')
source=(http://ftpmirror.gnu.org/autoconf/autoconf-${pkgver}.tar.gz
        patch-Makefile.in patch-autoconf.sh patch-autoconf.texi patch-autohader.sh patch-autoupdate.sh
)
md5sums=('9de56d4a161a723228220b0f425dc711'
         '9fd4005cf5551fedbc0b0a51c6f4f2f8'
         '538997e7daefcfcfc0a40544879021cb'
         '1417286844dc7452a930522f9d2b7f7d'
         '3e1b14646d8988454fdefc0892dfd06a'
         '540545af9a57be56b277532a8a360ac9')

build() {
  cd "${srcdir}/autoconf-${pkgver}"
  for i in patch-Makefile.in patch-autoconf.sh patch-autoconf.texi patch-autohader.sh patch-autoupdate.sh
  do
    patch -p0 -i "${srcdir}/$i"
  done
  ./configure --prefix=/usr --infodir=/usr/share/info --program-suffix=-2.13
  make
}

package() {
  cd "${srcdir}/autoconf-${pkgver}"
  make prefix="${pkgdir}/usr" infodir="${pkgdir}/usr/share/info" install
  rm -rf "${pkgdir}/usr/share/info"
}
