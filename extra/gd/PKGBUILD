# $Id: PKGBUILD 148724 2012-02-05 11:48:15Z ibiru $
# Maintainer: Eric Belanger <eric@archlinux.org>

pkgname=gd
pkgver=2.0.36RC1
pkgrel=5
pkgdesc="Library for the dynamic creation of images by programmers"
arch=('i686' 'x86_64')
url="http://www.libgd.org/"
license=('custom')
depends=('libpng' 'fontconfig' 'libjpeg')
optdepends=('perl: bdftogd script')
options=('!libtool')
source=("ftp://ftp.archlinux.org/other/${pkgname}/${pkgname}-${pkgver}.tar.bz2"
        'libpng14.patch')
md5sums=('e876979ca3130623a4e7866d9579171a'
         '7921556f4960e8d66cf4fb67ea7101b5')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  patch -Np1 -i ${srcdir}/libpng14.patch
  #Add -pthread to ldflags
  export LDFLAGS="${LDFLAGS} -pthread"
  ./configure --prefix=/usr --without-xpm
  gmake
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  gmake DESTDIR="${pkgdir}" install
  install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}/"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
