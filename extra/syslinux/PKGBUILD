# Contributor: Thomas BÃ¤chler <thomas@archlinux.org>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>
# Maintainer: Wolfgang Bumiller <archbsd at bumiller dot com>

pkgname=syslinux
pkgver=5.01
pkgrel=2
arch=('i686' 'x86_64')
pkgdesc="Collection of boot loaders that boot from FAT, ext2/3/4 and btrfs filesystems, from CDs and via PXE"
url="http://syslinux.zytor.com/"
license=('GPL2')
depends=('perl')
makedepends=('nasm' 'gsed' 'gmake')
source=(http://www.kernel.org/pub/linux/utils/boot/syslinux/$pkgname-${pkgver}.tar.bz2
patch-Makefile patch-com32-lib-Makefile patch-com32-libutil-md5.c patch-com32-libutil-sha256crypt.c patch-com32-libutil-sha512crypt.c patch-libinstaller-bin2c.pl patch-libinstaller-syslinux.h patch-libinstaller-syslxopt.c patch-libinstaller-syslxopt.h patch-man-syslinux.1 patch-mtools-Makefile patch-mtools-syslinux.c
        )

build() {
  # Do not try to build syslinux with our default LDFLAGS, it will fail
  unset LDFLAGS
  cd "$srcdir"/$pkgname-${pkgver}

  find . -type f -exec grep -le 'elf_i386' '{}' \; | \
    xargs sed -i '' -e 's/elf_i386/&_fbsd/g;'

  for i in patch-Makefile patch-com32-lib-Makefile patch-com32-libutil-md5.c \
           patch-com32-libutil-sha256crypt.c patch-com32-libutil-sha512crypt.c \
           patch-libinstaller-bin2c.pl patch-libinstaller-syslinux.h \
           patch-libinstaller-syslxopt.c patch-libinstaller-syslxopt.h \
           patch-man-syslinux.1 patch-mtools-Makefile patch-mtools-syslinux.c
  do
    patch -p0 -i "${srcdir}/$i"
  done

  sed -i '' -e 's|^UMAKEDEPS|#UMAKEDEPS|' mk/syslinux.mk

  sed -i '' \
      -e '/^#include <alloca\.h>$/d' \
      -e 's|^#include <uuid/uuid.h>|#include <uuid.h>|' \
      utils/isohybrid.c

  gmake
}

package() {
  cd "$srcdir"/$pkgname-${pkgver}

  install -dm755 "${pkgdir}/usr/lib/syslinux"
  for i in mbr/{,gpt}mbr.bin core/{pxelinux.0,isolinux.bin} gpxe/gpxelinux.0 \
           memdisk/memdisk com32/menu/*.c32 com32/modules/*.c32 com32/mboot/mboot.c32
  do
    install -m755 "$i" "${pkgdir}/usr/lib/syslinux/"$(basename "$i")
  done

  install -dm755 "${pkgdir}/usr/bin"
  install -m755 mtools/syslinux "${pkgdir}/usr/bin/syslinux"
  install -dm755 "${pkgdir}/usr/share/man/man1"
  install -m644 man/syslinux.1 "${pkgdir}/usr/share/man/man1/syslinux.1"
}
md5sums=('6c21244277fa4e444967ba45469de863'
         '2dde27fc1702cfdb701e851bb13dc52d'
         '247d90164399bc0c535be848d3047bff'
         '392ed722ff19baad4b4a1d7246d35ed9'
         'e71f0856eb0a8291762c770c6afcfe17'
         '53d312cc472292c611516fd16e74c0a8'
         '31c6eacf774693b765f4fa4acd699d46'
         '4ff9b959a04abf3311c26a39bba9cf1e'
         '26be37e3cbaf7fa9414998481d8c88ce'
         '89558fcfb5148e2f398a380cd897b643'
         '1cff6653882a65370aa6918feb0fca77'
         '11d9ba504ec2488a53c5300b2c7b8c27'
         '8f83f8464b43795c190400c2f2bf3677')
