pkgname=polkit
pkgver=0.99
pkgrel=1
pkgdesc="Application development toolkit for controlling system-wide privileges"
arch=(i686 x86_64)
license=('LGPL')
url="http://www.freedesktop.org/wiki/Software/polkit"
depends=('glib2' 'expat' 'js')
makedepends=('intltool' 'gtk-doc' 'gobject-introspection')
replaces=('policykit')
options=('!libtool')
install=polkit.install
source=(http://www.freedesktop.org/software/polkit/releases/$pkgname-$pkgver.tar.gz
	patch-data_Makefile.in
	patch-src_polkitbackend_Makefile.in
	patch-src_polkitd_gposixsignal.c
	patch-src_programs_pkexec.c)
md5sums=('fcc4d7b19c08ad54d3ce0eae0ab12398'
         '7639a2dbd210b88276bcf172ee2c70ae'
         '057e3c2f3304147ed3b8c3a7dab6803e'
         '4043f6a3d1cd1e190af355afcdfc9595'
         '9c600c9ded6d0fc1effe54e350220578')

build() {
  cd $pkgname-$pkgver
   
   export CPPFLAGS="${CPPFLAGS} -I/usr/local/include"
   for i in patch-data_Makefile.in \
	patch-src_polkitbackend_Makefile.in \
	patch-src_polkitd_gposixsignal.c \
	patch-src_programs_pkexec.c; do
   patch -p0 -i "${srcdir}/$i"
   done 

  ./configure --prefix=/usr/local --with-authfw=pam \
		--with-pam-include=system \
		--with-os-type=freebsd \
		--enable-introspection \
		--localstatedir=/var --disable-gtk-doc --disable-man-pages
  gmake
}

package() {
  cd $pkgname-$pkgver
  gmake DESTDIR="$pkgdir" install
  
  install -dm755 "${pkgdir}/usr/local/etc/dbus-1/system.d/"   
  install "${srcdir}/${pkgname}-${pkgver}/data/org.freedesktop.PolicyKit1.conf" \
		"${pkgdir}"/usr/local/etc/dbus-1/system.d/org.freedesktop.PolicyKit1.conf.dist

}
