# Maintainer: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Jason Chu <jason@archlinux.org>

pkgname=python
pkgver=3.3.0
pkgrel=3
_pybasever=3.3
pkgdesc="Next generation of the python high-level scripting language"
arch=('i686' 'x86_64')
license=('custom')
url="http://www.python.org/"
depends=('expat' 'gdbm' 'libffi')
makedepends=('tk>=8.6.0' 'sqlite' 'valgrind' 'bluez')
makedepends=('tk>=8.6.0' 'sqlite'                   )
optdepends=('tk: for tkinter (not yet supported)' 'sqlite')
provides=('python3')
replaces=('python3')
options=('!makeflags')
source=(http://www.python.org/ftp/python/${pkgver%rc*}/Python-${pkgver}.tar.xz
        patch-Modules-_ctypes-libffi-configure
        patch-Modules-_decimal-libmpdec-mpdecimal.h
        patch-Modules-fcntlmodule.c
        patch-setup.py
)
sha1sums=('833d73565e1b665f1878504081dc985a5a06e46a'
          'f67e8e61a5e448f15b1047e7dc21dcccefdc032c'
          '2293abd864b53cad74dc084d9fd0c0b60f8fc45e'
          '0e524fae81a19b5c19c63fcc986fa6d191616322'
          'f9d5ae9ad6de8df57bc4c2cae53ed5543d07e43d')

build() {
  cd "${srcdir}/Python-${pkgver}"

  cp -r Lib/plat-freebsd8 Lib/plat-freebsd9
  cp -r Lib/plat-freebsd8 Lib/plat-freebsd10

  for i in patch-Modules-_ctypes-libffi-configure \
           patch-Modules-_decimal-libmpdec-mpdecimal.h \
           patch-Modules-fcntlmodule.c \
           patch-setup.py
  do
    msg "patch $i"
    patch -p0 -i "${srcdir}/$i"
  done

  # Ensure that we are using the system copy of various libraries (expat, zlib and libffi),
  # rather than copies shipped in the tarball
  rm -r Modules/expat
  rm -r Modules/zlib
  rm -r Modules/_ctypes/{darwin,libffi}*

  LDFLAGS="$LDFLAGS -L. -pthread" \
  CFLAGS="$CFLAGS -pthread" \
  ./configure --prefix=/usr/local \
              --enable-shared \
              --with-threads \
              --with-computed-gotos \
              --enable-ipv6 \
              --without-valgrind \
              --with-system-expat \
              --with-dbmliborder=gdbm:ndbm \
              --with-system-ffi

  make
}

# 2 failing
check() {
  cd "${srcdir}/Python-${pkgver}"
  LD_LIBRARY_PATH="${srcdir}/Python-${pkgver}":${LD_LIBRARY_PATH} \
     "${srcdir}/Python-${pkgver}/python" -m test.regrtest -x test_distutils test_site \
     test_urllib test_uuid test_pydoc test_logging
}

package() {
  cd "${srcdir}/Python-${pkgver}"
  make DESTDIR="${pkgdir}" install maninstall

  # Why are these not done by default...
  ln -sf python3               "${pkgdir}/usr/local/bin/python"
  ln -sf python3-config        "${pkgdir}/usr/local/bin/python-config"
  ln -sf idle3                 "${pkgdir}/usr/local/bin/idle"
  ln -sf pydoc3                "${pkgdir}/usr/local/bin/pydoc"
  ln -sf python${_pybasever}.1 "${pkgdir}/usr/local/man/man1/python3.1"

  # Fix FS#22552
  ln -sf ../../libpython${_pybasever}m.so \
    "${pkgdir}/usr/local/lib/python${_pybasever}/config-${_pybasever}m/libpython${_pybasever}m.so"

  # Clean-up reference to build directory
  sed -i '' -e "s|$srcdir/Python-${pkgver}:||" "$pkgdir/usr/local/lib/python${_pybasever}/config-${_pybasever}m/Makefile"

  # License
  install -dm755 "${pkgdir}/usr/local/share/licenses/${pkgname}"
  install -m644 LICENSE "${pkgdir}/usr/local/share/licenses/${pkgname}/LICENSE"

  install -dm755 "${pkgdir}/usr/local/libdata"
  mv "${pkgdir}/usr/local/lib/pkgconfig" \
     "${pkgdir}/usr/local/libdata/pkgconfig"
}
