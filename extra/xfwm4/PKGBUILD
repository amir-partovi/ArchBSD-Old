# $Id: PKGBUILD 163772 2012-07-19 16:30:24Z foutrelis $
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: tobias <tobias funnychar archlinux.org>

pkgname=xfwm4
pkgver=4.10.0
pkgrel=3
pkgdesc="Xfce window manager"
arch=('i686' 'x86_64')
url="http://www.xfce.org/"
license=('GPL2')
groups=('xfce4')
depends=('libxfce4ui' 'libwnck' 'hicolor-icon-theme')
makedepends=('intltool')
options=('!libtool')
install=$pkgname.install
source=(http://archive.xfce.org/src/xfce/$pkgname/4.10/$pkgname-$pkgver.tar.bz2
        xfwm4-4.10.0-implement-NET_WM_MOVERESIZE_CANCEL.patch
	extrapatch-src__events.c)
sha256sums=('492357bf48121ebffabf2bf0d3b84213d19bf81087321175d687c8a68efe1f9c'
            'f088a56cc05d1856af8d68dae9e40bc53b724d9dde4d8c2b78a88922c0ee6d1d'
            '0dcb0aaee81934545f10386b60db6468df2ae556733e4b630a3a8893558a5673')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # Fix https://bugzilla.xfce.org/show_bug.cgi?id=8949 (FS#29970)
  patch -Np1 -i "$srcdir/xfwm4-4.10.0-implement-NET_WM_MOVERESIZE_CANCEL.patch"
  patch -p0 -i ${srcdir}/extrapatch-src__events.c

  find ${srcdir}/${pkgname}-${pkgver} -name "configure" -type f | xargs sed -i '' -e 's|-lpthread|-pthread|g'

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib \
    --localstatedir=/var \
    --disable-static \
    --enable-startup-notification \
    --enable-randr \
    --enable-compositor \
    --enable-xsync \
    --disable-debug
  gmake
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  gmake DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
