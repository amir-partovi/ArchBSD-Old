# $Id: PKGBUILD 184436 2013-05-06 19:39:47Z foutrelis $
# Maintainer: RÃ©my Oudompheng <remy@archlinux.org>
# Contributor: francois <francois.archlinux.org>

pkgname=texlive-bin
pkgver=2012.0
pkgrel=14
_biber_ver=1.5 # for biblatex 2.5 only.
pkgdesc="TeX Live binaries"
license=('GPL')
arch=('i686' 'x86_64')
depends=('t1lib' 'gd' 'poppler>=0.24.0' 'libsigsegv' 'zziplib' 'libpng' 'libjpeg' 'freetype2')
makedepends=('perl' 'clisp' 'ffcall')
optdepends=('ed: for texconfig')
provides=('lcdf-typetools' 'kpathsea' 'xindy' 'pdfjam')
conflicts=('pdfjam')
options=('!libtool' '!strip')
url='http://tug.org/texlive/'
install="texlive.install"
source=('texmf.cnf'
        'fix-fontforge-encoding.patch'
        '09-texlive-fonts.conf'
        'poppler-0.20.patch'
        'http://mirrors.kernel.org/archlinux/other/texlive/texlive-bin-source-20120623.tar.xz'
        'http://mirrors.kernel.org/archlinux/other/texlive/texlive-bin-texmf-20120623.tar.xz'
        "http://downloads.sourceforge.net/project/biblatex-biber/biblatex-biber/${_biber_ver}/binaries/FreeBSD/biber-amd64-freebsd.tar.xz"
        "http://downloads.sourceforge.net/project/biblatex-biber/biblatex-biber/${_biber_ver}/binaries/FreeBSD/biber-i386-freebsd.tar.xz"
        'luatex-r4449-radical-rule-thickness.patch'
)
noextract=(biber-amd64-freebsd.tar.xz
           biber-i386-freebsd.tar.xz)
backup=(etc/texmf/web2c/texmf.cnf \
    etc/texmf/chktex/chktexrc \
	etc/texmf/dvipdfm/config/config \
	etc/texmf/dvipdfmx/dvipdfmx.cfg \
	etc/texmf/dvips/config/config.ps \
	etc/texmf/tex/generic/config/language.dat \
	etc/texmf/tex/generic/config/language.def \
	etc/texmf/tex/generic/config/pdftexconfig.tex \
	etc/texmf/ttf2pk/ttf2pk.cfg \
	etc/texmf/web2c/fmtutil.cnf \
	etc/texmf/web2c/mktex.cnf \
	etc/texmf/xdvi/XDvi)

sha1sums=('efb60a682c5aa786d2aaf7a196f1bee97f9aeee2'
          '5301263529702ccfda0adb3706f461888dea22eb'
          '332844120fe850d10311772e572f57f33871d522'
          '32adfa13604d13dc951c17c404e2c6381788181c'
          '3f428b3a8f1c9d986cba56104e8a3030ccdcd2b0'
          '7fdc8972d22347880d1117bec8c3b75f82c4fbd5'
          '01a9adfcdeffa570dce6605b44bcc90419c92d87'
          '94225131e9e9aa622740fdadb23241ceea0d4a0f'
          '73cb530b6b2b1146a477a17dd98ecaaedce42c58')

build() {
   cd "$srcdir"

   if [ "${CARCH}" = "x86_64" ]; then
     export CFLAGS="${CFLAGS} -fPIC"
     export CXXFLAGS="${CXXFLAGS} -fPIC"
     tar xf biber-amd64-freebsd.tar.xz
   else
     tar xf biber-i386-freebsd.tar.xz
   fi

   # this patch removes spurious error message with locale "xx_YY.utf8"
   patch -Np0 -i fix-fontforge-encoding.patch
   # fix build with poppler >= 0.20
   patch -Np0 -i poppler-0.20.patch
   # fix radical rule thickness regression in Luatex
   # http://tex.stackexchange.com/q/61952/729
   patch -Np0 -i luatex-r4449-radical-rule-thickness.patch
   # t4ht expects to be un /usr/share/texmf/bin/t4ht (FS#27251)
   gsed -i s/SELFAUTOPARENT/TEXMFROOT/ source/texk/tex4htk/t4ht.c
   #############################################################
   ### configure
   cd source
   ## prevent compiling Xdvi with libXp
   gsed -i~ 's|-lXp ||' texk/xdvik/configure
   test ! -d Work && mkdir Work
   cd Work
   echo "--> Initial configuration..."
   # we use temporary prefix to avoid messing the existing
   # $pkgdir/usr/share/texmf tree
   # system zlib is disabled due to issues with zlib 1.2.6 (FS#28221)
   ../configure --prefix=/usr -C \
     --sysconfdir=/etc \
     --datarootdir=/usr/share \
     --datadir=/usr/share \
     --mandir=/usr/share/man \
     --disable-native-texlive-build \
     --with-banner-add="/ArchBSD" \
     --disable-multiplatform \
     --disable-dialog \
     --disable-psutils \
     --disable-t1utils \
     --disable-bibtexu \
     --disable-xz \
     --with-system-zlib \
     --with-system-zziplib \
     --with-system-pnglib \
     --with-system-ncurses \
     --with-system-t1lib \
     --with-system-gd \
     --with-system-poppler \
     --with-system-xpdf \
     --with-system-freetype2 \
     --without-system-graphite \
     --with-freetype2-libdir=/usr/lib \
     --with-freetype2-include=/usr/include/freetype2 \
     --with-xdvi-x-toolkit=xaw \
     --disable-dump-share \
     --disable-aleph \
     --enable-luatex \
     --with-clisp-runtime=default \
     --enable-xindy --disable-xindy-rules --disable-xindy-docs
   #############################################################
   ### make
   echo "-------------------------------------------------------"
   echo "--> Building the whole beast ..."
   echo "-------------------------------------------------------"
   gmake
}

package() {
   cd "$srcdir"
   #############################################################
   ### install texmf tree
   echo "-------------------------------------------------------"
   echo "--> installing the texmf tree"
   echo "-------------------------------------------------------"
   install -m755 -d "$pkgdir"/usr/share
   cp -r texmf "$pkgdir"/usr/share/
   install -d -m755 "$pkgdir"/etc/texmf/web2c
   install -d -m755 "$pkgdir"/etc/texmf/chktex
   install -d -m755 "$pkgdir"/etc/texmf/dvips/config
   install -d -m755 "$pkgdir"/etc/texmf/dvipdfm/config
   install -d -m755 "$pkgdir"/etc/texmf/dvipdfmx
   install -d -m755 "$pkgdir"/etc/texmf/tex/generic/config
   install -d -m755 "$pkgdir"/etc/texmf/ttf2pk
   install -d -m755 "$pkgdir"/etc/texmf/xdvi
   install -d -m755 "$pkgdir"/etc/fonts/conf.avail
   install -m644 "$srcdir"/09-texlive-fonts.conf "$pkgdir"/etc/fonts/conf.avail/
   # move man files to /usr/share/man
   for i in 1 5; do
     install -d -m755 "$pkgdir"/usr/share/man/man$i
     mv "$pkgdir"/usr/share/texmf/doc/man/man$i/*.$i "$pkgdir"/usr/share/man/man$i/
   done
   # move info files to /usr/share/info
   install -d -m755 "$pkgdir"/usr/share/info
   mv "$pkgdir"/usr/share/texmf/doc/info/*.info "$pkgdir"/usr/share/info/
   rm -rf "$pkgdir"/usr/share/texmf/doc/{man,info}
   # copy config files to $TEXMFCONFIG tree
   cp -a "$pkgdir"/usr/share/texmf/chktex/chktexrc \
     	"$pkgdir"/etc/texmf/chktex/
   cp -a "$pkgdir"/usr/share/texmf/web2c/mktex.cnf \
   	  "$pkgdir"/etc/texmf/web2c/
   cp -a "$pkgdir"/usr/share/texmf/web2c/updmap.cfg \
   	  "$pkgdir"/etc/texmf/web2c/
   cp -a "$pkgdir"/usr/share/texmf/web2c/fmtutil.cnf \
   	  "$pkgdir"/etc/texmf/web2c/
   cp -a "$pkgdir"/usr/share/texmf/dvips/config/config.ps \
   	  "$pkgdir"/etc/texmf/dvips/config/
   cp -a "$pkgdir"/usr/share/texmf/dvipdfm/config/config \
   	  "$pkgdir"/etc/texmf/dvipdfm/config/
   cp -a "$pkgdir"/usr/share/texmf/dvipdfmx/dvipdfmx.cfg \
   	  "$pkgdir"/etc/texmf/dvipdfmx/
   cp -a "$pkgdir"/usr/share/texmf/tex/generic/config/pdftexconfig.tex \
   	  "$pkgdir"/etc/texmf/tex/generic/config/
   cp -a "$pkgdir"/usr/share/texmf/tex/generic/config/language.dat \
   	  "$pkgdir"/etc/texmf/tex/generic/config/
   cp -a "$pkgdir"/usr/share/texmf/tex/generic/config/language.def \
   	  "$pkgdir"/etc/texmf/tex/generic/config/
   cp -a "$pkgdir"/usr/share/texmf/ttf2pk/ttf2pk.cfg \
   	  "$pkgdir"/etc/texmf/ttf2pk/
   cp -a "$pkgdir"/usr/share/texmf/xdvi/XDvi \
   	  "$pkgdir"/etc/texmf/xdvi/
   # remove TL specific warnings in the language.{dat,def} files:
   gsed -i -e '/DO NOT EDIT/,+3 d' "$pkgdir"/etc/texmf/tex/generic/config/language.*
   # clean updmap.cfg
   gsed -i '/^\(Map\|MixedMap\)/d' "$pkgdir"/etc/texmf/web2c/updmap.cfg
   gsed -i '/^#! \(Map\|MixedMap\)/d' "$pkgdir"/etc/texmf/web2c/updmap.cfg

   #############################################################
   ### install
   cd source
   # fixes for xindy
   find utils/xindy -name Makefile -exec sed -i -e "s|^prefix =.\+$|prefix = $pkgdir/usr|" -e "s|^mandir =.\+$|mandir = \${prefix}/share/man|" -e "s|^datadir =.\+$|datadir = \${datarootdir}/texmf|" -e "s|^docdir =.\+$|docdir = \${datadir}/doc/xindy|" '{}' \;

   echo "-------------------------------------------------------"
   echo "--> Proceeding with make install ..."
   echo "-------------------------------------------------------"
   cd Work
   gmake DESTDIR="${pkgdir}" texmf="$pkgdir"/usr/share/texmf install
   rm -rf "${pkgdir}"/usr/{texmf,share/texmf-dist}

   # replace upstream texmf.cnf with ours
   rm -f "$pkgdir"/usr/share/texmf/web2c/texmf.cnf
   install -m644 "$srcdir"/texmf.cnf "$pkgdir"/etc/texmf/web2c/texmf.cnf
   # since the location of texmf.cnf is hard-wired to be under /usr/share/texmf/web2c
   # we make a symlink from /etc/texmf/web2c/texmf.cnf to the latter
   ln -sf /etc/texmf/web2c/texmf.cnf "$pkgdir"/usr/share/texmf/web2c/texmf.cnf
   # fix location of TEXMFCACHE for luatools
   gsed -i 's#texlive2010#texlive#' "$pkgdir"/usr/share/texmf/web2c/texmfcnf.lua
   ## remove aleph from fmtutil.cnf
   gsed -i -e '/aleph/d' "$pkgdir"/usr/share/texmf/web2c/fmtutil.cnf

   ## install Perl libraries
   mkdir -p "$pkgdir"/usr/share/tlpkg/TeXLive
   install -m755 "${srcdir}/biber" "${pkgdir}/usr/bin/biber"
   install -m644 "${srcdir}"/source/utils/biber/TeXLive/*.pm "${pkgdir}/usr/share/tlpkg/TeXLive"

   # create symlinks for formats 
   echo "--> Create symlinks for TeX formats ..."
   PATH="$PATH:${pkgdir}/usr/bin" texlinks -f "$pkgdir"/usr/share/texmf/web2c/fmtutil.cnf "$pkgdir"/usr/bin/
   #############################################################
   # remove dangling symlinks
   _bibtexextra_scripts="
bibexport
listbib
urlbst
"
   _core_scripts="
afm2afm
arlatex
autoinst
bundledoc
checkcites
cmap2enc
ctanify
ctanupload
de-macro
dosepsbin
dviasm
epstopdf
findhyph
font2afm
fragmaster
installfont-tl
latex2man
latexdiff
latexdiff-vc
latexfileversion
latexmk
latexrevise
listings-ext.sh
match_parens
mf2pt1
mkjobtexmf
mkluatexfontdb
mkt1font
mptopdf
ot2kpx
pdf180
pdf270
pdf90
pdfatfi
pdfbook
pdfcrop
pdfflip
pdfjam
pdfjam-pocketmod
pdfjam-slides3up
pdfjam-slides6up
pdfjoin
pdfnup
pdfpun
pfm2kpx
pkfix
pkfix-helper
purifyeps
repstopdf
rpdfcrop
showglyphs
sty2dtx
texcount
texdef
texdiff
texdirflatten
texliveonfly
texloganalyser
typeoutfileinfo
thumbpdf
vpl2ovp
vpl2vpl
"
   _htmlxml_scripts="ht htcontext htlatex htmex httex httexi htxelatex htxetex mk4ht"
   _langcjk_scripts="convbkmk updmap-setup-kanji"
   _langextra_scripts="ebong"
   _langgreek_scripts="mkgrkindex"
   _latexextra_scripts="
authorindex
exceltex
makeglossaries
pdfannotextractor
perltex
ppower4
ps4pdf
splitindex
svn-multi
vpe"
   _music_scripts="m-tx musixtex musixflx pmx2pdf"
   _pictures_scripts="cachepic epspdf epspdftk fig4latex mathspic"
   _pstricks_scripts="pedigree pst2pdf"
   _science_scripts="ulqda"
   for s in \
        ${_bibtexextra_scripts} \
        ${_core_scripts}       \
        ${_htmlxml_scripts}    \
        ${_langcjk_scripts}    \
        ${_langextra_scripts}  \
        ${_langgreek_scripts}  \
        ${_latexextra_scripts} \
        ${_music_scripts}      \
        ${_pictures_scripts}   \
        ${_pstricks_scripts}   \
        ${_science_scripts}    \
        tlmgr; do
     rm -f "$pkgdir"/usr/bin/$s
   done
   ###################################################################
}
