# Maintainer: 
# Contributor: John Proctor <jproctor@prium.net>

pkgname=libogg
pkgver=1.3.0
pkgrel=1
pkgdesc="Ogg bitstream and framing library"
arch=('i686' 'x86_64')
url="http://www.xiph.org/ogg/"
license=('BSD')
source=(http://downloads.xiph.org/releases/ogg/$pkgname-$pkgver.tar.xz)
md5sums=('84a35715170f2cd4c77a4448772b95d5')

build() {
  cd "$srcdir/${pkgname}-${pkgver}"
  sed -i '' -e 's@CFLAGS="-O"@CFLAGS=""@' \
            -e 's@CFLAGS="-O20 -fsigned-char"@CFLAGS="-fsigned-char"@' \
            -e 's@PROFILE="-O20 -g -pg -fsigned-char"@CFLAGS="-g -pg -fsigned-char"@' \
            configure
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/${pkgname}-${pkgver}"
  make DESTDIR="$pkgdir" install

# install BSD license
  install -dm755 "$pkgdir/usr/share/licenses/$pkgname/"
  install -m644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"

}
