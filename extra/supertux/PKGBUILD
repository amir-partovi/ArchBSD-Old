# $Id: PKGBUILD 91300 2013-05-20 22:16:28Z foutrelis $
# Contributor: Jaroslaw Swierczynski <swiergot@aur.archlinux.org>
# Contributor: Eric BÃ©langer <eric@archlinux.org>
# Contributor: vande198
# Maintainer: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=supertux
pkgver=0.3.3
pkgrel=1
pkgdesc="A classic 2D jump'n run sidescroller game in a style similar to the original SuperMario games"
arch=('i686' 'x86_64')
url="http://super-tux.sourceforge.net/"
license=('GPL')
depends=('sdl_image' 'curl' 'physfs' 'openal' 'libvorbis' 'glew')
makedepends=('cmake' 'boost' 'mesa' 'optipng')
source=(http://download.berlios.de/supertux/${pkgname}-${pkgver}.tar.bz2
        supertux2-0.3.3-squirrel-gcc47.patch)
md5sums=('f3f803e629ee51a9de0b366a036e393d'
         'eb06315514be4f200428f14b927beb66')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  patch -p1 -i "${srcdir}/supertux2-0.3.3-squirrel-gcc47.patch"
  gsed -i '1i#include <cstddef>' src/supertux/screen_manager.hpp
  gsed -i '/types\.h/d' src/addon/addon_manager.cpp

  # Fix invalid PNG image to work with libpng 1.6
  optipng -quiet -force -fix data/images/creatures/flame_fish/left-0.png
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  cmake -D CMAKE_INSTALL_PREFIX=/usr -D INSTALL_SUBDIR_BIN=bin .
  gmake
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  gmake DESTDIR="${pkgdir}" install
}
