# $Id: PKGBUILD 183686 2013-04-26 16:05:42Z heftig $
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgbase=gst-plugins-base
pkgname=('gst-plugins-base-libs' 'gst-plugins-base')
pkgver=1.0.7
pkgrel=2
pkgdesc="GStreamer Multimedia Framework Base Plugins"
arch=('i686' 'x86_64')
license=('LGPL')
makedepends=('pkgconf' 'gstreamer' 'orc' 'libxv' 'alsa-lib' 'cdparanoia' 'libvisual' 'libvorbis' 'libtheora' 'pango' 'gobject-introspection')
options=(!libtool !emptydirs)
url="http://gstreamer.freedesktop.org/"
source=(${url}/src/$pkgbase/$pkgbase-${pkgver}.tar.xz)
sha256sums=('014805e50b696bc06c3862ea656df079fc0b5fef0c10f16e9f085f290545677a')

build() {
  cd $pkgbase-$pkgver
  gsed -i -e '/AC_PATH_XTRA/d' configure.ac
  aclocal -I m4 -I common/m4
  autoconf
  automake --add-missing
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --disable-static --enable-experimental \
    --with-package-name="GStreamer Base Plugins (ArchBSD)" \
    --with-package-origin="http://www.archbsd.net/"
  gmake
  gsed -e 's/^SUBDIRS_EXT =.*/SUBDIRS_EXT =/' -i Makefile
}

check() {
  cd $pkgbase-$pkgver
  # Testsuite fails on one test. Some refcount leak
  gmake check || true
}

package_gst-plugins-base-libs() {
  pkgdesc="GStreamer Multimedia Framework Base Plugin libraries"
  depends=('gstreamer' 'orc' 'libxv')

  cd $pkgbase-$pkgver
  gmake DESTDIR="${pkgdir}" install
}

package_gst-plugins-base() {
  depends=("gst-plugins-base-libs=$pkgver" 'alsa-lib' 'cdparanoia' 'libvisual' 'libvorbis' 'libtheora' 'pango')

  cd $pkgbase-$pkgver
  gmake -C gst-libs DESTDIR="${pkgdir}" install
  gmake -C ext DESTDIR="${pkgdir}" install
  gmake -C gst-libs DESTDIR="${pkgdir}" uninstall
}
