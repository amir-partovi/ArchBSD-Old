# $Id: PKGBUILD 185569 2013-05-15 08:05:28Z bpiotrowski $
# Maintainer: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Paul Mattal <paul@archlinux.org>
# Contributor: Jason Chu <jason@archlinux.org>
# Contributor: Anthony Donnelly <Amzo@archbsd.net>

pkgname=subversion
pkgver=1.7.9
pkgrel=1
pkgdesc="A Modern Concurrent Version Control System"
arch=('i686' 'x86_64')
license=('APACHE')
depends=('neon' 'apr-util' 'sqlite' 'serf')
optdepends=('python2: for some hook scripts' 'java-environment') #'ruby: for some hook scripts')
makedepends=('apache' 'python2' 'perl' 'java-runtime' 'java-environment'
             'autoconf' 'db')
backup=('etc/conf.d/svnserve')
url="http://subversion.apache.org/"
provides=('svn')
options=('!makeflags' '!libtool' '!emptydirs')
source=(http://apache.mirror.rafal.ca/subversion/$pkgname-$pkgver.tar.bz2 #{,.asc}
        svn
        subversion.rpath.fix.patch
        svnserve.initd2
        svnserve.confd)
md5sums=('8d532025771a67c06c23f299699f056f'
         'a0db6dd43af33952739b6ec089852630'
         'c799a8160852367830d1abc517707fb5'
         'ac39617ae727971088a85ff306b65878'
         '3b46c664a654279a52898fd8ecd68de3')

build() {
   cd "${srcdir}/${pkgname}-${pkgver}"
   export PYTHON=/usr/bin/python2

   patch -Np0 -i ../subversion.rpath.fix.patch
   gsed -i 's|/usr/bin/env python|/usr/bin/env python2|' tools/hook-scripts/{,mailer/{,tests/}}*.py

   ./configure --prefix=/usr --with-apr=/usr --with-apr-util=/usr \
               --with-zlib=/usr --with-neon=/usr --with-serf=/usr --with-apxs \
               --with-sqlite=/usr --with-berkeley-db=:/usr/include/:/usr/lib:db-5.3 \
               --enable-javahl --disable-static

   gmake external-all
   gmake LT_LDFLAGS="-L$Fdestdir/usr/lib" local-all
}

#check() {
#   cd "${srcdir}/${pkgname}-${pkgver}"
#   export LANG=C LC_ALL=C
#   make check check-swig-pl check-swig-py check-javahl CLEANUP=yes # check-swig-rb
#}

package() {
   cd "${srcdir}/${pkgname}-${pkgver}"

   export LD_LIBRARY_PATH="${pkgdir}"/usr/lib:${LD_LIBRARY_PATH}
   gmake DESTDIR="${pkgdir}" INSTALLDIRS=vendor \
     swig_pydir=/usr/lib/python2.7/site-packages/libsvn \
     swig_pydir_extra=/usr/lib/python2.7/site-packages/svn \
     install install-javahl #install-swig-rb

   install -dm755 "${pkgdir}"/usr/share/subversion
   cp -a tools/hook-scripts "${pkgdir}"/usr/share/subversion/
   rm "${pkgdir}"/usr/share/subversion/hook-scripts/*.in

   # ... common config
   install -dm755 "${pkgdir}"/etc/{conf.d,init.d}
   install -m644 "${srcdir}"/svnserve.confd "${pkgdir}"/etc/conf.d/svnserve
   install -m644 "${srcdir}"/svnserve.initd2 "${pkgdir}"/etc/init.d/svnserve
   install -dm755 "${pkgdir}"/usr/share/bash-completion/completions/
   install -m644 tools/client-side/bash_completion \
     "${pkgdir}"/usr/share/bash-completion/completions/subversion
   for i in svn svnadmin svndumpfilter svnlook svnsync svnversion; do
      ln -sf subversion "${pkgdir}"/usr/share/bash-completion/completions/${i}
   done
}
