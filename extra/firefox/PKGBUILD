# Maintainer : Ionut Biru <ibiru@archlinux.org>
# Contributor: Jakub Schmidtke <sjakub@gmail.com>

pkgname=firefox
pkgver=21.0
pkgrel=4
pkgdesc="Standalone web browser from mozilla.org"
arch=('i686' 'x86_64')
license=('MPL' 'GPL' 'LGPL')
depends=('gtk2' 'mozilla-common' 'libxt' 'startup-notification' 'mime-types'
         'libnotify' 'desktop-file-utils' 'hicolor-icon-theme'
         'libvpx' 'libevent' 'nss>=3.14.1.1.93-4' 'hunspell' 'sqlite'
         'dbus-glib')
# nspr can be removed soon, added it because 4.9.4 rel 2 was required for linking since rel 1 lacked some symlinks
makedepends=('unzip' 'zip' 'python2' 'yasm' 'mesa' 'dos2unix'
             'autoconf2.13' 'libidl2' 'xorg-server-xvfb' 'imake' 'nspr>=4.9.4-2')
optdepends=('networkmanager: Location detection via available WiFi networks')
url="http://www.mozilla.org/projects/firefox"
install=firefox.install
options=(!emptydirs)
source=(https://ftp.mozilla.org/pub/mozilla.org/firefox/releases/$pkgver/source/firefox-$pkgver.source.tar.bz2
        mozconfig firefox.desktop vendor.js
        firefox-install-dir.patch
        shared-libs.patch
        firefox-20.0.1-fixed-loading-icon.png
patch-browser-app-nsBrowserApp.cpp patch-bug685258 patch-bug722975 patch-bug780432 patch-bug783463 patch-bug791305 patch-bug803480 patch-bug807492 patch-bug810716 patch-bug824317 patch-bug826985 patch-bug836824 patch-bug844818 patch-bug854936 patch-build-pgo-profileserver.py patch-clang32-libc++-visibility-hack patch-clang33 patch-duckduckgo patch-extensions-spellcheck-hunspell-src-mozHunspell.cpp patch-ipc-chromium-Makefile.in patch-ipc-chromium-src-base-message_pump_libevent.cc patch-js-src-methodjit-MethodJIT.cpp patch-media-libsydneyaudio-src-sydney_audio_oss.c patch-media-webrtc-trunk-src-system_wrappers-source-spreadsortlib-spreadsort.hpp patch-media-webrtc-trunk-testing-gtest-include-gtest-internal-gtest-port.h patch-security_manager_ssl_src_nsNSSComponent.cpp patch-toolkit-components-osfile-osfile_unix_allthreads.jsm patch-toolkit_mozapps_installer_packager_mk patch-toolkit_xre_Makefile.in
)

export CC=gcc
export CPP=cpp
export CXX=g++
prepare() {
  cd mozilla-release
  dos2unix media/webrtc/trunk/webrtc/system_wrappers/source/spreadsortlib/spreadsort.hpp

  cp ../mozconfig .mozconfig
  patch -Np1 -i ../firefox-install-dir.patch
  patch -Np1 -i ../shared-libs.patch

  for i in \
    patch-browser-app-nsBrowserApp.cpp patch-bug685258 patch-bug722975 \
    patch-bug780432 patch-bug783463 patch-bug791305 patch-bug803480 \
    patch-bug807492 patch-bug810716 patch-bug824317 patch-bug826985 \
    patch-bug836824 patch-bug844818 patch-bug854936 \
    patch-build-pgo-profileserver.py patch-clang32-libc++-visibility-hack \
    patch-clang33 patch-duckduckgo \
    patch-extensions-spellcheck-hunspell-src-mozHunspell.cpp patch-ipc-chromium-Makefile.in \
    patch-ipc-chromium-src-base-message_pump_libevent.cc patch-js-src-methodjit-MethodJIT.cpp \
    patch-media-libsydneyaudio-src-sydney_audio_oss.c \
    patch-media-webrtc-trunk-src-system_wrappers-source-spreadsortlib-spreadsort.hpp \
    patch-media-webrtc-trunk-testing-gtest-include-gtest-internal-gtest-port.h \
    patch-security_manager_ssl_src_nsNSSComponent.cpp \
    patch-toolkit-components-osfile-osfile_unix_allthreads.jsm \
    patch-toolkit_mozapps_installer_packager_mk \
    patch-toolkit_xre_Makefile.in
  do
    msg "Patch $i"
    patch -p0 -i "${srcdir}/$i"
  done
  msg "Patching done"

  msg "sed mozpng"
  sed -i '' -e '/MOZPNG/s/=[0-9]*/=10511/' configure.in
  msg "preparing"

  # Fix PRE_RELEASE_SUFFIX
  gsed -i '/^PRE_RELEASE_SUFFIX := ""/s/ ""//' \
    browser/base/Makefile.in

  mkdir "$srcdir/path"

  # WebRTC build tries to execute "python" and expects Python 2
  ln -s /usr/bin/python2 "$srcdir/path/python"

  # configure script misdetects the preprocessor without an optimization level
  # https://bugs.archlinux.org/task/34644
  gsed -i '/ac_cpp=/s/$CPPFLAGS/& -O2/' configure

  # Fix tab loading icon (flickers with libpng 1.6)
  # https://bugzilla.mozilla.org/show_bug.cgi?id=841734
  cp "$srcdir/firefox-20.0.1-fixed-loading-icon.png" \
    browser/themes/gnomestripe/tabbrowser/loading.png
}

build() {
  cd mozilla-release

  if [[ -e /usr/lib/libgcov.a ]]; then
  	  echo "--- /usr/lib/libgcov.a exists - the build would fail"
  	  false
  fi

  export PATH="$srcdir/path:$PATH"
  export LDFLAGS="$LDFLAGS -Wl,-rpath,/usr/lib/firefox"
  export PYTHON="/usr/bin/python2"
  export MOZ_PGO=0

  #export DISPLAY=:99
  #Xvfb -nolisten tcp -extension GLX -screen 0 1280x1024x24 $DISPLAY &

  if ! gmake -f client.mk build MOZ_PGO=1; then
    kill $!
    return 1
  fi

  kill $! || true
}

package() {
  cd mozilla-release
  gmake -j1 -f client.mk DESTDIR="$pkgdir" install

  install -dm755 "$pkgdir/usr/lib/firefox/defaults/preferences/"
  install -m644 ../vendor.js "$pkgdir/usr/lib/firefox/defaults/preferences/vendor.js"

  for i in 16 22 24 32 48 256; do
      install -dm755 "$pkgdir/usr/share/icons/hicolor/${i}x${i}/apps/"
      install -m644 browser/branding/official/default$i.png \
        "$pkgdir/usr/share/icons/hicolor/${i}x${i}/apps/firefox.png"
  done
  install -dm755 "$pkgdir/usr/share/icons/hicolor/64x64/apps"
  install -dm755 "$pkgdir/usr/share/icons/hicolor/128x128/apps"
  install -dm755 "$pkgdir/usr/share/icons/hicolor/210x210/apps"
  install -m644 browser/branding/official/content/icon64.png \
    "$pkgdir/usr/share/icons/hicolor/64x64/apps/firefox.png"
  install -m644 browser/branding/official/mozicon128.png \
    "$pkgdir/usr/share/icons/hicolor/128x128/apps/firefox.png"
  install -m644 browser/branding/official/content/about-logo.png \
    "$pkgdir/usr/share/icons/hicolor/210x210/apps/firefox.png"

  install -dm755 "$pkgdir/usr/share/applications/"
  install -m644 ../firefox.desktop \
    "$pkgdir/usr/share/applications/firefox.desktop"

  # Use system-provided dictionaries
  rm -rf "$pkgdir"/usr/lib/firefox/{dictionaries,hyphenation}
  ln -s /usr/share/hunspell "$pkgdir/usr/lib/firefox/dictionaries"
  ln -s /usr/share/hyphen "$pkgdir/usr/lib/firefox/hyphenation"

  # We don't want the development stuff
  #rm -r "$pkgdir"/usr/{include,lib/firefox-devel,share/idl}

  #workaround for now
  #https://bugzilla.mozilla.org/show_bug.cgi?id=658850
  ln -sf firefox "$pkgdir/usr/lib/firefox/firefox-bin"
}
md5sums=('6e2510e9466b280c367de0e4c05a8840'
         '701ba879ce239185e43ba496ad2bfb6b'
         '5f420c2c401bf0e4f464eed99535a573'
         '0d053487907de4376d67d8f499c5502b'
         '150ac0fb3ac7b2114c8e8851a9e0516c'
         '52e52f840a49eb1d14be1c0065b03a93'
         '6e335a517c68488941340ee1c23f97b0'
         '4cf8f6260c356f02d2fc2df39337c538'
         'bb6af6766de701a95411e07f8d615bd1'
         '87d9839cc325c2506e70221fef2216c8'
         '1ea8adc6c4530524f42dd50825201da3'
         '45ecf83ecba252a5753601294574bb9e'
         '2b87e42a3d9fefa812f9d2799d61afd6'
         '38bcef5df8cb1ef426ce8fe23fcf0309'
         '3d6ee7ed6de1778d690216b2114dfaa8'
         'a0421e160d425bdaedd154a4a683303f'
         '3aefbbe106ddf535cf299e0ee1fc240b'
         '66886509b2f000ba32b269681fbed237'
         '3113bdcaa932861548042b293ab3eb92'
         '5c842ef10a8ee2be6c14d93fe02e423d'
         '44d9735b8b512b92fbd611639f4d68f8'
         '3e42c79b3a9614031a78a5282172fdc5'
         'b6fcd64840088c25e51746c9f9c4681c'
         '849b8131aae0252b438e15aadd35c374'
         'ce3b9bff03416b214b2de63b17afb109'
         '24925c94aed511f9c34605d47ed81643'
         '1c85f1aa3ce4f114531ce79270fbbe59'
         '5b01fd680ff878eaf8bedab84c696778'
         '274f4b93fb0f65792aefca3fe2bb2905'
         'a4e2412316aed1ee683f22a8aeb59c6d'
         'b98c17d23a29329784559959ee2cb7ed'
         'b59a5f9544e3d9bc213e4d96777a2b25'
         '0f507094a1ed60e7cb2e721bce78d813'
         'f7e2922d2fd993959aacce47ffe4c738'
         'ef23be40121ebb95970cdea623bffc13'
         'cf7230ffe380e8f9748fd78d976f3f19')
