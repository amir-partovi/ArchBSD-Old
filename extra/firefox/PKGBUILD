# Maintainer : Ionut Biru <ibiru@archlinux.org>
# Contributor: Jakub Schmidtke <sjakub@gmail.com>

pkgname=firefox
#pkgver=18.0.2
pkgver=20.0.1
pkgrel=2
pkgdesc="Standalone web browser from mozilla.org"
arch=('i686' 'x86_64')
license=('MPL' 'GPL' 'LGPL')
depends=('gtk2' 'libxt' 'startup-notification' 'mime-types'
         'libnotify' 'desktop-file-utils' 'hicolor-icon-theme'
         'libvpx' 'libevent' 'nss>=3.14.1.1.93-4' 'hunspell' 'sqlite'
         'dbus-glib')
# nspr can be removed soon, added it because 4.9.4 rel 2 was required for linking since rel 1 lacked some symlinks
makedepends=('unzip' 'zip' 'python2' 'yasm' 'mesa' 'dos2unix'
             'autoconf2.13' 'libidl2' 'xorg-server-xvfb' 'imake' 'nspr>=4.9.4-2')
optdepends=('networkmanager: Location detection via available WiFi networks')
url="http://www.mozilla.org/projects/firefox"
install=firefox.install
options=(!emptydirs)
source=(https://ftp.mozilla.org/pub/mozilla.org/firefox/releases/$pkgver/source/firefox-$pkgver.source.tar.bz2
        mozconfig firefox.desktop vendor.js firefox-install-dir.patch
patch-browser-app-nsBrowserApp.cpp patch-bug685258 patch-bug722975 patch-bug780432 patch-bug783463 patch-bug791305 patch-bug803480 patch-bug807492 patch-bug810716 patch-bug824317 patch-bug826985 patch-bug827521 patch-bug828003 patch-bug836824 patch-bug844818 patch-build-pgo-profileserver.py patch-clang32-libc++-visibility-hack patch-duckduckgo patch-extensions-spellcheck-hunspell-src-mozHunspell.cpp patch-ipc-chromium-Makefile.in patch-ipc-chromium-src-base-message_pump_libevent.cc patch-js-src-methodjit-MethodJIT.cpp patch-media-libsydneyaudio-src-sydney_audio_oss.c patch-media-webrtc-trunk-src-system_wrappers-source-spreadsortlib-spreadsort.hpp patch-media-webrtc-trunk-testing-gtest-include-gtest-internal-gtest-port.h patch-security_manager_ssl_src_nsNSSComponent.cpp patch-toolkit_mozapps_installer_packager_mk patch-toolkit_xre_Makefile.in
)

build() {
  cd mozilla-release

  export CC=gcc
  export CPP=cpp
  export CXX=g++

  dos2unix media/webrtc/trunk/src/system_wrappers/source/spreadsortlib/spreadsort.hpp

  cp ../mozconfig .mozconfig
  patch -p1 -i "${srcdir}/firefox-install-dir.patch"
  for i in patch-browser-app-nsBrowserApp.cpp patch-bug685258 patch-bug722975 \
           patch-bug780432 patch-bug783463 patch-bug791305 patch-bug803480 patch-bug807492 \
           patch-bug810716 patch-bug824317 patch-bug826985 patch-bug827521 patch-bug828003 \
           patch-bug836824 patch-bug844818 patch-build-pgo-profileserver.py \
           patch-clang32-libc++-visibility-hack patch-duckduckgo \
           patch-extensions-spellcheck-hunspell-src-mozHunspell.cpp patch-ipc-chromium-Makefile.in \
           patch-ipc-chromium-src-base-message_pump_libevent.cc patch-js-src-methodjit-MethodJIT.cpp \
           patch-media-libsydneyaudio-src-sydney_audio_oss.c \
           patch-media-webrtc-trunk-src-system_wrappers-source-spreadsortlib-spreadsort.hpp \
           patch-media-webrtc-trunk-testing-gtest-include-gtest-internal-gtest-port.h \
           patch-security_manager_ssl_src_nsNSSComponent.cpp patch-toolkit_mozapps_installer_packager_mk \
           patch-toolkit_xre_Makefile.in
  do
    msg "Patch $i"
    patch -p0 -i "${srcdir}/$i"
  done
  msg "Patching done"

  msg "sed 1"
  sed -i '' -e '/MOZPNG/s/=[0-9]*/=10511/' configure.in
  msg "sed 2"
  sed -i '' -e 's|%%LOCALBASE%%|/usr|g' \
      browser/app/nsBrowserApp.cpp \
      extensions/spellcheck/hunspell/src/mozHunspell.cpp
  msg "preparing"

  # Fix PRE_RELEASE_SUFFIX
  sed -i '' -e '/^PRE_RELEASE_SUFFIX := ""/s/ ""//' \
    browser/base/Makefile.in

  # WebRTC build tries to execute "python" and expects Python 2
  # Workaround taken from chromium PKGBUILD
  mkdir -p "$srcdir/python2-path"
  ln -sf /usr/bin/python2 "$srcdir/python2-path/python"
  export PATH="$srcdir/python2-path:$PATH"

  export LDFLAGS="$LDFLAGS -L/usr/lib -Wl,-rpath,/usr/lib/firefox"
  export PYTHON="/usr/bin/python2"
  export MOZ_MAKE_FLAGS="$MAKEFLAGS"
  unset MAKEFLAGS

  _fail=0
  # Enable PGO
#  export MOZ_PGO=0
#  export DISPLAY=:99
#  Xvfb -nolisten tcp -extension GLX -screen 0 1280x1024x24 $DISPLAY &
#  gmake -f client.mk build MOZ_PGO=1 || _fail=1
#  kill $! || true

# instead:
  gmake -f client.mk build || _fail=1

  return $_fail
}

package() {
  cd mozilla-release
  gmake -j1 -f client.mk DESTDIR="$pkgdir" install

  install -dm755 "$pkgdir/usr/lib/firefox/defaults/preferences/"
  install -m644 ../vendor.js "$pkgdir/usr/lib/firefox/defaults/preferences/vendor.js"

  for i in 16 22 24 32 48 256; do
      install -dm755 "$pkgdir/usr/share/icons/hicolor/${i}x${i}/apps/"
      install -m644 browser/branding/official/default$i.png \
        "$pkgdir/usr/share/icons/hicolor/${i}x${i}/apps/firefox.png"
  done

  install -dm755 "$pkgdir/usr/share/applications/"
  install -m644 ../firefox.desktop \
    "$pkgdir/usr/share/applications/firefox.desktop"

  # Use system-provided dictionaries
  rm -rf "$pkgdir"/usr/lib/firefox/{dictionaries,hyphenation}
  ln -s /usr/share/hunspell "$pkgdir/usr/lib/firefox/dictionaries"
  ln -s /usr/share/hyphen "$pkgdir/usr/lib/firefox/hyphenation"

  # We don't want the development stuff
  #rm -r "$pkgdir"/usr/{include,lib/firefox-devel,share/idl}

  #workaround for now
  #https://bugzilla.mozilla.org/show_bug.cgi?id=658850
  ln -sf firefox "$pkgdir/usr/lib/firefox/firefox-bin"
}
md5sums=('b822ff4b2348410587dec563235d9320'
         'a152683a43b3ab4e8e5a4f57a852f40e'
         '5f420c2c401bf0e4f464eed99535a573'
         '0d053487907de4376d67d8f499c5502b'
         '150ac0fb3ac7b2114c8e8851a9e0516c'
         'ad3cdc5c900d34750b8e4e996443e3d1'
         'bb6af6766de701a95411e07f8d615bd1'
         '87d9839cc325c2506e70221fef2216c8'
         '1ea8adc6c4530524f42dd50825201da3'
         '45ecf83ecba252a5753601294574bb9e'
         '2b87e42a3d9fefa812f9d2799d61afd6'
         '38bcef5df8cb1ef426ce8fe23fcf0309'
         'ed1c3637a63cf8d132a948524fd8228a'
         'a0421e160d425bdaedd154a4a683303f'
         '6a5a18c058acd62bbdb619ffe0361b0e'
         'ef8e58576cb8e44b314caff1b5b75690'
         '05af06766acc3d7256b878c95b362276'
         '79b2a0233be1b545a3f074edc7e1a580'
         '3113bdcaa932861548042b293ab3eb92'
         '0ecf50993afdd67cdfb9160de51314bc'
         'ff8b2616b55bfe9dbe7cea1e22c840a4'
         'b6fcd64840088c25e51746c9f9c4681c'
         'ce3b9bff03416b214b2de63b17afb109'
         '0a056ef938303f0deffe0cecf617fcbd'
         '1c85f1aa3ce4f114531ce79270fbbe59'
         '5b01fd680ff878eaf8bedab84c696778'
         '274f4b93fb0f65792aefca3fe2bb2905'
         'a4e2412316aed1ee683f22a8aeb59c6d'
         'bacd16b319e84d53abfa06835ca7b1e9'
         'b59a5f9544e3d9bc213e4d96777a2b25'
         '0f507094a1ed60e7cb2e721bce78d813'
         'ef23be40121ebb95970cdea623bffc13'
         'cf7230ffe380e8f9748fd78d976f3f19')
