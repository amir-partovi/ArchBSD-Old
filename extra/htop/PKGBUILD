# $Id: PKGBUILD 173197 2012-12-12 17:29:21Z heftig $
# Maintainer: Angel Velasquez <angvp@archlinux.org> 
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=htop
pkgver=1.0.2
pkgrel=2
pkgdesc="Interactive process viewer"
arch=('i686' 'x86_64')
url="http://htop.sourceforge.net/"
license=('GPL')
depends=('ncurses')
makedepends=('python2')
optdepends=('lsof: show files opened by a process'
            'strace: attach to a running process')
options=('!emptydirs')
source=("http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz"
        tree-crash.patch
        ioprio.patch)

build() {
  cd "$pkgname-$pkgver"

  gsed -i'' 's|ncursesw/curses.h|curses.h|' RichString.[ch]
  gsed -i'' 's|ncursesw/curses.h|curses.h|' configure
  gsed -i'' 's|python|python2|' scripts/MakeHeader.py

  # Boost field buffer size - crashes when trying to draw very deep UTF-8 trees
  # Test by nesting 30 shells
  patch -N -i ../tree-crash.patch
  patch -N -i ../ioprio.patch

  sed -i '' -e "/^ltmain=/!s|\$$ac_aux_dir/ltmain.sh|/usr/share/libtool/config/ltmain.sh|g" \
            -e "/^LIBTOOL=/s|\$$\(top_builddir\)/libtool|/usr/bin/libtool|g" \
            ${srcdir}/${pkgname}-${pkgver}/aclocal.m4
  find ${srcdir}/${pkgname}-${pkgver} -name "Makefile.in" | xargs sed -i '' -e 's|^LIBTOOL[ ]*=.*|LIBTOOL=/usr/bin/libtool|g'

  ./configure \
      --prefix=/usr \
      --with-proc=/compat/linux/proc \
      --enable-unicode \
      #--enable-openvz \
      #--enable-vserver \
      #--enable-cgroup

  make
}

package() {
  make -C "$pkgname-$pkgver" DESTDIR="$pkgdir" install
}
md5sums=('0d01cca8df3349c74569cefebbd9919e'
         '48eba3c0303bfd19d761b859bc69d713'
         '77367a18e898d821b77938d451061d01')
