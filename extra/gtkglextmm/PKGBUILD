# $Id: PKGBUILD 168604 2012-10-13 11:47:19Z andyrtr $
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Bastien Bouclet <bastien.bouclet@gmail.com>
# Contributor: SpepS <dreamspepser at yahoo dot it>

pkgname=gtkglextmm
pkgver=1.2.0
pkgrel=10
pkgdesc="Cpp C++ bindings for gtkglext"
arch=('i686' 'x86_64')
url="http://gtkglext.sourceforge.net/"
depends=('gtkmm' 'gtkglext')
makedepends=('mesa')
options=('!libtool')
license=('GPL')
source=("http://downloads.sourceforge.net/sourceforge/gtkglext/${pkgname}-${pkgver}.tar.bz2"
        'gdkspanfunc.patch')
md5sums=('27c05f4d45c5fd07b6fb0f044add3056'
         'd7c8ea33c748a6857ee629c16b2d9bff')

prepare() {
  cd "${pkgname}-${pkgver}"

  # FS#24706
  patch -Np1 -i "${srcdir}/gdkspanfunc.patch"

  # automake 1.8 fix
  gsed -i "s|\(AC_DEFUN(\)\(AC_.*_MULTIHEAD\)|\1[\2]|" m4macros/gtkglextmm.m4

  sed -i '' -e "/^ltmain=/!s|\$$ac_aux_dir/ltmain.sh|/usr/share/libtool/config/ltmain.sh|g" \
      -e "/^LIBTOOL=/s|\$$\(top_builddir\)/libtool|/usr/bin/libtool|g" \
      ${srcdir}/${pkgname}-${pkgver}/aclocal.m4
  find ${srcdir}/${pkgname}-${pkgver} -name "Makefile.in" | xargs sed -i '' -e 's|^LIBTOOL[ ]*=.*|LIBTOOL=/usr/bin/libtool|g'

  find ${srcdir}/${pkgname}-${pkgver} -name "configure" -type f | xargs sed -i '' -e 's|-lpthread|-gthread -pthread|g'

  mkdir bin
  ln -s /usr/bin/gmake bin/make
}

build() {
  cd "${pkgname}-${pkgver}"

  export PATH="${srcdir}/bin:${PATH}"
  ./configure --prefix=/usr \
              --disable-gtkglext-test \
              --disable-static
  gmake
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  gmake DESTDIR="${pkgdir}" install
}
