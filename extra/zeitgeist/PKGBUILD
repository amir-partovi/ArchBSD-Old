# $Id: PKGBUILD 183481 2013-04-21 22:21:01Z heftig $
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: György Balló <ballogy@freestart.hu>

pkgname=zeitgeist
pkgver=0.9.12
pkgrel=1
pkgdesc="Service logging user activities and events"
arch=(i686 x86_64)
url="http://zeitgeist-project.com/"
license=(GPL2 LGPL2.1)
depends=(sqlite json-glib telepathy-glib gtk3)
makedepends=(intltool gobject-introspection vala raptor python2-rdflib)
provides=(zeitgeist-datahub)
conflicts=(zeitgeist-datahub)
replaces=(zeitgeist-datahub)
options=('!libtool')
source=("https://launchpad.net/$pkgname/${pkgver%.*}/$pkgver/+download/$pkgname-$pkgver.tar.xz"
	'patch-log.vala')
sha256sums=('f9b1b656dcd4da57438289316d44f8960ce0ee4773ea2fbab4bce165330f41cd'
            '7ab94a780e58b77dee7bfc051dab57c1a571410e80efc88d7fa439c3080160dd')

prepare() {
  cd $pkgname-$pkgver
  gsed -i 's/python -/$PYTHON -/' configure configure.ac
}

build() {
  cd $pkgname-$pkgver
  export PYTHON=/usr/bin/python2
  autoreconf -fi
  patch -p0 -i "${srcdir}/patch-log.vala"
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --libexecdir=/usr/lib/$pkgname
  gmake
}

check() {
  cd $pkgname-$pkgver
  # Log test fails in chroot, as we have no X and no proper dbus
  gmake -k check || :
}

package() {
  cd $pkgname-$pkgver
  gmake DESTDIR="$pkgdir" install #zeitgeistdocdir=/usr/share/doc/zeitgeist install
  #install -D -m644 data/zeitgeist-daemon.bash_completion "$pkgdir/usr/share/bash-completion/completions/zeitgeist-daemon"
}
