# $Id: PKGBUILD 183529 2013-04-22 13:10:36Z heftig $
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgname=p11-kit
pkgver=0.18.1
pkgrel=1
pkgdesc="Library to work with PKCS#11 modules"
arch=(i686 x86_64)
url="http://p11-glue.freedesktop.org"
license=('BSD')
depends=('libtasn1')
options=(!libtool)
source=($url/releases/$pkgname-$pkgver.tar.gz{,.sig})
md5sums=('e4ef21592dbd126b62e1a6d4ad65ce5c'
         '8848d9f0826f1d13aa1625c545fba54d')

build() {
  cd $pkgname-$pkgver

  #Hacks
  sed -i '' -e "/^ltmain=/!s|\$$ac_aux_dir/ltmain.sh|/usr/share/libtool/config/ltmain.sh|g" \
  	    -e "/^LIBTOOL=/s|\$$\(top_builddir\)/libtool|/usr/bin/libtool|g" \
		aclocal.m4

  find ${srcdir}/${pkgname}-${pkgver} -name "Makefile.in" | xargs sed -i '' -e 's|^LIBTOOL[ ]*=.*|LIBTOOL=/usr/bin/libtool|g'

  find ${srcdir}/${pkgname}-${pkgver} -name "configure" -type f | xargs sed -i '' -e 's|-lpthread|-pthread|g'

  ./configure --prefix=/usr --sysconfdir=/etc \
    --with-module-path=/usr/lib/pkcs11 \
    --without-trust-paths
  gmake
}

package() {
  cd  $pkgname-$pkgver
  gmake DESTDIR="$pkgdir" install
  install -dm755 $pkgdir/usr/share/licenses/$pkgname/
  install -m644 COPYING $pkgdir/usr/share/licenses/$pkgname/COPYING
}

# vim:set ts=2 sw=2 et:
