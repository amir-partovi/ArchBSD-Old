pkgname=gstreamer0.10
pkgver=0.10.36
pkgrel=2
pkgdesc="GStreamer Multimedia Framework"
arch=('i686' 'x86_64')
license=('LGPL')
url="http://gstreamer.freedesktop.org/"
depends=('libxml2' 'glib2')
makedepends=('intltool' 'pkgconf' 'gobject-introspection')
options=('!libtool')
source=(${url}/src/gstreamer/gstreamer-${pkgver}.tar.xz
	'patch-Makefile.in')
md5sums=('15389c73e091b1dda915279c388b9cb2'
         'c0881cb8361caa1b5ad4e7717c7dec57')

build() {
  cd "${srcdir}/gstreamer-${pkgver}"

  sed -i '' -e 's|-Wmissing-include-dirs||g' "${srcdir}/gstreamer-${pkgver}/configure"
  patch -p0 -i ${srcdir}/patch-Makefile.in

  export PATH=/usr/local/bin:$PATH

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/libexec \
    --with-package-name="GStreamer (ArchBSD)" \
    --with-package-origin="http://www.archbsd.net/" \
    --disable-gtk-doc --disable-static --disable-failing-tests
  gmake
}

package() {
  cd "${srcdir}/gstreamer-${pkgver}"
  gmake DESTDIR="${pkgdir}" install

  #Remove unversioned gst-* binaries to get rid of conflicts
  cd "${pkgdir}/usr/bin"
  for bins in `ls *-0.10`; do
    rm -f ${bins/-0.10/}
  done

}
