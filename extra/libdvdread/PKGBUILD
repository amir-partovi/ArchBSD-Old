# Contributor: Thomas Baechler <thomas@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=libdvdread
pkgver=4.2.0
pkgrel=2
pkgdesc="Provides a simple foundation for reading DVD video disks"
arch=(i686 x86_64)
url="http://dvdnav.mplayerhq.hu"
license=('GPL')
makedepends=('libdvdcss' 'freebsd-world>=9.1-1' 'gmake')
optdepends=('libdvdcss: for decoding encrypted DVDs')
source=(http://dvdnav.mplayerhq.hu/releases/${pkgname}-${pkgver}.tar.bz2
        patch-src.md5.c)
sha256sums=('0bea15da842a4b04a482b009d72dcc6d9c9524ccc1bf67e5748319ec5ada8097'
            '077fe35243b17d17acac68088323608124079d31315c5575fcbc898f24904ce2')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  patch -p0 -i "${srcdir}/patch-src.md5.c"

  # http://lists.mplayerhq.hu/pipermail/dvdnav-discuss/2009-February/000986.html (FS#19324)

  ./autogen.sh --prefix=/usr
  gmake
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  gmake DESTDIR="${pkgdir}" install
}
