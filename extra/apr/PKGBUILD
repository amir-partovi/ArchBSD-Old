# $Id: PKGBUILD 158966 2012-05-13 20:20:53Z jgc $
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=apr
pkgver=1.4.6
pkgrel=2
pkgdesc="The Apache Portable Runtime"
arch=('i686' 'x86_64')
url="http://apr.apache.org/"
options=('!libtool')
license=('APACHE')
source=(http://www.apache.org/dist/apr/apr-${pkgver}.tar.gz)
md5sums=('76cc4457fbb71eefdafa27dba8f511fb')

build() {
  cd "${srcdir}/apr-${pkgver}"

  #Some FreeBSD fixes
  sed -i '' -e  's/void main/int main/' build/apr_network.m4

  # Fix pthread: Please do not remove, else apr-1-config returns wrong values
  sed -i '' -e 's/-lpthread/-pthread/g' \
		build/apr_threads.m4 build/apr_hints.m4

  ./configure --prefix=/usr --includedir=/usr/include/apr-1 \
    --with-installbuilddir=/usr/share/apr-1/build \
    --enable-nonportable-atomics \
    --with-devrandom=/dev/urandom
  make
}

check() {
  cd "${srcdir}/apr-${pkgver}"
  make -j1 check
}

package() {
  cd "${srcdir}/apr-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
