pkgname=consolekit
pkgver=0.4.5
pkgrel=2
pkgdesc="A framework for defining and tracking users, login sessions, and seats"
arch=('i686' 'x86_64')
url="http://www.freedesktop.org/wiki/Software/ConsoleKit"
license=('GPL')
depends=('polkit')
makedepends=('pkgconf' 'libxslt' 'python2')
options=('!libtool')
install=consolekit.install
source=(http://www.freedesktop.org/software/ConsoleKit/dist/ConsoleKit-$pkgver.tar.bz2
	patch-src_ck-sysdeps-freebsd.c
	patch-src_ck-sysdeps-unix.c
	patch-src_main.c
	patch-src_test-vt-monitor.c
	patch-tools_Makefile.in
	patch-tools_ck-collect-session-info.c
	patch-tools_ck-log-system-start.c)
md5sums=('f2657f93761206922d558471a936fbc3'
         '0e13c3e9e120ab492fdab198d3baed2f'
         '5c86b500c91c7f833ba85dcd5f1d92e3'
         '879ecbf9f0dd6b1738ae8a63525d61c1'
         'f4f788c72ee1fd12efdf679d82986469'
         '146f0c1d9363ea4d5a2b13dcfb6d7d00'
         'fb1e4dee9947201aff51aa2a78bd61a5'
         '9a7f0f98a5afa4474b74b21f2f600032')
build() {
  cd "${srcdir}/ConsoleKit-$pkgver"
 
  for patch in patch-src_ck-sysdeps-freebsd.c \
	patch-src_ck-sysdeps-unix.c \
	patch-src_main.c \
	patch-src_test-vt-monitor.c \
	patch-tools_Makefile.in \
	patch-tools_ck-collect-session-info.c \
	patch-tools_ck-log-system-start.c; do
  patch -p0 -i "${srcdir}/${patch}"
  done

  ./configure --with-pid-file=/var/run/${pkgname}.pid \
	      --localstatedir=/var --prefix=/usr

  gmake

}

package() {
  cd "${srcdir}/ConsoleKit-$pkgver"
  gmake DESTDIR=$pkgdir install
}
