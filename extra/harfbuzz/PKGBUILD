# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Wolfgang Bumiller <blub@speed.at>

pkgbase=(harfbuzz)
pkgname=(harfbuzz harfbuzz-icu)
pkgver=0.9.18
pkgrel=1
pkgdesc="OpenType text shaping engine."
arch=(i686 x86_64)
url="http://www.freedesktop.org/wiki/Software/HarfBuzz"
license=(MIT)
makedepends=('glib2>=2.34.3' freetype2 'graphite>=1.2.0' cairo icu)
optdepends=('cairo: hb-view program')
options=(!libtool)
source=(http://www.freedesktop.org/software/harfbuzz/release/${pkgbase}-${pkgver}.tar.bz2)
sha256sums=('fef0fbbc6d6492cb4074a5e26a699d87d88bef8adc25dda62af6077735a7d908')

build() {
  cd $pkgbase-$pkgver
  ./configure --prefix=/usr \
    --with-glib --with-freetype --with-cairo --with-icu --with-graphite2
  gmake
}

package_harfbuzz() {
  depends=(glib2 freetype2 graphite)
  optdepends=('cairo: hb-view program')

  cd $pkgbase-$pkgver
  gmake DESTDIR="$pkgdir" install
  install -dm755 "$pkgdir/usr/share/licenses/harfbuzz"
  install -m644 COPYING "$pkgdir/usr/share/licenses/harfbuzz/COPYING"

# Split harfbuzz-icu
  mkdir -p ../hb-icu/usr/{include/harfbuzz,lib/pkgconfig}; cd ../hb-icu
  mv "$pkgdir"/usr/lib/libharfbuzz-icu* ./usr/lib
  mv "$pkgdir"/usr/lib/pkgconfig/harfbuzz-icu.pc ./usr/lib/pkgconfig
  mv "$pkgdir"/usr/include/harfbuzz/hb-icu.h ./usr/include/harfbuzz
}

package_harfbuzz-icu(){
  pkgdesc="$pkgdesc (ICU integration)"
  depends=(harfbuzz icu)

  mv hb-icu/* "$pkgdir"

  install -dm755 "$pkgdir/usr/share/licenses/harfbuzz-icu"
  install -m644 $pkgbase-$pkgver/COPYING "$pkgdir/usr/share/licenses/harfbuzz-icu/COPYING"
}
