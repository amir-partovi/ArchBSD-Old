# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Alexander Baldeck <alexander@archlinux.org>

pkgname=xorg-xinit
pkgver=1.3.2
pkgrel=3
pkgdesc="X.Org initialisation program "
arch=('i686' 'x86_64')
license=('custom')
url="http://xorg.freedesktop.org/"
depends=('libx11' 'xorg-xauth')
groups=('xorg')
makedepends=('xorg-util-macros')
optdepends=('xorg-twm' 'xterm')
backup=('etc/skel/.xinitrc'
        'etc/skel/.xsession'
        'etc/X11/xinit/xserverrc'
        'etc/X11/xinit/xinitrc')
source=(${url}/releases/individual/app/xinit-${pkgver}.tar.bz2
        fs25361.patch
        xinitrc
        xsession
        xserverrc)
options=(!emptydirs)
md5sums=('9c0943cbd83e489ad1b05221b97efd44'
         '73a597d3fb7f5b9487318ac8cfd4bedf'
         'c7b406c3c5c1e8011b3ac7d6b29c1a2c'
         '6c0f59472e3cc99b964b1062480f8cbb'
         '4deba1b7f2cc7f93b304b956023e65d6')

build() {
  cd "${srcdir}/xinit-${pkgver}"
  patch -Np1 -i "${srcdir}/fs25361.patch"

  sed -i '' -e 's/test.*-traditional.*;/true;/' configure
  sed -i '' -e 's|hexdump|/usr/bin/hexdump|' startx.cpp

  sed -i -e 's/XSLASHGLOB.sh/XSLASHGLOB/' xinitrc.cpp
  ./configure --prefix=/usr --with-xinitdir=/etc/X11/xinit
  make
}

package() {
  cd "${srcdir}/xinit-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/etc/skel"
  install -m644 "${srcdir}/xinitrc" "${pkgdir}/etc/skel/.xinitrc"
  install -m644 "${srcdir}/xsession" "${pkgdir}/etc/skel/.xsession"
  install -m644 "${srcdir}/xserverrc" "${pkgdir}/etc/X11/xinit/xserverrc"
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
