pkgname=docbook-xsl
pkgver=1.77.1
pkgrel=5
pkgdesc='XML stylesheets for Docbook-xml transformations'
arch=('any')
license=('custom')
url='http://docbook.sourceforge.net/'
depends=('libxml2' 'libxslt' 'docbook-xml')
install="$pkgname.install"
source=("http://downloads.sourceforge.net/docbook/${pkgname}-${pkgver}.tar.bz2")

package() {
	cd ${srcdir}/${pkgname}-${pkgver}

	_pkgroot=${pkgdir}/usr/share/xml/docbook/xsl-stylesheets-${pkgver}

	install -m755 -d ${_pkgroot}
	install -m644 VERSION VERSION.xsl ${_pkgroot}

	for fn in assembly common eclipse epub epub3 fo highlighting html htmlhelp javahelp lib \
	          manpages params profiling roundtrip template website xhtml xhtml-1_1 xhtml5; do
		install -m755 -d ${_pkgroot}/${fn}
		install -m644 "${fn}"/*.xml "${_pkgroot}"/${fn}/ || true  # ignore missing files
                install -m644 "${fn}"/*.xsl "${_pkgroot}"/${fn}/ || true  # ignore missing files
                install -m644 "${fn}"/*.dtd "${_pkgroot}"/${fn}/ || true  # ignore missing files
                install -m644 "${fn}"/*.ent "${_pkgroot}"/${fn}/ || true  # ignore missing files
	done

	install -m755 -d ${pkgdir}/etc/xml
	install -m644 -d ${pkgdir}/usr/share/licenses/${pkgname}/
	install -m644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
md5sums=('a88dbdc0cd74702656083b6e4e4241d8')
