# $Id: PKGBUILD 171997 2012-11-25 21:50:28Z bisson $
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=fontforge
pkgver=20120731_b
pkgrel=6
pkgdesc='Outline and bitmap font editor'
arch=('i686' 'x86_64')
url='http://fontforge.sourceforge.net/'
license=('BSD')
depends=('libxkbui' 'libxi' 'libxml2' 'pango' 'giflib' 'libtiff' 'python2' 'libspiro'
         'desktop-file-utils' 'gtk-update-icon-cache' 'hicolor-icon-theme')
options=('!libtool' '!makeflags')
source=("http://downloads.sourceforge.net/project/fontforge/fontforge-source/fontforge_full-${pkgver//_/-}.tar.bz2")
sha1sums=('b520f532b48e557c177dffa29120225066cc4e84')

# git clone git://fontforge.git.sourceforge.net/gitroot/fontforge/fontforge; cd fontforge; git archive --prefix=${pkgname}-${pkgver}/ master | xz > ../${pkgname}-${pkgver}.tar.xz
# source=("ftp://ftp.archlinux.org/other/${pkgname}/${pkgname}-${pkgver}.tar.xz")

install=install

build() {
	cd "${srcdir}/${pkgname}-${pkgver//_/-}"
	gsed -i 's/python /python2 /g' Makefile.dynamic.in
	./configure \
		--prefix=/usr \
		--mandir=/usr/share/man \
		--enable-type3 \
		--enable-devicetables \
		--enable-double \
		--with-regular-link \
		--with-python=python2 \
		--enable-pyextension \
		--with-freetype-src=/home/wry/ArchBSD/extra/freetype2/src/freetype-2.4.11

	gmake
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver//_/-}"
	gmake DESTDIR="${pkgdir}" install
	install -d "${pkgdir}/usr/share/icons"
	cp -a Packaging/icons "${pkgdir}/usr/share/icons/hicolor"
	rm -fr "${pkgdir}/usr/share/icons/hicolor/src"
	install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}"
	install -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	install -dm755 "${pkgdir}/usr/share/applications"
	install -m644 Packaging/fontforge.desktop "${pkgdir}/usr/share/applications/fontforge.desktop"
}
