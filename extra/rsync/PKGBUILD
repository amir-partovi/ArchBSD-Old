# Contributor: Wolfgang Bumiller <blub at speed dot at>

pkgname=rsync
pkgver=3.0.9
pkgrel=5
pkgdesc="A file transfer program to keep remote files in sync"
arch=('i686' 'x86_64')
url="http://samba.anu.edu.au/rsync/"
license=('GPL3')
depends=('perl')
backup=('etc/rsyncd.conf')
source=("http://rsync.samba.org/ftp/rsync/$pkgname-$pkgver.tar.gz"
        'rsyncd.conf' 'rsyncd')

build() {
	cd "$srcdir/$pkgname-$pkgver"

	sed -i '' -e 's|CFLAGS|CONFIGUREDCFLAGS|' \
	          -e 's|perl.*||' \
	          Makefile.in

	sed -i '' -e 's|malloc.h|stdlib.h|g' rsync.h zlib/zutil.h

	./configure --prefix=/usr \
	            --disable-debug --enable-ipv6 \
	            --with-rsyncd-conf=/etc/rsyncd.conf \
	            --with-included-popt
	make
}

check() {
	cd "$srcdir/$pkgname-$pkgver"
	make test
}

package() {
	cd "$srcdir/$pkgname-$pkgver"
	make DESTDIR="$pkgdir" install
	install -dm755 "${pkgdir}/etc/init.d"

	install -m755 ../rsyncd      "$pkgdir/etc/init.d/rsyncd"

	install -m644 ../rsyncd.conf "$pkgdir/etc/rsyncd.conf"
	install -dm755 "$pkgdir/usr/lib/rsync"
	install -m755 support/rrsync "$pkgdir/usr/lib/rsync/rrsync"
}
md5sums=('5ee72266fe2c1822333c407e1761b92b'
         '14bc303db10da09425e165661a47d7c3'
         '0da75a8f90e7ce24efda2b9343ada481')
