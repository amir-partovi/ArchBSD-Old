# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgbase=imagemagick
pkgname=('imagemagick' 'imagemagick-doc')
pkgver=6.8.1.9
pkgrel=3
arch=('i686' 'x86_64')
url="http://www.imagemagick.org/"
license=('custom')
makedepends=('libltdl' 'lcms2' 'libxt' 'fontconfig' 'libxext' 'ghostscript' \
             'openexr' 'libwmf' 'librsvg' 'libxml2' 'jasper' 'liblqr')
source=(ftp://ftp.sunet.se/pub/multimedia/graphics/ImageMagick/ImageMagick-${pkgver%.*}-${pkgver##*.}.tar.xz \
        perlmagick.rpath.patch
        patch-coders__jp2.c patch-magick-fourier.c
        )
sha1sums=('3d5178ae2b2d819d5f7923031f0f3602a12adf96'
          '23405f80904b1de94ebd7bd6fe2a332471b8c283'
          'a4158fce46cf970ff7c865136b7de18a54f8db34'
          '5554458d03cc31332d7e0f812bcf2048719bef3e')

prepare() {
  cd "${srcdir}"/ImageMagick-${pkgver%.*}-${pkgver##*.}

  patch -p0 -i "$srcdir"/patch-coders__jp2.c
  patch -p0 -i "$srcdir"/patch-magick-fourier.c

  sed -i '' -e '/AC_PATH_XTRA/d' configure.ac
  autoreconf --force --install
  patch -p0 -i ../perlmagick.rpath.patch
}

build() {
  cd "${srcdir}"/ImageMagick-${pkgver%.*}-${pkgver##*.}

  ./configure --prefix=/usr --sysconfdir=/etc --with-modules \
    --disable-openmp --with-wmf --with-openexr --with-xml --with-lcms2 --with-jp2 \
    --with-gslib --with-gs-font-dir=/usr/share/fonts \
    --with-perl --with-perl-options="INSTALLDIRS=vendor" --with-lqr --with-rsvg \
    --without-gvc --without-djvu --without-autotrace --without-webp \
    --without-jbig --without-fpx --without-dps --without-fftw
  gmake
}

check() {
  cd "${srcdir}"/ImageMagick-${pkgver%.*}-${pkgver##*.}
  gmake check
}

package_imagemagick() {
  pkgdesc="An image viewing/manipulation program"
  depends=('perl' 'libltdl' 'lcms2' 'libxt' 'fontconfig' 'libxext' 'liblqr')
  optdepends=('ghostscript: for Ghostscript support' 
              'openexr: for OpenEXR support' 
              'libwmf: for WMF support' 
              'librsvg: for SVG support' 
              'libxml2: for XML support' 
              'jasper: for JPEG-2000 support' 
              'libpng: for PNG support')
  backup=('etc/ImageMagick/coder.xml'
          'etc/ImageMagick/colors.xml'
          'etc/ImageMagick/delegates.xml'
          'etc/ImageMagick/log.xml'
          'etc/ImageMagick/magic.xml'
          'etc/ImageMagick/mime.xml'
          'etc/ImageMagick/policy.xml'
          'etc/ImageMagick/quantization-table.xml'
          'etc/ImageMagick/thresholds.xml'
          'etc/ImageMagick/type.xml'
          'etc/ImageMagick/type-dejavu.xml'
          'etc/ImageMagick/type-ghostscript.xml'
          'etc/ImageMagick/type-windows.xml')
  options=('!docs' 'libtool' '!emptydirs')

  cd "${srcdir}"/ImageMagick-${pkgver%.*}-${pkgver##*.}
  gmake DESTDIR="${pkgdir}" install
  chmod 755 "${pkgdir}/usr/lib/perl5/vendor_perl/auto/Image/Magick/Magick.so"
  install -dm755 "${pkgdir}/usr/share/licenses/imagemagick/"
  install -m644 LICENSE "${pkgdir}/usr/share/licenses/imagemagick/LICENSE"
  install -dm755 "${pkgdir}/usr/share/licenses/imagemagick/"
  install -m644 NOTICE "${pkgdir}/usr/share/licenses/imagemagick/NOTICE"

#Cleaning
  find "${pkgdir}" -name '*.bs' -delete
  rm -f "${pkgdir}"/usr/lib/*.la
}

package_imagemagick-doc() {
  pkgdesc="The ImageMagick documentation (utilities manuals and libraries API)"

  cd "${srcdir}"/ImageMagick-${pkgver%.*}-${pkgver##*.}
  gmake DESTDIR="${pkgdir}" install-data-html
  install -dm755 "${pkgdir}/usr/share/licenses/imagemagick-doc/"
  install -m644 LICENSE "${pkgdir}/usr/share/licenses/imagemagick-doc/LICENSE"
  install -dm755 "${pkgdir}/usr/share/licenses/imagemagick-doc/"
  install -m644 NOTICE "${pkgdir}/usr/share/licenses/imagemagick-doc/NOTICE"
}
