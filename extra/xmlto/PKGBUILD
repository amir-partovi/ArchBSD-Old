# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Sergej Pupykin <sergej@aur.archlinux.org>
# Contributor: Robert Stoffers <rob1@ubuntu.com>
# Contributor: Wolfgang Bumiller <blub@speed.at>

pkgname=xmlto
pkgver=0.0.25
pkgrel=3
pkgdesc="Convert xml to many other formats"
arch=('i686' 'x86_64')
url="http://cyberelk.net/tim/software/xmlto/"
license=('GPL')
depends=('libxslt' 'perl-yaml-syck' 'perl-test-pod')
makedepends=('docbook-xsl' 'getopt' 'bash')
source=("https://fedorahosted.org/releases/x/m/${pkgname}/${pkgname}-${pkgver}.tar.bz2")
md5sums=('6b6267b1470f8571fe5f63a128970364')

prepare() {
  cd "${pkgname}-${pkgver}"
	
  # remove non-standard type -t
  find . -type f -exec sed -i '' -e 's/type -t/which/;' '{}' '+'
  # remove GNUISM
  sed -i '' -e '/^GEN_MANPAGE/{s/\$</$>/;}' Makefile.in
}

build() {
  cd "$srcdir/${pkgname}-${pkgver}"

  GETOPT=/usr/local/bin/getopt \
  BASH=/usr/bin/bash \
  ./configure --prefix=/usr \
              --mandir=/usr/share/man
  make
}

package() {
  cd "$srcdir/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install
}
