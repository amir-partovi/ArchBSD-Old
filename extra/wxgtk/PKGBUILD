pkgname=wxgtk
pkgver=2.8.12.1
pkgrel=4
pkgdesc="GTK+ implementation of wxWidgets API for GUI"
arch=('i686' 'x86_64')
url="http://wxwidgets.org"
license=('custom:wxWindows')
depends=('gtk2' 'gstreamer0.10-base' 'libgl' 'libxxf86vm' 'libsm' 'sdl')
makedepends=('gstreamer0.10-base-plugins' 'gconf' 'mesa' 'glu' 'libxt')
#source=(http://downloads.sourceforge.net/wxwindows/wxGTK-${pkgver}.tar.bz2)
source=(http://downloads.sourceforge.net/wxpython/wxPython-src-${pkgver}.tar.bz2)
sha1sums=('05688dc03d61631750f5904273122bb40a2115f5')

build() {
  cd "${srcdir}/wxPython-src-${pkgver}"

  export CPPFLAGS="${CPPFLAGS} -I/usr/local/include"

  ./configure --prefix=/usr --libdir=/usr/lib --with-gtk=2 --with-opengl --enable-unicode \
    --enable-graphics_ctx  --disable-optimize --enable-mediactrl --with-regex=builtin \
    --with-libpng=sys --with-libxpm=sys --with-libjpeg=sys --with-libtiff=sys \
    --with-sdl --disable-precomp-headers
  gmake
  gmake -C locale allmo
  cd contrib/src
  gmake
}

package() {
  cd "${srcdir}/wxPython-src-${pkgver}"
  gmake DESTDIR="${pkgdir}" install
  cd contrib/src
  gmake DESTDIR="${pkgdir}" install
  install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}/"
  install -m644 ../../docs/licence.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
