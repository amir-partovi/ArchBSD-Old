pkgname=vala
pkgver=0.18.1
pkgrel=1
pkgdesc="Compiler for the GObject type system"
arch=('i686' 'x86_64')
url="http://live.gnome.org/Vala"
license=('LGPL')
depends=('glib2')
makedepends=('libxslt')
options=('!libtool')
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz
	patch-vapi_glib-2.0.vapi
	patch-vapi_libpq.vapi
	patch-vapigen_Makefile.in
)
sha256sums=('7f5e2e8ca03f9fa2cf13acaf93bfdc3a7ad0a10b000a337ba9c6bdc021bdca32'
            '6fdd22f0a15359d6ee6829eb9e92fe93804c0a52052c4af4503889f21480ff79'
            'ef456434b627ac4e9ec5c6742e0dd9fb64b02152505e09e6f9e4c42e6c4aa334'
            '6b1d3c863617fc5078641d326a0b5451a9c12d5dccdad0473763a7f401fb9501')

build() {
    cd $pkgname-$pkgver

    patch -p0 -i ${srcdir}/patch-vapi_glib-2.0.vapi
    patch -p0 -i ${srcdir}/patch-vapi_libpq.vapi
    patch -p0 -i ${srcdir}/patch-vapigen_Makefile.in

    sed -i ''  -e 's|@vapigen_decl@|vapigen=$${bindir}/vapigen-0.18|g' \
		-e 's|@gen_introspect_decl@|gen_introspect=$${libdir}/vala-0.18/gen-introspect-0.18|g' \
		-e 's|@vala_gen_introspect_decl@|vala_gen_introspect=$${bindir}/vala-gen-introspect-0.18|g' \
		${srcdir}/$pkgname-$pkgver/libvala.pc.in
    sed -i '' -e 's|cp $< $@|cp libvala.pc libvala-0.18.pc|g' \
		${srcdir}/$pkgname-$pkgver/Makefile.in


    ./configure --prefix=/usr/local --disable-vapigen

    make
}

package() {
    cd $pkgname-$pkgver
    make DESTDIR="$pkgdir" install
}
