# $Id: PKGBUILD 174748 2013-01-06 01:52:18Z stephane $
# Maintainer: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Aaron Griffin <aaron@archlinux.org>

pkgname=nasm
pkgver=2.10.07
pkgrel=1
pkgdesc="An 80x86 assembler designed for portability and modularity"
url="http://www.nasm.us"
license=('BSD')
arch=('i686' 'x86_64')
source=(http://www.nasm.us/pub/nasm/releasebuilds/${pkgver}/${pkgname}-${pkgver}.tar.bz2
        patch-output+outelf32.c
        patch-output+outelf64.c
        )
md5sums=('af1cd6669f5e98148cd5ec42dee90f19'
         '39313aa59a6ee46bc88ab99676574264'
         '32a958e48d313de095e94797744efe3a')
install=nasm.install

build() {
  cd "${srcdir}/${pkgbase}-${pkgver}"
  patch -p0 -i "$srcdir"/patch-output+outelf32.c
  patch -p0 -i "$srcdir"/patch-output+outelf64.c
  ./configure --prefix=/usr
  gmake
  gmake -C doc info
}

package() {
  cd "${srcdir}/${pkgbase}-${pkgver}"
  gmake INSTALLROOT="${pkgdir}" install install_rdf
  install -dm755 "${pkgdir}"/usr/share/info
  install -m644 doc/info/* "${pkgdir}"/usr/share/info/
  install -d -m755 "${pkgdir}"/usr/share/licenses/nasm/
  install    -m644 LICENSE "${pkgdir}"/usr/share/licenses/nasm/LICENSE
}
