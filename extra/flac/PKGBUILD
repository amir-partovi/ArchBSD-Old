# $Id: PKGBUILD 135033 2011-08-09 23:31:20Z eric $
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgbase=flac
pkgname=('flac' 'flac-doc')
pkgver=1.2.1
pkgrel=3
arch=('i686' 'x86_64')
url="http://flac.sourceforge.net/"
license=('BSD' 'GPL')
depends=('libogg')
makedepends=('nasm' 'xmms')
options=('!libtool' '!makeflags')
source=(http://downloads.sf.net/sourceforge/${pkgbase}/${pkgbase}-${pkgver}.tar.gz 
	flac-1.2.1-gcc-4.3-includes.patch
	patch-Makefile.in
	patch-configure
	patch-man_flac.1
	patch-src_libFLAC_Makefile.in)
md5sums=('153c8b15a54da428d1f0fadc756c22c7'
         'b9d245422bbc547b18a72897366bea77'
         '04dc626c7847f8215e8aa5a00fbff5f0'
         '8897137e7cf873d0801e321e0089f80a'
         '8f03b8027f5cda92aaa24adced7b87fe'
         '7c6e49881cd16ed17ace2d996a1fede4')
sha1sums=('bd54354900181b59db3089347cc84ad81e410b38'
          '7e9f8c2ef4d9daf5ddd20df63b608fe19b8c1560'
          '08e74aaa9b8831313edbf65d7360777375e75b9e'
          '5f82d9804c94855441f17f641d4830d40737c07e'
          '3d40527fce8c37639f386060f250d8a4f97f2e70'
          '35adaef2a5f1afab8b5f66f0b5321866a3531440')

build() {
  cd "${srcdir}/${pkgbase}-${pkgver}"
  patch -Np1 -i "${srcdir}/flac-1.2.1-gcc-4.3-includes.patch"
  for i in patch-Makefile.in patch-configure patch-man_flac.1 patch-src_libFLAC_Makefile.in; do
	patch -p0 -i ${srcdir}/$i
  done

  ./configure --prefix=/usr --mandir=/usr/share/man --enable-shared \
    --disable-sse --disable-rpath --with-pic --without-libiconv-prefix
  gmake
}

check() {
  cd "${srcdir}/${pkgbase}-${pkgver}"
  gmake check
}

package_flac() {
  pkgdesc="Free Lossless Audio Codec"
  optdepends=('xmms: for using the xmms plugin')
  options=('!libtool' '!docs')

  cd "${srcdir}/${pkgbase}-${pkgver}"
  gmake DESTDIR="${pkgdir}" install
  install -dm755  "${pkgdir}/usr/share/licenses/${pkgname}/"
  install -D -m644 COPYING.Xiph "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_flac-doc() {
  pkgdesc="Developer documentation for the Free Lossless Audio Codec"
  depends=('flac')

  cd "${srcdir}/${pkgbase}-${pkgver}"
  gmake DESTDIR="${pkgdir}" -C doc install
  install -dm755 ${pkgdir}/usr/share/licenses/${pkgname}/
  install -m644 COPYING.Xiph "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
