# $Id: PKGBUILD 187064 2013-06-03 11:16:43Z allan $
# Maintainer: Tom Gundersen <teg@jklm.no>
# Contributor: Angel Velasquez <angvp@archlinux.org> 
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Aaron Griffin <aaron@archlinux.org>
# Contributor: Jochem Kossen <j.kossen@home.nl>

pkgname=lsof
pkgver=4.87
pkgrel=2
pkgdesc="Lists open files for running Unix processes"
arch=('i686' 'x86_64')
url="http://people.freebsd.org/~abe/"
license=('custom')
#depends=('glibc')
depends=('libc')
source=(ftp://sunsite.ualberta.ca/pub/Mirror/lsof/${pkgname}_${pkgver}.tar.bz2 license.txt)
md5sums=('80e2a76d0e05826db910ec88e631296c'
         '1b63c76bd10437cabf890508c8e58d36')

build() {
	cd ${srcdir}/${pkgname}_${pkgver}
	tar xf ${pkgname}_${pkgver}_src.tar
	cd ${pkgname}_${pkgver}_src
	sed -i '' 's|/\* #define\tHASSECURITY\t1 \*/|#define\tHASSECURITY\t1|' dialects/linux/machine.h
	./Configure -n freebsd
	make 
}

package() {
	cd ${srcdir}/${pkgname}_${pkgver}/${pkgname}_${pkgver}_src

    install -dm0755 $pkgdir/usr/share/doc/$pkgname/
    FILES=("00CREDITS" "00DCACHE" "00DIALECTS" "00DIST" "00FAQ" "00LSOF-L" "00MANIFEST" "00PORTING" "00QUICKSTART" "00README" "00.README.FIRST" "00TEST" "00XCONFIG") 
    for file in "${FILES[@]}"
    do
        install -Dm0644 $file \
        $pkgdir/usr/share/doc/$pkgname/$file
    done

	install -dm0755 ${pkgdir}/usr/bin
	install -Dm0755 lsof ${pkgdir}/usr/bin/lsof 
	install -dm0755 ${pkgdir}/usr/share/man/man8/
	install -Dm0644 lsof.8 ${pkgdir}/usr/share/man/man8/lsof.8 
  
  	install -dm0755 ${pkgdir}/usr/share/licenses/${pkgname}
	install -Dm0644 ${srcdir}/license.txt \
	     ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE 
}
