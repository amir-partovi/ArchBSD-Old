# $Id: PKGBUILD 186662 2013-05-30 12:24:29Z heftig $
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=clutter
pkgver=1.14.4
pkgrel=5
pkgdesc="A GObject based library for creating fast, visually rich graphical user interfaces"
arch=('i686' 'x86_64')
url="http://clutter-project.org/"
options=('!libtool')
license=('LGPL')
depends=('cogl>=1.14.0-5' 'mesa' 'json-glib' 'atk' 'libxi' 'libxkbcommon')
makedepends=('gobject-introspection')
source=(http://download.gnome.org/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz)
sha256sums=('c996d91fff6fff24d9e23dcd545439ebc6b999fb1cf9ee44c28ca54c49c0ee1c')

build() {
  cd "$pkgname-$pkgver"
  #./configure --prefix=/usr --enable-introspection \
  #  --enable-wayland-{backend,compositor} --enable-egl-backend --enable-evdev-input
# add egl when our mesa package handles it
  ./configure --prefix=/usr --enable-introspection \
    --with-x --enable-conformance=no

  # https://bugzilla.gnome.org/show_bug.cgi?id=655517
  gsed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  gmake
}

package() {
  cd "$pkgname-$pkgver"
  gmake DESTDIR="$pkgdir" install
}
