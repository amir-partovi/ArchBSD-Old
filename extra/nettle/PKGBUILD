pkgname=nettle
pkgver=2.6
pkgrel=1
pkgdesc="A low-level cryptographic library"
arch=('i686' 'x86_64')
url="http://www.lysator.liu.se/~nisse/nettle/"
license=('GPL2')
install=$pkgname.install
#depends=('gmp')
source=(ftp://ftp.lysator.liu.se/pub/security/lsh/$pkgname-$pkgver.tar.gz)
md5sums=('02f76b668855c4da67186c04cd3a222f')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr/local --libdir=/usr/local/lib \
	--enable-shared
  gmake
}

check() {
  cd "$srcdir/$pkgname-$pkgver"
  gmake -k check
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  gmake DESTDIR="$pkgdir/" install
  
  # remove static libs 	#--disable-static - build would break (since nettle 2.6) - and had no effect before
  rm -f ${pkgdir}/usr/local/lib/{libhogweed,libnettle}.a

  install -dm755 "${pkgdir}/usr/local/libdata"
  cp -Ra "${pkgdir}/usr/local/lib/pkgconfig" "${pkgdir}/usr/local/libdata"
  rm -rf "${pkgdir}/usr/local/lib/pkgconfig"
}
