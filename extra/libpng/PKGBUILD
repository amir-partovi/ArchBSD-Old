# $Id: PKGBUILD 169446 2012-10-21 10:49:41Z ibiru $
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: dorphell <dorphell@archlinux.org>
# Contributor: Travis Willard <travis@archlinux.org>
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>

pkgname=libpng
pkgver=1.5.13
_apngver=1.5.13
pkgrel=1
pkgdesc="A collection of routines used to create PNG format graphics files"
arch=('i686' 'x86_64')
url="http://www.libpng.org/pub/png/libpng.html"
license=('custom')
options=('!libtool')
source=("http://downloads.sourceforge.net/sourceforge/$pkgname/$pkgname-$pkgver.tar.xz"
        "http://downloads.sourceforge.net/sourceforge/libpng-apng/libpng-$_apngver-apng.patch.gz"
        patch-ac
        patch-makefile.freebsd)
md5sums=('0b607a685da977f1bfc96e1c47055183'
         '23c8765aa6ca188c0575386d5ea8fe70'
         '8bafc2a81bf6c588727cec782aaefe85'
         'cf54ad12ef74d5d4bf01d61a40cccb06')

build() {
  cd $pkgname-$pkgver

  # Add animated PNG (apng) support
  # see http://sourceforge.net/projects/libpng-apng/
  patch -p1 -i ../libpng-$_apngver-apng.patch

  patch -p0 -i "${srcdir}/patch-ac"
  patch -p0 -i "${srcdir}/patch-makefile.freebsd"

  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  cd contrib/pngminus
  make PNGLIB="-L$pkgdir/usr/lib -lpng" -f makefile.std png2pnm pnm2png
  install -m755 png2pnm pnm2png "$pkgdir/usr/bin/"
  install -d -m755 "$pkgdir/usr/share/licenses/$pkgname"
  install    -m644 ../../LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
