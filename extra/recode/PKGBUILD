# Maintainer: Kevin Piche <kevin@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=recode
pkgver=3.6
pkgrel=8
pkgdesc="Converts files between various character sets and usages"
arch=('i686' 'x86_64')
url="http://recode.progiciels-bpi.ca/index.html"
license=('GPL' 'LGPL')
depends=('texinfo')
makedepends=('perl' 'gsed')
install=recode.install
source=(${pkgname}-${pkgver}.tar.gz::"https://github.com/pinard/${pkgname}/tarball/v${pkgver}"
        'http://ftp.de.debian.org/debian/pool/main/r/recode/recode_3.6-17.diff.gz'
        'recode-3.6-as-if.patch' 'recode-3.6-gettextfix.diff'
        patch-lib-error.c
        patch-libiconv-Makefile.in
        patch-src-Makefile.in
)
md5sums=('f82e9a6ede9119268c13493c9add2809'
         '3716ae269074e31add3d29387ff09438'
         'fca7484ba332c8ad59eb02334883cd92'
         'eb602e80a24b5448604bfebeacc55304'
         '5fb0165e435a665888bda49b99f4d0aa'
         '08c84fa8a5b7b6c999a95b1d028947db'
         '9eabd027009f73025cc1ccc2dd39b81a')

build() {
  cd ${srcdir}/pinard-Recode-2127b34
  patch -Np1 -i ${srcdir}/recode-3.6-as-if.patch
  patch -Np1 -i ${srcdir}/recode-3.6-gettextfix.diff
  patch -Np1 -i ${srcdir}/recode_3.6-17.diff
  gsed -i '1i#include <stdlib.h>' src/argmatch.c
  for i in patch-lib-error.c patch-libiconv-Makefile.in patch-src-Makefile.in; do
    patch -p0 -i "${srcdir}/$i"
  done
  sed -i '' -e 's/AM_CONFIG_HEADER/AC_CONFIG_HEADER/' configure.in
  sed -i '' -e '/AM_C_PROTOTYPES/d' configure.in
  sed -i '' -e '/AUTOMAKE_OPTIONS/s/ansi2knr//g' src/Makefile.am
  rm -f acinclude.m4
  autoreconf -fi
  libtoolize
  CFLAGS="$CFLAGS -fPIC" \
  CPPFLAGS="$CPPFLAGS -I/usr/local/include" \
  LDFLAGS="$LDFLAGS -L/usr/local/lib" \
  ./configure --prefix=/usr/local --mandir=/usr/local/man \
    --infodir=/usr/local/info --without-included-gettext
  gmake

  perl -pi -e '
  	if (/Project-Id-Version/) {
  		s/3\.[45](r|\.[0-9])?/3.6/;
  		s/: (Free )?recode/: GNU recode/;
  	}
  ' i18n/*.po
}

package() {
  cd ${srcdir}/pinard-Recode-2127b34
  make DESTDIR=${pkgdir} install

  rm -f "${pkgdir}/usr/local/lib/charset.alias"
  rm ${pkgdir}/usr/local/info/dir
  gzip ${pkgdir}/usr/local/info/*
}
