pkgname=lighttpd
pkgver=1.4.32
pkgrel=2
pkgdesc='A secure, fast, compliant and very flexible web-server'
license=('custom')
arch=('i686' 'x86_64')
url="http://www.lighttpd.net/"
depends=('pcre' 'libldap')
makedepends=('fcgi' 'libmysqlclient' 'lua51' 'libxml2' 'gdbm')
optdepends=('libxml2: mod_webdav'
            'lua51: mod_cml/mod_magnet'
            'libmysqlclient: mod_mysql_vhost')
backup=('usr/local/etc/lighttpd/lighttpd.conf')
options=('!libtool' 'emptydirs')
source=("http://download.lighttpd.net/lighttpd/releases-1.4.x/lighttpd-${pkgver}.tar.xz"
        "http://download.lighttpd.net/lighttpd/releases-1.4.x/lighttpd-${pkgver}.tar.xz.asc"
        'lighttpd.config' 'lighttpd.conf.d' 'lighttpd.rc.d')
sha256sums=('1368f80069ce71f5928cad59c8e60c0b95876942ca9e02c53853e54ae24aedc1'
            '5456a90967df6774386010baa3fa67dba1c80c3b7058c70c80931d8dea5093e7'
            'ee56422fe48f2683ccb5ca2e3dc6bad79ea8e1cbd043b21d2ea73b87018e35aa'
            'b6ef982ae5a7634b0c4613fb0196a0b0d2c74a9a5824af2fe3b838cd11110abe'
            'c8a06fc76f5d2ac08a0e93d28d69feccfca751d898549ff37d0e4d197108c42a')
build() {
	cd $srcdir/$pkgname-$pkgver
	CC="$CC $(pkg-config lua5.1 --cflags)" \
	LUA_CFLAGS=" " \
	LUA_LIBS="$(pkg-config lua5.1 --libs)" \
	./configure --prefix=/usr/local/ \
		--libdir=/usr/local/lib/lighttpd/ \
		--sysconfdir=/usr/local/etc/lighttpd \
		--enable-static \
		--with-mysql \
		--with-ldap \
		--with-attr \
		--with-openssl \
		--with-kerberos5 \
		--without-fam \
		--with-gdbm \
		--with-memcache \
		--with-bzip2 \
		--with-lua
	find . -name Makefile | xargs sed -i '' -e 's/-l-pthread/-pthread/g'
	make
}

check() {
	cd $srcdir/$pkgname-$pkgver
	make check
}

package() {
	cd $srcdir/$pkgname-$pkgver
	make DESTDIR=$pkgdir install

	install -dm755 $pkgdir/usr/local/etc/lighttpd/
	install -m644 $srcdir/lighttpd.config $pkgdir/usr/local/etc/lighttpd/lighttpd.conf.example

	install -dm755 $pkgdir/var/{log,cache}/lighttpd/
	install -dm 755 $pkgdir/srv/
	install -dm755 $pkgdir/srv/http/

	install -dm755 $pkgdir/usr/local/etc/init.d/
	install -dm755 $pkgdir/usr/local/etc/conf.d/

	install -m755 ${srcdir}/lighttpd.rc.d $pkgdir/usr/local/etc/init.d/lighttpd
	install -m755 ${srcdir}/lighttpd.conf.d $pkgdir/usr/local/etc/conf.d/lighttpd

	pushd doc/config >/dev/null
	install -dm755 $pkgdir/usr/local/share/doc/lighttpd/config/{conf.d,vhosts.d}
	find . -type f ! -name 'Makefile*' -exec install -m644 {} ${pkgdir}/usr/local/share/doc/lighttpd/config/{} \;
	popd >/dev/null

	install -dm755 $pkgdir/usr/local/share/licenses/$pkgname/COPYING
	install -m644 COPYING $pkgdir/usr/local/share/licenses/$pkgname/COPYING
}
