# Maintainer: Tobias Kieslich <tobias@archlinux.org>
# Contributor: dorphell <dorphell@archlinux.org>
# Contributor: John Proctor <jproctor@prium.net>

pkgname=libvorbis
pkgver=1.3.3
pkgrel=1
pkgdesc="Vorbis codec library"
arch=('i686' 'x86_64')
url="http://www.xiph.org/vorbis/"
license=('BSD')
depends=('libogg')
source=(http://downloads.xiph.org/releases/vorbis/${pkgname}-${pkgver}.tar.gz)
md5sums=('6b1a36f0d72332fae5130688e65efe1f')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  sed -i '' -e 's@CFLAGS="-O"@CFLAGS=""@' \
            -e 's@DEBUG="-g -Wall -Wextra -D__NO_MATH_INLINES -fsigned-char"@DEBUG="-g -Wall -Wextra -fsigned-char"@' \
            -e 's@CFLAGS="-O20 -g -Wall -Wextra -D__NO_MATH_INLINES -fsigned-char"@CFLAGS="-Wall -Wextra -fsigned-char"@' \
            -e 's@PROFILE="-O20 -g -pg -D__NO_MATH_INLINES -fsigned-char"@CFLAGS="-g -pg -fsigned-char"@' \
            configure

  #-march=i686 optimizes too much, strip it out
  # LIBS+="-lm" ./configure --prefix=/usr --disable-static
  CFLAGS=${CFLAGS/-march=$CARCH} \
  ./configure --prefix=/usr \
              --with-ogg=/usr \
              --disable-docs
  make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make -j1 check
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -dm755 "${pkgdir}/usr/share/licenses/$pkgname/"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/$pkgname/LICENSE"

}
