# Maintainer: Tobias Powalowski <tpowa@archlinux.org>

pkgname=ilmbase
pkgver=1.0.3
pkgrel=2
pkgdesc="Base libraries from ILM for OpenEXR"
arch=(i686 x86_64)
url="http://www.openexr.com"
license=('custom')
source=(https://github.com/downloads/openexr/openexr/$pkgname-$pkgver.tar.gz 
        patch-IexMath-IexMathFpu.cpp
        patch-Imath-ImathFun.cpp
        patch-ImathTest-testBoxAlgo.cpp
        patch-ImathTest-testShear.cpp
        patch-configure
        )
md5sums=('d99962ec7e2bfac0647aaabf6b91d5de'
         '6755adf3427c50b3f236828b779031cf'
         '4e11142acbc3b4cdbe278e63fc4e5974'
         'cc3dcfd8ce60358386ec9e425a84863a'
         '50caea1213cf1e42ddcd5e27ea8af4cf'
         '41aed3e7befe95058f8f354b12d10fb3')

build() {
  cd "${srcdir}/$pkgname-$pkgver"
  for i in patch-IexMath-IexMathFpu.cpp patch-Imath-ImathFun.cpp \
           patch-ImathTest-testBoxAlgo.cpp patch-ImathTest-testShear.cpp \
           patch-configure
  do
    patch -p0 -i "${srcdir}/$i"
  done
  PTHREAD_CFLAGS=-pthread \
  PTHREAD_LIBS=-pthread \
  ./configure --prefix=/usr --enable-threading
  make
}

check() {
  cd "${srcdir}/$pkgname-$pkgver"
  # one of the tests fails randomly for an unknown reason
  make check || true
}

package() {
  cd "${srcdir}/$pkgname-$pkgver"
  make DESTDIR="${pkgdir}" install
  install -dm755 "$pkgdir/usr/share/licenses/$pkgname/"
  install -m644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
