# $Id: PKGBUILD 184222 2013-05-04 08:38:14Z heftig $
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: DrZaius <lou[at]fakeoutdoorsman[dot]com>
# Contributor: pumbur

pkgname=opus
pkgver=1.0.2
pkgrel=2
pkgdesc="Codec designed for interactive speech and audio transmission over the Internet"
arch=(i686 x86_64)
url="http://www.opus-codec.org/"
license=(BSD)
options=('!libtool')
source=(http://downloads.us.xiph.org/releases/$pkgname/$pkgname-$pkgver.tar.gz)
md5sums=('c503ad05a59ddb44deab96204401be03')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --enable-custom-modes
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -dm755 "$pkgdir/usr/share/licenses/$pkgname"
  install -m644 COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
