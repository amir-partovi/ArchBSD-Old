# $Id: PKGBUILD 150868 2012-02-23 13:23:16Z ibiru $
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=guile
pkgver=1.8.8
pkgrel=3
pkgdesc="a portable, embeddable Scheme implementation written in C"
url="http://www.gnu.org/software/guile/"
arch=(i686 x86_64)
license=('GPL')
depends=('gmp' 'libltdl' 'ncurses>=5.7' 'texinfo')
install=guile.install
_port_patches=(
	patch-config.h.in
	patch-configure.in
	patch-libguile___scm.h
	patch-libguile_eval.c
	patch-libguile_gc.c
	patch-libguile_gc_os_dep.c
	patch-libguile_gen-scmconfig.c
	patch-libguile_numbers.c
	patch-libguile_pthread-threads.h
	patch-libguile_read.c
	patch-libguile_threads.c)
source=(ftp://ftp.gnu.org/pub/gnu/${pkgname}/${pkgname}-${pkgver}.tar.gz
    "${_port_patches[@]}")
options=('!libtool')
md5sums=('18661a8fdfef13e2fcb7651720aa53f3'
         '64ed569740c7d1c2767e139a167da083'
         'd0b3f90fa0bc5316b54e8592a6871d22'
         'b8a4de90e2f7a0be94573b44c795a421'
         '25973a57c640d1ff66e71c52a1403735'
         '8d6c8742e7046d41b22da24c8d69e0b0'
         '5c5fdf5cf200116a0d752b5a50a4c176'
         '622674c6d292ff9cb2693e656d2ba86f'
         '4de763e1a5c9784a75009ae6306b861b'
         'f740b0c4693fad35dd073e23da914efe'
         '530bfb73b5ca428311a55cec13272322'
         '6c9ca9d7b817e0876203d8f0edc3a741')

prepare() {
  cd "${pkgname}-${pkgver}"

  for i in "${_port_patches[@]}"; do
    msg "patch $i"
    patch -p0 -i "${srcdir}/$i"
  done

  for x in libguile/smob.c libguile/filesys.c libguile/gc.c \
			libguile/mallocs.c libguile/eval.c libguile/gc-malloc.c \
			libguile/ports.c libguile/gc-mark.c libguile/gc_os_dep.c; do
  sed -i '' -e 's|<malloc\.h>|<stdlib.h>|g' $x
  done
}

build() {
  cd "${pkgname}-${pkgver}"

  ./configure --prefix=/usr \
	--disable-static  \
	--disable-error-on-warning
  gmake LDFLAGS+="-lpthread"
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  gmake DESTDIR="${pkgdir}" install
}
