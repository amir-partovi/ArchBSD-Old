# $Id: PKGBUILD 149604 2012-02-08 19:38:19Z pierre $
# Maintainer:
# Contributor: dorphell <dorphell@archlinux.org>

pkgname=zip
pkgver=3.0
_pkgver=30
pkgrel=3
pkgdesc="Creates PKZIP-compatible .zip files"
arch=('i686' 'x86_64')
url="http://www.info-zip.org/Zip.html"
license=('BSD')
options=('!makeflags')
source=("ftp://ftp.info-zip.org/pub/infozip/src/${pkgname}${_pkgver}.zip"
        patch-aa patch-ab
        )
md5sums=('e88492c8abd68fa9cfba72bc08757dba'
         '1b783c55c1d9a785980f617e127fc7d0'
         '5194f111d60ee8f2b9c1c98732040bfe')

build() {
   cd "${srcdir}/${pkgname}${_pkgver}"
   for i in patch-aa patch-ab
   do
     patch -p0 -i "${srcdir}/$i"
   done
   make -f unix/Makefile LOCAL_ZIP="$CFLAGS" prefix=/usr generic_gcc
}

package() {
   cd "${srcdir}/${pkgname}${_pkgver}"
   make -f unix/Makefile INSTALL=/usr/bin/install prefix=${pkgdir}/usr \
     MANDIR=${pkgdir}/usr/share/man/man1 install
   install -d -m755 ${pkgdir}/usr/share/licenses/$pkgname/
   install    -m644 LICENSE ${pkgdir}/usr/share/licenses/$pkgname/LICENSE
}
