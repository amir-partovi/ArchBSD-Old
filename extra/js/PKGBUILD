# $Id: PKGBUILD 183636 2013-04-25 18:14:18Z ioni $
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgname=js
pkgver=17.0.0
pkgrel=1
pkgdesc="JavaScript interpreter and libraries"
arch=(i686 x86_64)
url="https://developer.mozilla.org/En/SpiderMonkey/17"
license=(MPL)
depends=(nspr gcc-libs libffi readline)
makedepends=(python2 zip)
source=(http://ftp.mozilla.org/pub/mozilla.org/js/mozjs$pkgver.tar.gz)
md5sums=('20b6f8f1140ef6e47daa3b16965c9202')

build() {
  unset CPPFLAGS
  cd mozjs$pkgver/js/src
  find $srcdir/$pkgname-$pkgver  -name "configure" -type f | xargs sed -i '' \
                                -e 's|-lpthread|-pthread|g' \
                                -e 's|DATADIRNAME=lib|DATADIRNAME=share|g' \
                                -e 's|{libdir}/locale|{prefix}/share/locale|g'
  ./configure --prefix=/usr --with-system-nspr --with-system-ffi \
     --enable-readline --enable-threadsafe --with-pthreads
  echo "fenv.h" >> config/system-headers
  echo "pthread_np.h" >> config/system-headers
  gmake
}

check() {
  cd mozjs$pkgver/js/src
  gmake check
}

package() {
  cd mozjs$pkgver/js/src
  gmake DESTDIR="$pkgdir" install
  find "$pkgdir"/usr/{lib/pkgconfig,include} -type f -exec chmod -x {} +
}

# vim:set ts=2 sw=2 et:
