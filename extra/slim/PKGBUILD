pkgname=slim
pkgver=1.3.5
pkgrel=1
pkgdesc="Desktop-independent graphical login manager for X11"
arch=('i686' 'x86_64')
url="http://slim.berlios.de/"
license=('GPL2')
depends=('libxmu' 'libpng' 'libjpeg' 'libxft')
makedepends=('cmake' 'freeglut')
install=slim.install
backup=('usr/local/etc/slim.conf')
source=(http://download.berlios.de/$pkgname/$pkgname-$pkgver.tar.gz
	patch-Ck.cpp
	patch-app.cpp
	patch-slim.conf
	patch-switchuser.cpp
	patch-themes__default__slim.theme
	slim
)
sha256sums=('818d209f51e2fa8d5b94ef75ce90a7415be48b45e796d66f8083a9532b655629'
            '25ee70ea712b562e0125f253120445cc555b5831dd53b690da3055133a23ec3e'
            '3717f06a6e3ef63826da53a386195af6a55edd772d1e154fd8e24cdb4617c49b'
            'fd18085b2b7686167b72f2d0cdd4e58573c7d21f6c7c3d35e40e5cc3ac674625'
            '3ecea86810251ace4431db605bd71db5621d775c967dbc6cdb310df6a6b94107'
            'af8da49774742161b0aa71474391053b130e86402f43e3fb2b47b1fabcf2383f'
            '481c8d6ad4e2f78a1f92cff49241fc5f98556e396cdaa83897e91624d2507f01')
build() {
  cd "$srcdir/$pkgname-$pkgver"

  sed -i '' -e 's|%%LOCALBASE%%|${LOCALBASE}|g' $srcdir/$pkgname-$pkgver/slim.conf
  sed -i '' -e 's|"/etc"|"/usr/local/etc"|' \
	    -e 's|set(MANDIR "$${CMAKE_INSTALL_PREFIX}/share/man")|set(MANDIR "/usr/local/man")|' \
	    $srcdir/$pkgname-$pkgver/CMakeLists.txt


 for i in patch-Ck.cpp \
	patch-app.cpp \
	patch-slim.conf \
	patch-switchuser.cpp \
	patch-themes__default__slim.theme; do
  patch -p0 -i ${srcdir}/${i}
  done

  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr/local \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_SKIP_RPATH=ON \
    -DUSE_CONSOLEKIT=no
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install

  # Provide sane defaults
  sed -i '' -e 's|#xserver_arguments.*|xserver_arguments -nolisten tcp vt07|' \
         -e 's|/var/run/slim.lock|/var/lock/slim.lock|' \
    "$pkgdir/usr/local/etc/slim.conf"
  install -m755 -d "$pkgdir/etc/init.d/"
  install -m644 ${srcdir}/slim "$pkgdir/etc/init.d/slim"
  chmod +x "$pkgdir/etc/init.d/slim"
}

# vim:set ts=2 sw=2 et:
