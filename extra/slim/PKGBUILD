pkgname=slim
pkgver=1.3.5
pkgrel=1
pkgdesc="Desktop-independent graphical login manager for X11"
arch=('i686' 'x86_64')
url="http://slim.berlios.de/"
license=('GPL2')
depends=('libxmu' 'libpng' 'libjpeg' 'libxft' 'consolekit')
makedepends=('cmake' 'freeglut')
install=slim.install
backup=('etc/slim.conf')
source=(http://download.berlios.de/$pkgname/$pkgname-$pkgver.tar.gz
	patch-Ck.cpp
	patch-PAM.cpp
	patch-app.cpp
	patch-switchuser.cpp
	patch-slim.conf
	patch-themes__default__slim.theme
	slim
	freebsd-init.in)
sha256sums=('818d209f51e2fa8d5b94ef75ce90a7415be48b45e796d66f8083a9532b655629'
            'c0145026529e77d25632f56e5eef407bdeecd0761d58d18a5adf7eb0c9ad7587'
            'fe786998b6540d9340488780f310f00c0c823adbc238fc3c16e9d58545c0a618'
            '9177b9fbe07dca3a043455ddb1be4f6ee3d6f94627415188292c3045e86d19a3'
            '5a898117c223768ce949a057759cc160021eaaff512e270f997587028c4a4e4a'
            '03b867333f533190bc85c20ee3d62dbfc2c3a44ced89eeaed4c12c923c6c2886'
            'ac213d47f3f7621f1a71cb29a0223f6573ca981a2c992ca7b17b31570da9469d'
            '50479a6353f712408039ef24dfef2e5b9d4761efab4b7644e6a6047337d263d3'
            '7fc2213640da5ffc24b584487de229cbb9b34044380923c04b4915a4e0fa7f89')

build() {
  cd "$srcdir/$pkgname-$pkgver"

 export CC=clang
 export CXX=clang++

 for i in patch-Ck.cpp \
	patch-PAM.cpp \
	patch-app.cpp \
	patch-switchuser.cpp \
	patch-slim.conf \
	patch-themes__default__slim.theme; do
  patch -p0 -i ${srcdir}/${i}
  done

  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_SKIP_RPATH=ON \
    -DUSE_CONSOLEKIT=yes

  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install

  # Provide sane defaults
  sed -i '' -e 's|\/bin\/bash|\/usr\/bin\/bash|' "$pkgdir/etc/slim.conf"

  sed -i '' -e 's|%%LOCALBASE%%|\/usr|g' "$pkgdir/etc/slim.conf"

  install -m755 -d "$pkgdir"/etc/{rc,init}.d
  install -m644 ${srcdir}/slim "$pkgdir/etc/init.d/slim"
  install -m644 ${srcdir}/freebsd-init.in "$pkgdir/etc/rc.d/slim"
  chmod +x "$pkgdir/etc/init.d/slim"
}

# vim:set ts=2 sw=2 et:
