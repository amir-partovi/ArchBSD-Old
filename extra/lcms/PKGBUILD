# $Id: PKGBUILD 148810 2012-02-05 11:51:27Z ibiru $
# Maintainer: Tobias Kieslich <neri@archlinux.org>
# Contributor: Ben <contrasutra@myrealbox.com>

pkgname=lcms
pkgver=1.19
pkgrel=2
pkgdesc="Lightweight color management development library/engine"
arch=('i686' 'x86_64')
license=('custom')
depends=('libtiff')
url="http://www.littlecms.com"
options=('!libtool')
source=(http://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}.tar.gz
        patch-icc34
        patch-test-static
)
md5sums=('8af94611baf20d9646c7c2c285859818'
         'ca6e421b0c086ac12b1b8dbb894936a6'
         '697b4f9ae4592dccce58c0c1489841d1')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p0 -i "$srcdir"/patch-icc34
  patch -p0 -i "$srcdir"/patch-test-static
  ./configure --prefix=/usr --disable-static
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
