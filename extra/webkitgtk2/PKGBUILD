# $Id: PKGBUILD 188485 2013-06-14 23:12:24Z heftig $
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

pkgname=webkitgtk2
pkgver=1.10.2
pkgrel=8
pkgdesc="GTK+ Web content engine library for GTK+ 2.0"
arch=(i686 x86_64)
url="http://webkitgtk.org/"
license=(custom)
depends=(libxt libxslt sqlite libsoup enchant libgl geoclue gtk2 gstreamer0.10-base
         icu)
makedepends=(gperf gobject-introspection python2 mesa ruby gtk-doc)
optdepends=()
provides=("libwebkit=${pkgver}")
conflicts=(libwebkit)
replaces=(libwebkit)
options=(!libtool !emptydirs)
source=(http://webkitgtk.org/releases/webkitgtk-$pkgver.tar.xz
	patch-configure)
sha256sums=('19234f8e4edfaefe91ed06471a2aa8c71d26dd55d33787eede1dcfc9a3a96d9c'
            '12f57bc645bd2dbcbc0747e31fbb0fc752621da409587c0d17d01c2825fd1e2b')
prepare() {
	cd webkitgtk-${pkgver}
# find isatty
	gsed -i -e '/#include <stdlib\.h>/i#include <unistd.h>' \
	  Source/ThirdParty/gtest/include/gtest/internal/gtest-port.h
# SIGCHLD and some others
	gsed -i -e '/#include "config\.h"/i#include <sys/wait.h>' \
	        -e 's/SIGCLD/SIGCHLD/g' \
	  Source/WebKit2/UIProcess/Plugins/PluginProcessProxy.cpp
}


build() {
  cd webkitgtk-$pkgver

# hmm... build system fail?
  export LIBS=-lharfbuzz-icu
# we don't have a pkg-config for zlib
  export ZLIB_CFLAGS=" " ZLIB_LIBS=-lz

  # Suppress a *lot* of gcc 4.8 warnings
  CPPFLAGS+=" -Wno-unused-local-typedefs"

  patch -p0 -i "${srcdir}/patch-configure"

  FLEX=/usr/local/bin/flex PYTHON=/usr/bin/python2 ./configure --prefix=/usr \
    --libexecdir=/usr/lib/$pkgname \
    --enable-introspection \
    --disable-webkit2 \
    --with-gstreamer=0.10 \
    --disable-glibtest \
    --with-gtk=2.0

  # https://bugzilla.gnome.org/show_bug.cgi?id=655517
  gsed -i 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  gmake all stamp-po
}

package() {
  cd webkitgtk-$pkgver
  gmake -j1 DESTDIR="$pkgdir" install
  install -dm755 "$pkgdir/usr/share/licenses/$pkgname/"
  install -Dm644 Source/WebKit/LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  rm -r "$pkgdir/usr/share/gtk-doc/html/webkitgtk"
}
