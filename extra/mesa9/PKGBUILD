# Maintainer: Jan de Groot <jgc@archlinux.org>
# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Wolfgang Bumiller <blub@speed.at>

pkgbase=mesa
pkgname=('libgl' 'mesa')
pkgver=9.0.2
pkgrel=3
arch=('i686' 'x86_64')
makedepends=('glproto>=1.4.16' 'libdrm>=2.4.39' 'libxxf86vm>=1.1.2' 'libxdamage>=1.1.3' 'expat>=2.1.0' 'libx11>=1.5.0' 'libxt>=1.1.3' 
             'dri2proto>=2.8' 'python2' 'libxml2' 'imake' 'libvdpau>=0.5' 'gindent')
url="http://mesa3d.sourceforge.net"
license=('custom')
options=('!libtool')
source=(LICENSE llvm32.patch
	"ftp://ftp.freedesktop.org/pub/mesa/${pkgver}/MesaLib-${pkgver}.tar.bz2")
md5sums=('5c65a0fe315dd347e09b1f2826a1df5a'
         '5fe9ad0c5beed19bc98d8c34aaad4ade'
         'dc45d1192203e418163e0017640e1cfc')

build() {
    cd ${srcdir}/?esa-*
    
    patch -Np1 -i ${srcdir}/llvm32.patch

    export CFLAGS="${CFLAGS} -fPIC"
    export CXXFLAGS="${CXXFLAGS} -fPIC"
    export INDENT=/usr/bin/gindent

    unset CC
    unset CXX
    unset CPP

    COMMONOPTS="--prefix=/usr/ \
    --sysconfdir=/etc \
    --with-dri-driverdir=/usr/lib/dri \
    --with-gallium-drivers=nouveau,swrast \
    --with-dri-drivers=i915,i965,r200,radeon,nouveau,swrast \
    --enable-shared-glapi \
    --enable-glx-tls \
    --disable-gbm \
    --enable-dri \
    --enable-glx \
    --enable-osmesa \
    --enable-gles1 \
    --enable-gles2 \
    --enable-texture-float \
    --enable-xa \
    --enable-vdpau"

# not default:
#    --enable-gallium-egl enable optional EGL state tracker (not required for
#                          EGL support in Gallium with OpenGL and OpenGL ES)
#                          [default=disable]
#    --enable-xa             enable build of the XA X Acceleration API                          [default=no]

# *facepalm* ...
# if you use autotools / libtool
# do it right please...
    sed -i '' -e 's@\(ln -f \.libs/libglapi\.so\.0\)\.0\.0 @\1 @' src/mapi/shared-glapi/Makefile.am
    sed -i '' -e 's@\(ln -sf libglapi\.so\.0\)\.0\.0 @\1 @' src/mapi/shared-glapi/Makefile.am
    sed -i '' -e 's|\(ln -f \.libs/libdricore@VERSION@\.so\.1\)\.0\.0 |\1 |' src/mesa/libdricore/Makefile.am
    sed -i '' -e 's|\(ln -f \.libs/libGLESv1_CM\.so\.1\)\.1\.0 |\1 |' src/mapi/es1api/Makefile.am
    sed -i '' -e 's|\(ln -f \.libs/libGLESv2\.so\.2\)\.0\.0 |\1 |' src/mapi/es2api/Makefile.am
    sed -i '' -e 's|\(ln -f \.libs/lib@GL_LIB@\.so\.1\)\.2\.0 |\1 |' src/glx/Makefile.am
    sed -i '' -e 's|\(ln -f \.libs/libEGL\.so\.1\)\.0\.0 |\1 |' src/egl/main/Makefile.am

    autoreconf -vfi
    ./configure \
      $COMMONOPTS

    # fix a build break with new llvm
    sed -i '' -e "s:-Werror=implicit-function-declaration::" src/gallium/drivers/r600/Makefile

    gmake
}

package_libgl() {
  depends=('libdrm>=2.4.39' 'libxxf86vm>=1.1.2' 'libxdamage>=1.1.3' 'expat>=2.1.0' 'libglapi')
  pkgdesc="Mesa 3-D graphics library and DRI software rasterizer"

  # fix linking because of splitted package
  gmake -C ${srcdir}/?esa-*/src/mapi/shared-glapi DESTDIR="${pkgdir}" install

  # libGL & libdricore
  gmake -C ${srcdir}/?esa-*/src/glx DESTDIR="${pkgdir}" install
  gmake -C ${srcdir}/?esa-*/src/mesa/libdricore DESTDIR="${pkgdir}" install

  # fix linking because of splitted package - cleanup
  gmake -C ${srcdir}/?esa-*/src/mapi/shared-glapi DESTDIR="${pkgdir}" uninstall


  gmake -C ${srcdir}/?esa-*/src/gallium/targets/dri-swrast DESTDIR="${pkgdir}" install

  # See FS#26284
  install -m755 -d "${pkgdir}/usr/lib/xorg/modules/extensions"
  ln -s libglx.xorg "${pkgdir}/usr/lib/xorg/modules/extensions/libglx.so"

  install -m755 -d "${pkgdir}/usr/share/licenses/libgl"
  install -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/libgl/"
}

package_mesa() {
  # check also gl.pc
  depends=('libx11>=1.5.0' 'libxext>=1.3.1' 'libxdamage' 'libxfixes' 'libxcb' 'libxxf86vm'
           'libdrm')
  conflicts=('libglaphi' 'osmesa' 'khrplatform-devel' 'libgles')
  provides=("libglapi=${pkgver}-${pkgrel}"
            "osmesa=${pkgver}-${pkgrel}"
            "khrplatform-devel=${pkgver}-${pkgrel}"
            "libgles=${pkgver}-${pkgrel}")
  optdepends=('opengl-man-pages: for the OpenGL API man pages'
              'libgl: OpenGL library')
  pkgdesc="Mesa 3-D graphics libraries and include files"

  gmake -C ${srcdir}/?esa-*/src/mesa DESTDIR="${pkgdir}" install-glHEADERS
  gmake -C ${srcdir}/?esa-*/src/mesa/drivers/dri DESTDIR="${pkgdir}" install-driincludeHEADERS  
  gmake -C ${srcdir}/?esa-*/src/mesa DESTDIR="${pkgdir}" install-pkgconfigDATA
  gmake -C ${srcdir}/?esa-*/src/mesa/drivers/dri DESTDIR="${pkgdir}" install-pkgconfigDATA  
  gmake -C ${srcdir}/?esa-*/src/mesa/drivers/dri/common DESTDIR="${pkgdir}" install-sysconfDATA

#  gmake -C ${srcdir}/?esa-*/src/gallium/targets/xa-vmwgfx DESTDIR="${pkgdir}" install

  
# libglapi {{
  gmake -C ${srcdir}/?esa-*/src/mapi/shared-glapi DESTDIR="${pkgdir}" install
# }}

# osmesa {{
  gmake -C ${srcdir}/?esa-*/src/mesa/drivers/osmesa DESTDIR="${pkgdir}" install
# }}

# libgles {{
  gmake -C ${srcdir}/?esa-*/src/mapi/es1api DESTDIR="${pkgdir}" install
  gmake -C ${srcdir}/?esa-*/src/mapi/es2api DESTDIR="${pkgdir}" install
# }}

# khrplatform-devel {{
  install -m755 -d "${pkgdir}/usr/include/KHR"
  install -m644 ${srcdir}/?esa-*/include/KHR/khrplatform.h "${pkgdir}/usr/include/KHR/" 

  install -m755 -d "${pkgdir}/usr/share/licenses/khrplatform-devel"
  install -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/khrplatform-devel/"

  install -m755 -d "${pkgdir}/usr/share/licenses/mesa"
  install -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/mesa/"
}
