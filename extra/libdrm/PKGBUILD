# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=libdrm
pkgver=2.4.46
pkgrel=1
pkgdesc="Userspace interface to kernel DRM services"
arch=(i686 x86_64)
license=('custom')
depends=('libpciaccess')
makedepends=('cairo')
replaces=('libdrm-new' 'libdrm-nouveau')
url="http://dri.freedesktop.org/"
_port_patches=(
        extra-configure
        extra-tests_modetest_Makefile.in
        extra-tests_modetest_modetest.c
        extra-tests_modetest_strchrnul.c
        extra-xf86drmMode.c
)
source=(http://dri.freedesktop.org/$pkgname/$pkgname-$pkgver.tar.bz2
        no-pthread-stubs.patch
        COPYING
        ${_port_patches[@]})
sha256sums=('33cf320dad4e8060768714792e12643ddf6756a719d262ba7d60b39c2b2650f1'
            '3915aaff4d5b0ed523c3144911e7f79ee812dece0e3933c13a88dddcf5912ded'
            '9631d4f694952e3e6ae5a05534c2e93e994e47d3413677a3a00e45c8cef6db93'
            '4fa2a5f05a82042827ad6dafe48a419a8a2b118695e4ee5613f9a46f785b2ba9'
            '8b68319d939d7956cbe149b411df334bcf1da23c98318905a06745a8ef2e4f9e'
            '276bd7c6869c93de26a46de17b47fa9c5703629b39038b3bfbfb9e796c87d840'
            'abb6821f559992111c366199a75f8c0277c7f21d09c27f0d34e8158a6a04083a'
            '87ee4bf81818dc8e1929f1d640ed3002b5cff1f1b458247801060866f6ec4e4a')

prepare() {
  cd $pkgname-$pkgver

  patch -Np1 -i ../no-pthread-stubs.patch
  # man/Makefile,in is missing
  sed -i '' -e '/86|x86_64) INTEL=yes/ s@x86_64@&|amd64@' configure

  #libtoolize --force
  autoreconf --force --install

  # NOTE: HAS TO BE AFTER autoreconf
  for i in ${_port_patches[@]}; do
    msg "port patch $i"
    patch -p0 -i "${srcdir}/$i"
  done

  gsed -i -e '/#include <sys\/mman\.h>/i#define ERESTART (-1)' \
    libkms/vmwgfx.c
}

build() {
  cd $pkgname-$pkgver

  ./configure --prefix=/usr --enable-libkms
  gmake
}

check() {
  cd $pkgname-$pkgver
  gmake -k check
}

package() {
  cd $pkgname-$pkgver
  gmake DESTDIR="$pkgdir" install

  install -m755 -d "$pkgdir/usr/share/licenses/$pkgname"
  install -m644 ../COPYING "$pkgdir/usr/share/licenses/$pkgname/"
}
