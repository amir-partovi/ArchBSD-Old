# $Id: PKGBUILD 90462 2013-05-12 20:22:01Z bluewind $
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: John Proctor <jproctor@prium.net>

_pkgbasename=libxml2
pkgname=lib32-$_pkgbasename
pkgver=2.8.0
pkgrel=1
pkgdesc="XML parsing library, version 2 (32-bit)"
arch=(x86_64)
license=('custom')
depends=('lib32-readline>=6.1' 'lib32-ncurses>=5.7' $_pkgbasename)
depends=($_pkgbasename)
makedepends=(lib32-devel)
options=('!libtool')
url="http://www.xmlsoft.org/"
source=(ftp://ftp.xmlsoft.org/${_pkgbasename}/${_pkgbasename}-${pkgver}.tar.gz
        http://www.w3.org/XML/Test/xmlts20080205.tar.gz
        patch-aa
        patch-config.h.in
        patch-doc_devhelp_Makefile.in
        patch-python__Makefile.in
        patch-python__tests__Makefile.in)
md5sums=('c62106f02ee00b6437f0fb9d370c1093'
         'b255be9a1c7f7021e52448e4ec8d7a0d'
         '37752819e3d4211c01e367e69abbaa19'
         'f323d88a8beecf355f77ccb3d0afffb6'
         'c719e018f075cec722c5619837b90805'
         '550a4d6f95b1d43b650ca1ab5f4c931b'
         '40c179289cff4b586aa6c7a85d96c03a')

prepare() {
  cd "${_pkgbasename}-${pkgver}"
  for d in . doc doc/devhelp doc/examples
  do
    sed -i '' -e '/^install-data-am:/ s|install-data-local||' \
      ${d}/Makefile.in
  done

   for i in patch-aa patch-config.h.in patch-doc_devhelp_Makefile.in patch-python__tests__Makefile.in
   do
     patch -Np0 -i ${srcdir}/${i}
   done
}

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export CPP=cpp
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  export CFLAGS="${CFLAGS//-fstack-protector/}"
  export CPPFLAGS="${CPPFLAGS//-fstack-protector/}"
  export CXXFLAGS="${CXXFLAGS//-fstack-protector/}"

  cd "${_pkgbasename}-${pkgver}"
  #autoreconf -fi
  ./configure --prefix=/usr --with-threads --with-history --libdir=/usr/lib32 --without-lzma
  gmake
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  gmake DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{include,share,bin} "$pkgdir/usr/lib32/xml2Conf.sh"
  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
