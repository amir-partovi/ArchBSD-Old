# $Id: PKGBUILD 92221 2013-06-03 00:16:45Z lcarlier $
# Maintainer: Jan "heftig" Steffens <jan.steffens@gmail.com>

_pkgbasename=libogg
pkgname=lib32-$_pkgbasename
pkgver=1.3.1
pkgrel=1
pkgdesc="Ogg bitstream and framing library (32-bit)"
arch=('x86_64')
url="http://www.xiph.org/ogg/"
license=('BSD')
depends=('freebsd-lib32' $_pkgbasename)
makedepends=(lib32-devel)
options=('!libtool')
source=(http://downloads.xiph.org/releases/ogg/$_pkgbasename-$pkgver.tar.xz)
md5sums=('ca25d8da0ddfc8c6cbbf78d847a209fe')

build() {
    export CC="gcc -m32"
    export CXX="g++ -m32"
  export CPP=cpp
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  export CFLAGS="${CFLAGS//-fstack-protector/}"
  export CPPFLAGS="${CPPFLAGS//-fstack-protector/}"
  export CXXFLAGS="${CXXFLAGS//-fstack-protector/}"

    cd $srcdir/${_pkgbasename}-${pkgver}
    ./configure --prefix=/usr --libdir=/usr/lib32
    gmake
}

package() {
    cd $srcdir/${_pkgbasename}-${pkgver}

    gmake DESTDIR=$pkgdir install

    rm -rf "$pkgdir"/usr/{share,bin}
    find "$pkgdir"/usr/include/ogg -type f -not -name config_types.h -delete
    mv "$pkgdir"/usr/include/ogg/config_types{,-32}.h
    mkdir -p "$pkgdir/usr/share/licenses"
    ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
