# $Id: PKGBUILD 86582 2013-03-20 09:19:12Z bluewind $
# Maintainer: Florian Pritz <bluewind@xinu.at>
# Contributor: Joakim Hernberg <jhernberg at alchemy dot lu>
_pkgbasename=gettext
pkgname=lib32-$_pkgbasename
pkgver=0.18.2.1
pkgrel=1
pkgdesc="GNU internationalization library (32-bit)"
arch=('x86_64')
url="http://www.gnu.org/software/gettext/"
license=('GPL')
depends=('lib32-libiconv' $_pkgbasename)
makedepends=(lib32-devel)
optdepends=('cvs: for autopoint tool')
options=(!libtool !docs)
source=(ftp://ftp.gnu.org/pub/gnu/gettext/${_pkgbasename}-${pkgver}.tar.gz{,.sig})
md5sums=('034c8103b14654ebd300fadac44d6f14'
         'e4658c307333395a12f8d0b4d95fbb0f')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export CPP=cpp
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  export CFLAGS="${CFLAGS//-fstack-protector/}"
  export CPPFLAGS="${CPPFLAGS//-fstack-protector/}"
  export CXXFLAGS="${CXXFLAGS//-fstack-protector/}"

  cd "${srcdir}/${_pkgbasename}-${pkgver}"

  ./configure --prefix=/usr --libdir=/usr/lib32
  gmake
}

check() {
 cd "${srcdir}/${_pkgbasename}-${pkgver}"
 # too lazy to deal with LD_32_LIBRARY_PATH - this package has lots of LD_LIBRARY_PATH lines :P
 #gmake check
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  gmake DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/{bin,include,share}
}
