# $Id: PKGBUILD 91811 2013-05-28 11:37:37Z lcarlier $
# Maintainer: Florian Pritz <bluewind@xinu.at>
# Contributor: Christoph Vigano <mail@cvigano.de>

_pkgbasename=p11-kit
pkgname=lib32-$_pkgbasename
pkgver=0.18.1
pkgrel=1
pkgdesc="Library to work with PKCS#11 modules (32-bit)"
arch=(x86_64)
url="http://p11-glue.freedesktop.org"
license=('BSD')
depends=(freebsd-lib32 lib32-libtasn1 $_pkgbasename)
makedepends=(lib32-devel)
options=(!libtool)
source=($url/releases/$_pkgbasename-$pkgver.tar.gz)
md5sums=('e4ef21592dbd126b62e1a6d4ad65ce5c')

prepare() {
  cd "$_pkgbasename-$pkgver"

  #Hacks
  sed -i '' -e "/^ltmain=/!s|\$$ac_aux_dir/ltmain.sh|/usr/share/libtool/config/ltmain.sh|g" \
  	    -e "/^LIBTOOL=/s|\$$\(top_builddir\)/libtool|/usr/bin/libtool|g" \
		aclocal.m4

  find ${srcdir}/${pkgname}-${pkgver} -name "Makefile.in" | xargs sed -i '' -e 's|^LIBTOOL[ ]*=.*|LIBTOOL=/usr/bin/libtool|g'

  find ${srcdir}/${pkgname}-${pkgver} -name "configure" -type f | xargs sed -i '' -e 's|-lpthread|-pthread|g'
}

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export CPP=cpp
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  export CFLAGS="${CFLAGS//-fstack-protector/}"
  export CPPFLAGS="${CPPFLAGS//-fstack-protector/}"
  export CXXFLAGS="${CXXFLAGS//-fstack-protector/}"

  cd "$_pkgbasename-$pkgver"
  ./configure --prefix=/usr --libdir=/usr/lib32 --sysconfdir=/etc \
    --with-module-path=/usr/lib32/pkcs11 \
    --without-trust-paths
  gmake
}

package() {
  cd "$srcdir/$_pkgbasename-$pkgver"
  gmake DESTDIR="$pkgdir" install
  rm -rf "${pkgdir}"/etc
  rm -rf "${pkgdir}"/usr/{bin,include,share}
  install -dm755 $pkgdir/usr/share/licenses/$pkgname
  install -m644 COPYING $pkgdir/usr/share/licenses/$pkgname/COPYING
}

# vim:set ts=2 sw=2 et:
