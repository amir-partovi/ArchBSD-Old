# $Id: PKGBUILD 56009 2011-09-28 20:00:34Z bluewind $
# Maintainer: tobias <tobias@archlinux.net>
# Maintainer: Ray Rashif <schivmeister@gmail.com>
# Contributor: Robert Emil Berge <robert@rebi.no>

_pkgbasename=jack
pkgname=lib32-$_pkgbasename
_longname=jack-audio-connection-kit
pkgver=0.121.3
pkgrel=1
pkgdesc="A low-latency audio server (32-bit)"
arch=('x86_64')
license=('GPL' 'LGPL')
depends=('freebsd-lib32' "$_pkgbasename=$pkgver")
makedepends=(lib32-devel)
url="http://jackaudio.org/"
options=('!libtool')
source=(http://jackaudio.org/downloads/${_longname}-${pkgver}.tar.gz)
md5sums=('35f470f7422c37b33eb965033f7a42e8')

build() {
  cd ${srcdir}/${_longname}-${pkgver}

  export CC="gcc -m32"
  export CXX="g++ -m32"
  export CPP=cpp
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  export CFLAGS="${CFLAGS//-fstack-protector/}"
  export CPPFLAGS="${CPPFLAGS//-fstack-protector/}"
  export CXXFLAGS="${CXXFLAGS//-fstack-protector/}"

  ./configure --prefix=/usr --libdir=/usr/lib32
  gmake -C libjack
}

package() {
  cd ${srcdir}/${_longname}-${pkgver}

  gmake DESTDIR=${pkgdir} install-pkgconfigDATA
  gmake -C libjack DESTDIR=${pkgdir} install
}

# vim:set ts=2 sw=2 et:
