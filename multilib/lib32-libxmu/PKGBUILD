# $Id: PKGBUILD 68508 2012-03-26 09:56:19Z pschmitz $
# Contributor: Jan de Groot <jgc@archlinux.org>

_pkgbasename=libxmu
pkgname=lib32-$_pkgbasename
pkgver=1.1.1
pkgrel=1
pkgdesc="X11 miscellaneous micro-utility library (32-bit)"
arch=('x86_64')
url="http://xorg.freedesktop.org/"
depends=('lib32-libxext' 'lib32-libxt' $_pkgbasename)
makedepends=('xorg-util-macros' lib32-devel)
license=('custom')
options=('!libtool')
source=(${url}/releases/individual/lib/libXmu-${pkgver}.tar.bz2)
sha1sums=('3e280aa927d78c05f94db6c7ae606f7ad132b495')

build() {
  cd "${srcdir}/libXmu-${pkgver}"

  export CC="gcc -m32"
  export CPP=cpp
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  export CFLAGS="${CFLAGS//-fstack-protector/}"
  export CPPFLAGS="${CPPFLAGS//-fstack-protector/}"
  export CXXFLAGS="${CXXFLAGS//-fstack-protector/}"

  ./configure --prefix=/usr --sysconfdir=/etc --disable-static \
    --libdir=/usr/lib32
  make
}

package() {
  cd "${srcdir}/libXmu-${pkgver}"

  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{include,share}

  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname" 
}
