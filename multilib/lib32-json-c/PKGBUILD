# $Id: PKGBUILD 92672 2013-06-11 17:11:40Z heftig $
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Geoffroy Carrier <geoffroy.carrier@koon.fr>
# Contributor: congyiwu <congyiwu AT gmail DOT com>
_pkgbasename=json-c
pkgname=lib32-$_pkgbasename
pkgver=0.11
pkgrel=1
pkgdesc="A JSON implementation in C (32-bit)"
url="https://github.com/json-c/json-c/wiki"
license=(MIT)
arch=(x86_64)
depends=(freebsd-lib32 $_pkgbasename)
makedepends=(lib32-devel lib32-libtool)
options=(!libtool !makeflags)
source=(https://s3.amazonaws.com/${_pkgbasename}_releases/releases/$_pkgbasename-${pkgver}.tar.gz)
sha256sums=('28dfc65145dc0d4df1dfe7701ac173c4e5f9347176c8983edbfac9149494448c')

build() {
  cd $_pkgbasename-$pkgver

  export CC="gcc -m32"
  export CPP=cpp
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  export CFLAGS="${CFLAGS//-fstack-protector/}"
  export CPPFLAGS="${CPPFLAGS//-fstack-protector/}"

  ./configure --prefix=/usr --disable-static --libdir=/usr/lib32
  make
}

package() {
  cd $_pkgbasename-$pkgver
  make DESTDIR="$pkgdir" install

  rm -r "$pkgdir/usr/include"

  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
