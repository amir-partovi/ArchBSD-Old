# $Id: PKGBUILD 92764 2013-06-14 20:53:21Z lcarlier $
# Maintainer : Ionut Biru <ibiru@archlinux.org>

pkgname=lib32-dbus
_pkgbasename=dbus
pkgver=1.6.12
pkgrel=1
pkgdesc="Freedesktop.org message bus system (32-bit)"
arch=('x86_64')
url="http://www.freedesktop.org/Software/dbus"
license=('GPL' 'custom')
depends=('freebsd-lib32' 'lib32-expat' 'dbus')
makedepends=('lib32-devel' lib32-libx11)
provides=('lib32-dbus-core')
conflicts=('lib32-dbus-core')
replaces=('lib32-dbus-core')
options=(!libtool)
source=(http://dbus.freedesktop.org/releases/dbus/dbus-${pkgver}.tar.gz)
md5sums=('a70edc50524f258eaf5c9a9994ed8748')

build() {
    export CC="gcc -m32"
    export CXX="g++ -m32"
  export CPP=cpp
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  export CFLAGS="${CFLAGS//-fstack-protector/}"
  export CPPFLAGS="${CPPFLAGS//-fstack-protector/}"
  export CXXFLAGS="${CXXFLAGS//-fstack-protector/}"

    cd "${srcdir}/dbus-${pkgver}"

  ./configure --prefix=/usr --sysconfdir=/etc \
              --localstatedir=/var \
              --with-test-socket-dir="${srcdir}/test" \
              --with-system-pid-file=/var/run/dbus/pid \
              --with-system-socket=/var/run/dbus/system_bus_socket \
              --with-session-socket-dir=/tmp \
              --disable-doxygen-docs \
              --disable-xml-docs \
              --with-dbus-user=messagebus \
              --disable-systemd \
              --libdir=/usr/lib32

    gmake
}

package() {
    cd "${srcdir}/dbus-${pkgver}"
    gmake DESTDIR=${pkgdir} install

    rm -rf "${pkgdir}"/usr/{bin,include,lib,share,libexec}
    rm -rf "${pkgdir}"/{etc,var}

    mkdir -p "${pkgdir}/usr/share/licenses"
    ln -s ${_pkgbasename} "${pkgdir}/usr/share/licenses/${pkgname}"
}
