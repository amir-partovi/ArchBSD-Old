# $Id: PKGBUILD 64032 2012-02-08 19:00:07Z pschmitz $
# Maintainer: Jan "heftig" Steffens <jan.steffens@gmail.com>

_pkgbasename=speex
pkgname=lib32-$_pkgbasename
pkgver=1.2rc1
pkgrel=4
pkgdesc="A free codec for free speech (32-bit)"
arch=(x86_64)
license=('BSD')
depends=('lib32-libogg' $_pkgbasename)
makedepends=(lib32-devel)
options=('!libtool')
source=(http://downloads.us.xiph.org/releases/$_pkgbasename/$_pkgbasename-$pkgver.tar.gz)
md5sums=('c4438b22c08e5811ff10e2b06ee9b9ae')
url="http://www.speex.org/"

build() {
  cd $srcdir/speex-$pkgver

  export CC="gcc -m32"
  export CPP=cpp
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  export CFLAGS="${CFLAGS//-fstack-protector/}"
  export CPPFLAGS="${CPPFLAGS//-fstack-protector/}"
  export CXXFLAGS="${CXXFLAGS//-fstack-protector/}"

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --libdir=/usr/lib32 \
    --with-ogg-libraries=/usr/lib32
  make
}

package() {
  cd $srcdir/speex-$pkgver
  make -j1 DESTDIR=$pkgdir install
  rm -rf "${pkgdir}"/usr/{include,share,bin}
}
