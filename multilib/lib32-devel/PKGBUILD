# Maintainer: Wolfgang Bumiller <archbsd at bumiller dot com>

# package to enable -m32 for gcc

pkgname=lib32-devel
_gccver=4.8.0
pkgver=$_gccver
pkgrel=2
arch=('x86_64')
options=('!strip' 'emptydirs')
depends=("gcc=${_gccver}" "gcc-libs=${_gccver}" 'freebsd-source')
makedepends=("gcc=${_gccver}")
source=(ftp://gcc.gnu.org/pub/gcc/releases/gcc-${pkgver}/gcc-${pkgver}.tar.bz2
        specs.diff
        )
md5sums=('e6040024eb9e761c3bea348d1fa5abb0'
         'a8edaf776d3defdd07bff1db0fddaa17')

_CHOST_=x86_64-unknown-freebsd9.1

prepare() {
	mkdir include32
	mkdir include32/machine
	for i in /usr/src/sys/i386/include/*; do
		ln -svf "$i" "include32/machine/$(basename "$i")"
	done

	mkdir build
}

build() {
	gcc -dumpspecs > specs
	patch -p1 -i specs.diff

	# create our temporary specs:
	sed -e 's,@@INCLUDE32@@,'"$srcdir"'/include32,g' specs > build-specs
	# create to-be-installed specs:
	sed -e 's,@@INCLUDE32@@,/usr/lib32/include,g' specs > inst-specs


	export CC="gcc -specs=$srcdir/build-specs"
	export CXX="g++ -specs=$srcdir/build-specs"
	export CPP=cpp

	cd build
	../gcc-4.8.0/libstdc++-v3/configure \
		--cache-file=./config.cache \
		--prefix=/usr \
		--build=${_CHOST_} --host=${_CHOST_} --target=${_CHOST_} \
		--with-bugurl='https://bugs.archbsd.net/' \
		--disable-nls \
		--with-as=/usr/bin/as \
		--with-gmp=/usr \
		--with-ld=/usr/bin/ld \
		--with-libiconv-prefix=/usr \
		--with-pkgversion='ArchBSD Package Collection' \
		--with-system-zlib \
		--enable-cloog-backend=isl \
		--disable-cloog-version-check \
		--enable-multilib \
		--enable-languages=c,c++,fortran,lto,objc \
		--program-transform-name=s,y,y, \
		--with-target-subdir=${_CHOST_} \
		--srcdir=../gcc-4.8.0/libstdc++-v3 \
		--with-build-libsubdir=.
	gmake
}

package() {
	# install and cleanup
	cd build
	gmake DESTDIR="$srcdir/temp-install-dir" install
	install -dm755 "${pkgdir}/usr"
	mv "${srcdir}/temp-install-dir/usr/lib32" "${pkgdir}/usr"
	install -dm755 "${pkgdir}/usr/include/c++/${_gccver}/${_CHOST_}"
	mv "${srcdir}/temp-install-dir/usr/include/c++/${_gccver}/${_CHOST_}/32" \
	   "${pkgdir}/usr/include/c++/${_gccver}/${_CHOST_}/32"
	rm -r "$srcdir/temp-install-dir"


	cd "$srcdir"
	# specs file
	install -dm755 "${pkgdir}/usr/lib/gcc/x86_64-unknown-freebsd9.1/${_gccver}"
	install -m644 inst-specs "${pkgdir}/usr/lib/gcc/x86_64-unknown-freebsd9.1/${_gccver}/specs"


	# machine headers:
	install -dm755 "${pkgdir}/usr/lib32/include/machine"
	# The slash at the end is important!
	cp -R include32/machine/ "${pkgdir}/usr/lib32/include/machine"
}
