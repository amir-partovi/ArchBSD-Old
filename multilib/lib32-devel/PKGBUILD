# Maintainer: Wolfgang Bumiller <archbsd at bumiller dot com>

# package to enable -m32 for gcc

pkgname=lib32-devel
_gccver=4.8.0
_gccrel=2
pkgver=$_gccver
pkgrel=5
arch=('x86_64')
options=('!strip' 'emptydirs')
depends=("gcc>=${_gccver}-${_gccrel}" "gcc-libs=${_gccver}" 'freebsd-world>=9.1-8' 'freebsd-lib32>=9.1-8')
makedepends=("gcc=${_gccver}-${_gccrel}")
source=(ftp://gcc.gnu.org/pub/gcc/releases/gcc-${pkgver}/gcc-${pkgver}.tar.bz2
        specs.diff
        )
md5sums=('e6040024eb9e761c3bea348d1fa5abb0'
         '0858e56506fa6470942fc20b2ebfc2ee')

provides=("lib32-gcc-libs=${_gccver}" "gcc-multilib=${_gccver}")

_CHOST_=x86_64-unknown-freebsd9.2

prepare() {
	mkdir include32
	mkdir include32/machine
	for i in /usr/src/sys/i386/include/*; do
		ln -svf "$i" "include32/machine/$(basename "$i")"
	done

	mkdir build
}

build() {
	gcc -dumpspecs > specs
	patch -p1 -i specs.diff

	export CC="gcc -specs=$srcdir/specs"
	export CXX="g++ -specs=$srcdir/specs"
	export CPP=cpp

	cd build
	../gcc-4.8.0/libstdc++-v3/configure \
		--cache-file=./config.cache \
		--prefix=/usr \
		--build=${_CHOST_} --host=${_CHOST_} --target=${_CHOST_} \
		--with-bugurl='https://bugs.archbsd.net/' \
		--disable-nls \
		--with-as=/usr/bin/as \
		--with-gmp=/usr \
		--with-ld=/usr/bin/ld \
		--with-libiconv-prefix=/usr \
		--with-pkgversion='ArchBSD Package Collection' \
		--with-system-zlib \
		--enable-cloog-backend=isl \
		--disable-cloog-version-check \
		--enable-multilib \
		--enable-languages=c,c++,fortran,lto,objc \
		--program-transform-name=s,y,y, \
		--with-target-subdir=${_CHOST_} \
		--srcdir=../gcc-4.8.0/libstdc++-v3 \
		--with-build-libsubdir=.
	gmake
}

package() {
	# install and cleanup
	cd build
	gmake DESTDIR="$srcdir/temp-install-dir" install
	install -dm755 "${pkgdir}/usr"
	mv "${srcdir}/temp-install-dir/usr/lib32" "${pkgdir}/usr"
	install -dm755 "${pkgdir}/usr/include/c++/${_gccver}/${_CHOST_}"
	mv "${srcdir}/temp-install-dir/usr/include/c++/${_gccver}/${_CHOST_}/32" \
	   "${pkgdir}/usr/include/c++/${_gccver}/${_CHOST_}/32"
	rm -r "$srcdir/temp-install-dir"


	cd "$srcdir"
	# specs file
	install -dm755 "${pkgdir}/usr/lib/gcc/${_CHOST_}/${_gccver}"
	install -m644 specs "${pkgdir}/usr/lib/gcc/${_CHOST_}/${_gccver}/specs"


	## machine headers:
	#install -dm755 "${pkgdir}/usr/lib32/include/machine"
	## The slash at the end is important!
	#cp -R include32/machine/ "${pkgdir}/usr/lib32/include/machine"
}
