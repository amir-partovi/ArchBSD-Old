# $Id: PKGBUILD 93326 2013-07-01 23:11:34Z lcarlier $
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Wolfgang Bumiller <blub@speed.at>

#
#
# NOTE:
#   makepkg -R not supported without first executing the fakeinstall at the end of build() again
#
#

pkgbase=lib32-mesa
pkgname=('lib32-ati-dri' 'lib32-intel-dri' 'lib32-nouveau-dri' 'lib32-mesa' 'lib32-mesa-libgl')
pkgver=9.1.4
pkgrel=1
arch=('x86_64')
makedepends=('python2' 'lib32-libxml2' 'lib32-expat' 'lib32-libx11' 'glproto' 'lib32-libdrm' 'dri2proto' 'lib32-libxxf86vm' 'lib32-libxdamage'
             'lib32-devel' 'lib32-llvm-amdgpu-snapshot')
url="http://mesa3d.sourceforge.net"
license=('custom')
options=('!libtool')
source=(ftp://ftp.freedesktop.org/pub/mesa/${pkgver}/MesaLib-${pkgver}.tar.bz2)
md5sums=('a2c4e25d0e27918bc67f61bae04d0cb8')

prepare() {
    cd ${srcdir}/?esa-*

# *facepalm* ...
    sed -i '' -e 's@\(ln -f \.libs/libglapi\.so\.0\)\.0\.0 @\1 @' src/mapi/shared-glapi/Makefile.am
    sed -i '' -e 's@\(ln -sf libglapi\.so\.0\)\.0\.0 @\1 @' src/mapi/shared-glapi/Makefile.am
    sed -i '' -e 's|\(ln -f \.libs/libdricore@VERSION@\.so\.1\)\.0\.0 |\1 |' src/mesa/libdricore/Makefile.am
    sed -i '' -e 's|\(ln -f \.libs/libGLESv1_CM\.so\.1\)\.1\.0 |\1 |' src/mapi/es1api/Makefile.am
    sed -i '' -e 's|\(ln -f \.libs/libGLESv2\.so\.2\)\.0\.0 |\1 |' src/mapi/es2api/Makefile.am
    sed -i '' -e 's|\(ln -f \.libs/lib@GL_LIB@\.so\.1\)\.2\.0 |\1 |' src/glx/Makefile.am
    sed -i '' -e 's|\(ln -f \.libs/libEGL\.so\.1\)\.0\.0 |\1 |' src/egl/main/Makefile.am

    autoreconf -vfi

# fix glibc-ism
    sed -i '' -e 's/program_invocation_short_name/getprogname()/' \
        src/gallium/drivers/r300/r300_chipset.c
    sed -i '' -e 's|<byteswap.h>|<sys/endian.h>|' \
              -e 's| bswap_32|bswap32|' \
        src/gallium/drivers/r600/r600_asm.c \
        src/gallium/drivers/r600/r600_shader.c \
        src/gallium/drivers/r600/r600_state_common.c \
        src/gallium/drivers/{r600,radeonsi}/r600_buffer.c
}

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export CPP=cpp
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  export CFLAGS="${CFLAGS//-fstack-protector/}"
  export CPPFLAGS="${CPPFLAGS//-fstack-protector/}"
  export CXXFLAGS="${CXXFLAGS//-fstack-protector/}"
  export LLVM_CONFIG=/usr/bin/llvm-config32

  export INDENT=/usr/bin/gindent

  cd ${srcdir}/?esa-*

  ./configure --enable-32-bit \
    --libdir=/usr/lib32 \
    --prefix=/usr \
    --sysconfdir=/etc \
    --with-dri-driverdir=/usr/lib32/dri \
    --with-gallium-drivers=r300,r600,radeonsi,nouveau,swrast \
    --with-dri-drivers=i915,i965,r200,radeon,nouveau,swrast \
    --enable-shared-glapi \
    --enable-glx-tls \
    --disable-gbm \
    --enable-dri \
    --enable-glx \
    --enable-osmesa \
    --enable-gles1 \
    --enable-gles2 \
    --enable-texture-float \
    --enable-xa \
    --disable-vdpau

  # fix a build break with new llvm
  #sed -i '' -e "s:-Werror=implicit-function-declaration::" src/gallium/drivers/r600/Makefile

  gmake
  mkdir "$srcdir/fakeinstall"
  gmake DESTDIR="$srcdir/fakeinstall" install
}

package_lib32-ati-dri() {
  pkgdesc="Mesa drivers for AMD/ATI Radeon (32-bit)"
  depends=("lib32-mesa-libgl=${pkgver}" 'ati-dri')
  conflicts=('xf86-video-ati<6.9.0-6')

  install -m755 -d ${pkgdir}/usr/lib32/dri
  mv -v ${srcdir}/fakeinstall/usr/lib32/dri/{r200,r300,r600,radeon,radeonsi}_dri.so ${pkgdir}/usr/lib32/dri/

#  install -m755 -d ${pkgdir}/usr/lib32/gallium-pipe
#  mv -v ${srcdir}/fakeinstall/usr/lib32/gallium-pipe/pipe_{r300,r600,radeonsi}* ${pkgdir}/usr/lib32/gallium-pipe/
  
  install -m755 -d "${pkgdir}/usr/share/licenses/ati-dri"
  ln -s ati-dri "$pkgdir/usr/share/licenses/ati-dri/lib32-ati-dri"
}

package_lib32-intel-dri() {
  pkgdesc="Mesa DRI drivers for Intel (32-bit)"
  depends=("lib32-mesa-libgl=${pkgver}" 'intel-dri')

  install -m755 -d ${pkgdir}/usr/lib32/dri
  mv -v ${srcdir}/fakeinstall/usr/lib32/dri/{i915,i965}_dri.so ${pkgdir}/usr/lib32/dri/

  install -m755 -d "${pkgdir}/usr/share/licenses/intel-dri"
  ln -s intel-dri "$pkgdir/usr/share/licenses/intel-dri/lib32-intel-dri"
}

package_lib32-nouveau-dri() {
  pkgdesc="Mesa drivers for Nouveau (32-bit)"
  depends=("lib32-mesa-libgl=${pkgver}" 'nouveau-dri')

  install -m755 -d ${pkgdir}/usr/lib32/dri
  mv -v ${srcdir}/fakeinstall/usr/lib32/dri/nouveau_{dri,vieux_dri}.so ${pkgdir}/usr/lib32/dri/

#  install -m755 -d ${pkgdir}/usr/lib32/gallium-pipe
#  mv -v ${srcdir}/fakeinstall/usr/lib32/gallium-pipe/pipe_nouveau* ${pkgdir}/usr/lib32/gallium-pipe/

  install -m755 -d "${pkgdir}/usr/share/licenses/nouveau-dri"
  ln -s nouveau-dri "$pkgdir/usr/share/licenses/nouveau-dri/lib32-nouveau-dri"
}

package_lib32-mesa() {
  pkgdesc="an open-source implementation of the OpenGL specification (32-bit)"
  depends=('lib32-libdrm' 'lib32-libxxf86vm' 'lib32-libxdamage' 'lib32-llvm-amdgpu-lib-snapshot' 'mesa')
  optdepends=('opengl-man-pages: for the OpenGL API man pages')
  provides=('lib32-libglapi' 'lib32-osmesa' 'lib32-libgbm' 'lib32-libgles' 'lib32-libegl')
  conflicts=('lib32-libglapi' 'lib32-osmesa' 'lib32-libgbm' 'lib32-libgles' 'lib32-libegl')
  replaces=('lib32-libglapi' 'lib32-osmesa' 'lib32-libgbm' 'lib32-libgles' 'lib32-libegl')

  mv -v ${srcdir}/fakeinstall/* ${pkgdir}
  mv ${pkgdir}/usr/lib32/libGL.so.1 ${pkgdir}/usr/lib32/mesa-libGL.so.1.2.0
  rm ${pkgdir}/usr/lib32/libGL.so
  rm -r ${pkgdir}/etc
  rm -r ${pkgdir}/usr/include
  
  install -m755 -d "${pkgdir}/usr/share/licenses/mesa"
  ln -s mesa "$pkgdir/usr/share/licenses/mesa/lib32-mesa"
}

package_lib32-mesa-libgl() {
  pkgdesc="Mesa 3-D graphics library (32-bit)"
  depends=("lib32-mesa=${pkgver}")
  provides=("lib32-libgl=${pkgver}")
  replaces=('lib32-libgl')

  install -m755 -d ${pkgdir}/usr/lib32

  ln -s mesa-libGL.so.1.2.0 ${pkgdir}/usr/lib32/libGL.so
  ln -s mesa-libGL.so.1.2.0 ${pkgdir}/usr/lib32/libGL.so.1
  ln -s mesa-libGL.so.1.2.0 ${pkgdir}/usr/lib32/libGL.so.1.2.0

  install -m755 -d "${pkgdir}/usr/share/licenses/libglapi"
  ln -s libglapi "${pkgdir}/usr/share/licenses/libglapi/lib32-libglapi"
}
