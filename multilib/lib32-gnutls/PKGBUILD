# $Id: PKGBUILD 92470 2013-06-06 01:06:19Z lcarlier $
# Maintainer: Florian Pritz <bluewind@xinu.at>
# Contributor: Christoph Vigano <mail at cvigano dot de>
# Contributor: Biru Ionut <ionut@archlinux.ro>
# Contributor: Pierre Schmitz <pierre@archlinux.de>
# Contributor: Mikko Seppälä <t-r-a-y@mbnet.fi>

_pkgbasename=gnutls
pkgname=lib32-$_pkgbasename
pkgver=3.1.10
pkgrel=1
pkgdesc="A library which provides a secure layer over a reliable transport layer (32-bit)"
arch=('x86_64')
license=('GPL3' 'LGPL2.1')
url="http://gnutls.org/"
options=('!libtool')
depends=('lib32-nettle' 'lib32-p11-kit' 'lib32-libtasn1' 'lib32-gettext' $_pkgbasename)
makedepends=('lib32-devel' 'lib32-libidn')
source=(ftp://ftp.gnutls.org/gcrypt/gnutls/v3.1/${_pkgbasename}-${pkgver}.tar.xz{,.sig})
md5sums=('9e73aa5c3ef3b7358159a31302e25aa7'
         'SKIP')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export CPP=cpp
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  export CFLAGS="${CFLAGS//-fstack-protector/}"
  export CPPFLAGS="${CPPFLAGS//-fstack-protector/}"
  export CXXFLAGS="${CXXFLAGS//-fstack-protector/}"

  cd ${srcdir}/${_pkgbasename}-${pkgver}

  # build fails without --disable-hardware-acceleration because of assembler errors
  ./configure --prefix=/usr --libdir=/usr/lib32 \
    --with-zlib \
    --disable-static \
    --disable-guile \
    --disable-valgrind-tests --disable-hardware-acceleration
  gmake
}

check() {
  cd ${srcdir}/${_pkgbasename}-${pkgver}
  #make -k check
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  gmake DESTDIR="${pkgdir}" install
  find $pkgdir

  rm -rf "${pkgdir}"/usr/{bin,include,share}
}
