# Maintainer: Florian Pritz <bluewind@xinu.at>

_pkgbasename=harfbuzz
pkgname=lib32-$_pkgbasename
pkgver=0.9.18
pkgrel=1
pkgdesc="OpenType text shaping engine. (32-bit)"
arch=('x86_64')
url="http://www.freedesktop.org/wiki/Software/HarfBuzz"
license=('MIT')
depends=('lib32-icu' 'lib32-glib2' 'lib32-freetype2' $_pkgbasename)
makedepends=('lib32-devel')
options=(!libtool)
source=(http://www.freedesktop.org/software/harfbuzz/release/${_pkgbasename}-${pkgver}.tar.bz2)
sha256sums=('fef0fbbc6d6492cb4074a5e26a699d87d88bef8adc25dda62af6077735a7d908')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export CPP=cpp
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  export CFLAGS="${CFLAGS//-fstack-protector/}"
  export CPPFLAGS="${CPPFLAGS//-fstack-protector/}"
  export CXXFLAGS="${CXXFLAGS//-fstack-protector/}"

  cd "${srcdir}/${_pkgbasename}-${pkgver}"

  # work around autogen.sh requiring ragel
  autoreconf --force --install --verbose
  ./configure --prefix=/usr --libdir=/usr/lib32 --disable-silent-rules --with-graphite2=no --without-cairo
  gmake
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"

  gmake DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{include,share,bin}
  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
