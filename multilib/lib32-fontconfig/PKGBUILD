# $Id: PKGBUILD 91376 2013-05-21 12:21:03Z lcarlier $
# Maintainer: Jan de Groot <jgc@archlinux.org>

_pkgbasename=fontconfig
pkgname=lib32-$_pkgbasename
pkgver=2.10.93
pkgrel=1
pkgdesc="A library for configuring and customizing font access (32-bit)"
arch=(x86_64)
url="http://www.fontconfig.org/release/"
license=('custom')
depends=('lib32-expat' 'lib32-freetype2' $_pkgbasename)
makedepends=(lib32-devel)
options=('!libtool')
source=(http://www.fontconfig.org/release/${_pkgbasename}-${pkgver}.tar.bz2
        git_fixes.diff)
sha256sums=('ea901f278848829ed9937d76fb0ce63ad362d7d5b9e75aa6a6b78bfef42e529c'
            '7dc98137405ee0e5f98f3266a760a4da27d78e3c2bc7b7ef4db6b481df3c39f0')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export CPP=cpp
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  export CFLAGS="${CFLAGS//-fstack-protector/}"
  export CPPFLAGS="${CPPFLAGS//-fstack-protector/}"
  export CXXFLAGS="${CXXFLAGS//-fstack-protector/}"

  cd "${srcdir}/${_pkgbasename}-${pkgver}"

  # post release fixes
  # patch -Np1 -i ${srcdir}/git_fixes.diff

  # make sure there's no rpath trouble and sane .so versioning - FC and Gentoo do this as well
  libtoolize -f
  autoreconf -fi

  # so the tests can run:
  gsed -i 's/shlibpath_var=.*/shlibpath_var=LD_32_LIBRARY_PATH/g' configure
  ./configure --prefix=/usr \
	  --sysconfdir=/etc \
	  --with-templatedir=/etc/fonts/conf.avail \
	  --with-xmldir=/etc/fonts \
	  --localstatedir=/var \
	  --disable-static \
	  --with-default-fonts=/usr/share/fonts \
	  --with-add-fonts=/usr/share/fonts \
	  --libdir=/usr/lib32

  gmake
}

check() {
  cd "$srcdir/$_pkgbasename-$pkgver"
  gmake -k check
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"

  gmake DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{include,share,bin} "$pkgdir"/{etc,var}
  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
