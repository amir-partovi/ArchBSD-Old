# $Id: PKGBUILD 92105 2013-05-31 14:53:05Z lcarlier $
# Contributor: Jan de Groot <jgc@archlinux.org>

_pkgbasename=libxtst
pkgname=lib32-$_pkgbasename
pkgver=1.2.1
pkgrel=1
pkgdesc="X11 Testing -- Resource extension library (32-bit)"
arch=(x86_64)
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('lib32-libxext' 'lib32-libxi' 'lib32-libxfixes' $_pkgbasename)
makedepends=('lib32-devel' 'xorg-util-macros')
options=('!libtool')
source=(${url}/releases/individual/lib/libXtst-${pkgver}.tar.bz2)
sha1sums=('44e19899f9938d3db5546e51c338d6f86d88cfe2')

build() {
  cd "${srcdir}/libXtst-${pkgver}"

  export CC="gcc -m32"
  export CPP=cpp
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  export CFLAGS="${CFLAGS//-fstack-protector/}"
  export CPPFLAGS="${CPPFLAGS//-fstack-protector/}"
  export CXXFLAGS="${CXXFLAGS//-fstack-protector/}"

  ./configure --prefix=/usr --disable-static \
    --libdir=/usr/lib32
  make
}

package() {
  cd "${srcdir}/libXtst-${pkgver}"

  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{include,share}

  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
