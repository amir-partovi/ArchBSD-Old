# Maintainer: Allan McRae <allan@archlinux.org>
# Contributor: Andreas Radke <andyrtr@archlinux.org>

pkgname=autoconf
pkgver=2.69
pkgrel=1
pkgdesc="A GNU tool for automatically configuring source code"
arch=('any')
license=('GPL2' 'GPL3' 'custom')
url="http://www.gnu.org/software/autoconf"
groups=('base-devel')
depends=('gm4' 'bash')
# for test-suite
#makedepends=('gcc-fortran' 'erlang')
install=autoconf.install
source=(ftp://ftp.gnu.org/pub/gnu/${pkgname}/${pkgname}-${pkgver}.tar.xz{,.sig}
        patch-autoconf.texi patch-doc-Makefile.in
)
md5sums=('50f97f4159805e374639a73e2636f22e'
         '2941e2fe687e05b3431eac6b714053bc'
         'e0416acd013ab0962970768c9e429eda'
         'fda9ecec0c93be3cf667b979ef0bd80c')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  patch -p0 -i "$srcdir"/patch-autoconf.texi
  patch -p0 -i "$srcdir"/patch-doc-Makefile.in
  CONFIG_SHELL=/usr/bin/bash \
  M4=/usr/bin/gm4 \
  AWK=/usr/bin/awk \
  ac_cv_prog_TEST_EMACS=no \
  ./configure --prefix=/usr --without-lispdir
  gmake
}

#check() {
#  cd ${srcdir}/${pkgname}-${pkgver}
#  gmake check
#}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  gmake DESTDIR=${pkgdir} install

  # license exception
  install -dm755 $pkgdir/usr/share/licenses/autoconf/
  install -m644 COPYING.EXCEPTION $pkgdir/usr/share/licenses/autoconf/COPYING.EXCEPTION

  # conflict with bintuils
  rm -f ${pkgdir}/usr/share/info/standards.info
}
