pkgname=mpfr
_pkgver=3.1.1
_patchlevel=p2
pkgver=${_pkgver}.${_patchlevel}
pkgrel=2
pkgdesc="Multiple-precision floating-point library"
arch=('i686' 'x86_64')
url="http://www.mpfr.org/"
license=('LGPL')
depends=('gmp>=5.0')
options=('!libtool')
install=mpfr.install
source=(http://www.mpfr.org/mpfr-current/mpfr-${_pkgver}.tar.xz{,.asc}
	mpfr-${_pkgver}-${_patchlevel}.patch)
md5sums=('91d51c41fcf2799e4ee7a7126fc95c17'
         '91b187a2229211e543ba6fb82a079b2c'
         'eef835f97bc26ea20b0232bd913acdd8')

build() {
  cd "${srcdir}/${pkgname}-${_pkgver}"
  patch -p1 -i $srcdir/mpfr-${_pkgver}-${_patchlevel}.patch
  ./configure --prefix=/usr --enable-thread-safe --enable-shared
  make
}

check() {
  cd "${srcdir}/${pkgname}-${_pkgver}"
  make check
}

package() {
  cd "${srcdir}/${pkgname}-${_pkgver}"
  make DESTDIR="${pkgdir}" install
}
