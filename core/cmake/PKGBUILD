# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>
# Contributor: Wolfgang Bumiller <blub@speed.at>

pkgname=cmake
pkgver=2.8.9
pkgrel=2
arch=('i686' 'x86_64')
url="http://www.cmake.org/"
license=('custom')
depends=('curl' 'shared-mime-info')
makedepends=('expat')
optdepends=('qt: cmake-gui')
options=('!docs')
conflicts=('cmake-modules')
provides=('cmake-modules')
source=("http://www.cmake.org/files/v2.8/${pkgname}-${pkgver}.tar.gz"
        patch-Modules_FindDCMTK.cmake
        patch-Modules_FindJava.cmake
        patch-Modules_FindQt4.cmake
        patch-Modules_FindwxWidgets.cmake
        patch-Modules_FindwxWindows.cmake)
md5sums=('801f4c87f8b604f727df5bf1f05a59e7'
         'a58b1e1abe2c3cf9b81a225928725e01'
         'f5efcae8c3d35575b93172cec7d6af59'
         '715efdbe373b9c40249dde17fc2a998b'
         'c717a03d877f6e248a09c3592ef5d098'
         '3d54a7a68af89b8d9a8c51c6579c1461')

build() {
  cd "${srcdir}/${pkgbase}-${pkgver}"

# Ports patches
  patch -p0 -i "${srcdir}/patch-Modules_FindDCMTK.cmake"
  patch -p0 -i "${srcdir}/patch-Modules_FindJava.cmake"
  patch -p0 -i "${srcdir}/patch-Modules_FindQt4.cmake"
  patch -p0 -i "${srcdir}/patch-Modules_FindwxWidgets.cmake"
  patch -p0 -i "${srcdir}/patch-Modules_FindwxWindows.cmake"

  ./configure --prefix=/usr \
    --mandir=/man \
    --docdir=/doc/cmake \
    --datadir=/share \
    --system-libs

  gmake
}

package() {
  pkgname="cmake"
  pkgdesc="A cross-platform open-source make system"
  install="${pkgname}.install"
  depends=('curl' 'libarchive' 'shared-mime-info' 'cmake-modules')

  cd "${srcdir}/${pkgbase}-${pkgver}"
  gmake DESTDIR="${pkgdir}" install

  vimpath="${pkgdir}/usr/share/vim/vimfiles"
  install -dm755 "${vimpath}"/indent
  install -m644 Docs/cmake-indent.vim "${vimpath}"/indent/cmake-indent.vim
  install -dm755 "${vimpath}"/syntax
  install -m644 Docs/cmake-syntax.vim "${vimpath}"/syntax/cmake-syntax.vim

# install -Dm644 Docs/cmake-mode.el "${pkgdir}"/usr/share/emacs/site-lisp/cmake-mode.el
# emacs -batch -f batch-byte-compile "${pkgdir}"/usr/share/emacs/site-lisp/cmake-mode.el

  install -dm755 "${pkgdir}"/usr/share/licenses/${pkgname}-${pkgver}
  install -m644 Copyright.txt "${pkgdir}"/usr/share/licenses/${pkgname}-${pkgver}/LICENSE
}
