# Maintainer: Allan McRae <allan@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

pkgname=flex
pkgver=2.5.37
pkgrel=2
pkgdesc="A tool for generating text-scanning programs"
arch=('i686' 'x86_64')
url="http://flex.sourceforge.net"
license=('custom')
groups=('base-devel')
install=flex.install
source=(http://downloads.sourceforge.net/sourceforge/flex/flex-$pkgver.tar.bz2
        lex.sh)
md5sums=('c75940e1fc25108f2a7b3ef42abdae06'
         'f725259ec23a9e87ee29e2ef82eda9a5')

build() {
  cd $srcdir/$pkgname-$pkgver

  sed -i '' -E \
      -e 's/tests//' \
      -e 's/^([[:space:]]*)\$\(RANLIB\) ([^ ;]*)/\1chmod u+w \2;&/' \
      Makefile.in
  sed -i '' -e 's/@MAKEINFO@/& --no-split/g' doc/Makefile.in

  rm doc/*.info*

  ./configure --prefix=/usr --includedir=/usr/include/flex
  gmake
}

check() {
  cd $srcdir/$pkgname-$pkgver
  
  # these tests used features removed in bison-2.6
  sed -i '' -e '/test-bison-yylloc/d' -e '/test-bison-yylval/d' tests/Makefile.in

  gmake check
}

package() {
  cd $srcdir/$pkgname-$pkgver

  gmake DESTDIR=$pkgdir install
  install -dm755 $pkgdir/usr/bin/
  install -m755 $srcdir/lex.sh $pkgdir/usr/bin/lex
  install -dm755 \
  	$pkgdir/usr/share/licenses/$pkgname/
  install -m644 COPYING \
  	$pkgdir/usr/share/licenses/$pkgname/license.txt
}

