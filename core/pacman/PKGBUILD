pkgname=pacman
pkgver=4.1.0
pkgrel=2
pkgdesc="A library-based package manager with dependency support"
arch=('i686' 'x86_64')
url="http://www.archlinux.org/pacman/"
license=('GPL')
groups=('base')
depends=('bash' 'libarchive>=3.0.2' 'curl>=7.19.4' 'gpgme' 'pacman-mirrorlist' 'archbsd-keyring')
makedepends=('asciidoc' 'gsed' 'gmake')
optdepends=('fakeroot: for makepkg usage as normal user')
backup=(etc/pacman.conf etc/makepkg.conf)
install=pacman.install
source=(ftp://ftp.archlinux.org/other/pacman/$pkgname-$pkgver.tar.gz
        pacman.conf
        pacman.conf.x86_64
        makepkg.site
        makepkg.conf
	patch-install-shell.patch)
md5sums=('a0f2b3148bee4784f21cf373cf59a0bc'
         'aab0c0c1e41faa79380cf241848ed02d'
         '9e0344aae49bf68296b7e639e8ad0b46'
         'a4a3867810d26de84ba52ce123d02658'
         'ffe002385ed3dbf461366e7ee78dd5bd'
         'ebe9198f8213fd0a19eb321acd1b1bdd')

build() {
  cd $srcdir/$pkgname-$pkgver
#  sed -i.bak -e 's|\/bin\/sh|\/usr\/bin\/bash|g' lib/libalpm/trans.c

#  patch -p0 -i $srcdir/patch-install-shell.patch

  export LDFLAGS="-L/usr/local/lib,-rpath=/usr/local/lib"
  export CPPFLAGS="$CPPFLAGS -I/usr/include/gpgme"

  ./configure --prefix=/usr --sysconfdir=/etc \
    --localstatedir=/var --enable-doc \
    --with-scriptlet-shell=/usr/bin/bash \
    --with-ldconfig=/sbin/ldconfig

  sed -i ' ' -e '/m4.*-P/ s/m4/gm4/' scripts/Makefile
  gmake
}

package() {
  cd $srcdir/$pkgname-$pkgver
  gmake DESTDIR=$pkgdir install

  # install Arch specific stuff
  mkdir -p $pkgdir/etc
  case "$CARCH" in
    i686)
      install -m644 $srcdir/pacman.conf $pkgdir/etc/pacman.conf
      mycarch="i686"
      mychost="i686-archbsd-freebsd"
      myflags="-march=i686"
      ;;
    x86_64)
      install -m644 $srcdir/pacman.conf.x86_64 $pkgdir/etc/pacman.conf
      mycarch="x86_64"
      mychost="x86_64-archbsd-freebsd"
      myflags="-march=x86-64"
      ;;
  esac
  install -m644 $srcdir/makepkg.conf $pkgdir/etc/
  # set things correctly in the default conf file
  sed -i '' -e "s|@CARCH@|$mycarch|g" \
      $pkgdir/etc/makepkg.conf
  sed -i '' -e "s|@CHOST@|$mychost|g" \
      $pkgdir/etc/makepkg.conf
  sed -i '' -e "s|@CARCHFLAGS@|$myflags|g" \
      $pkgdir/etc/makepkg.conf

}
