# vim: set ts=2 sw=2 et:
# $Id: PKGBUILD 181860 2013-04-04 22:54:14Z allan $
# Maintainer: Dan McGee <dan@archlinux.org>
# Maintainer: Dave Reisner <dreisner@archlinux.org>

pkgname=pacman
pkgver=4.1.2
pkgrel=1
pkgdesc="A library-based package manager with dependency support"
arch=('i686' 'x86_64')
url="http://www.archlinux.org/pacman/"
license=('GPL')
groups=('base' 'base-devel')
depends=('bash>=4.2.042-2' 'libarchive>=3.1.2' 'curl>=7.30.0'
         'gpgme' 'pacman-mirrorlist' 'archbsd-keyring')
checkdepends=('python2')
provides=('pacman-contrib')
conflicts=('pacman-contrib')
replaces=('pacman-contrib')
backup=(etc/pacman.conf etc/makepkg.conf)
install=pacman.install
options=(!libtool)
source=(ftp://ftp.archlinux.org/other/pacman/$pkgname-$pkgver.tar.gz{,.sig}
        pacman.conf.x86_64
	pacman.conf.i686
        makepkg.conf)
md5sums=('063c8b0ff6bdf903dc235445525627cd'
         'SKIP'
         'de74a13618347f08ae4a9637f74471c4'
         '2db6c94709bb30cc614a176ecf8badb1'
         '883292a326d2b3e0d9e7c3ec576263f5')


build() {
  cd $srcdir/$pkgname-$pkgver

  export LDFLAGS="-L/usr/local/lib,-rpath=/usr/local/lib"
  export CPPFLAGS="$CPPFLAGS -I/usr/include/gpgme"

  #Fix now being able to remove pkgdir as regular user
  sed -i '' -e 's/a\-srwx/a\-sx/g' scripts/makepkg.sh.in

  ./configure --prefix=/usr --sysconfdir=/etc \
    --localstatedir=/var --enable-doc \
    --with-scriptlet-shell=/usr/bin/bash \
    --with-ldconfig=/sbin/ldconfig

  sed -i '' -e '/m4.*-P/ s/m4/gm4/' contrib/Makefile scripts/Makefile
  sed -i '' -e 's/sed /gsed /' scripts/makepkg.sh.in contrib/* Makefile

  gmake
  gmake -C contrib
}

check() {
  #All tests pass except ldconfig tests
  gmake -C "$pkgname-$pkgver" check || true
}

package() {
  cd $srcdir/$pkgname-$pkgver
  gmake DESTDIR=$pkgdir install
  gmake DESTDIR=$pkgdir -C contrib install

  # install Arch specific stuff
  install -dm755 $pkgdir/etc
  install -m644 $srcdir/pacman.conf.$CARCH $pkgdir/etc/pacman.conf
  
  case "$CARCH" in
    i686)    
      mycarch="i686"
      mychost="i686-archbsd-freebsd"
      myflags="-march=i686"
      ;;
    x86_64)
      mycarch="x86_64"
      mychost="x86_64-unknown-freebsd"
      myflags="-march=x86-64"
      ;;
  esac
  install -m644 $srcdir/makepkg.conf $pkgdir/etc/
  # set things correctly in the default conf file
  gsed -i $pkgdir/etc/makepkg.conf \
    -e "s|@CARCH[@]|$mycarch|g" \
    -e "s|@CHOST[@]|$mychost|g" \
    -e "s|@CARCHFLAGS[@]|$myflags|g"
    
  # put bash_completion in the right location
  install -dm755 ${pkgdir}/usr/share/bash-completion/completions
  mv ${pkgdir}/etc/bash_completion.d/pacman \
    ${pkgdir}/usr/share/bash-completion/completions
  rmdir ${pkgdir}/etc/bash_completion.d

  for f in makepkg pacman-key; do
    ln -s pacman "$pkgdir/usr/share/bash-completion/completions/$f"
  done
}
