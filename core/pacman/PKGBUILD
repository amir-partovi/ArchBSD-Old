pkgname=pacman
pkgver=4.0.3
pkgrel=9
pkgdesc="A library-based package manager with dependency support"
arch=('i686' 'x86_64')
url="http://www.archlinux.org/pacman/"
license=('GPL')
groups=('base')
depends=('bash' 'libarchive>=3.0.2' 'curl>=7.19.4' 'gpgme' 'pacman-mirrorlist' 'archbsd-keyring')
makedepends=('asciidoc' 'gsed' 'gmake')
optdepends=('fakeroot: for makepkg usage as normal user')
backup=(etc/pacman.conf etc/makepkg.conf)
install=pacman.install
source=(ftp://ftp.archlinux.org/other/pacman/$pkgname-$pkgver.tar.gz
        0001-Add-conflict-for-replacing-owned-empty-directory.patch
        0002-Check-empty-subdirectory-ownership.patch
        0003-Ensure-pre_upgrade-scriptlet-gets-old-package-version.patch
        pacman.conf
        pacman.conf.x86_64
        makepkg.site
        makepkg.conf
	patch-install-shell.patch)
md5sums=('387965c7125e60e5f0b9ff3b427fe0f9'
         'e0c7c9026dfbb5a042a6cb05c3810fd6'
         '02db451be806335ce189ffadb5cf84b9'
         '9926eb703225b8e34d54e222685d63d8'
         'aab0c0c1e41faa79380cf241848ed02d'
         'c6e25f79c32b1ab331c30e2f771eb03e'
         'a4a3867810d26de84ba52ce123d02658'
         'c45e6ec22b452d8da91eac9f35d2cc2e'
         '57b9a20db9a6bb7350973ce71b677084')

build() {
  cd $srcdir/$pkgname-$pkgver
  sed -i.bak -e 's|\/bin\/sh|\/usr\/bin\/bash|g' lib/libalpm/trans.c

  patch -p1 -i $srcdir/0001-Add-conflict-for-replacing-owned-empty-directory.patch
  patch -p1 -i $srcdir/0002-Check-empty-subdirectory-ownership.patch
  patch -p1 -i $srcdir/0003-Ensure-pre_upgrade-scriptlet-gets-old-package-version.patch
  patch -p0 -i $srcdir/patch-install-shell.patch
  export CPPFLAGS="$CPPFLAGS -I/usr/include/gpgme"
  ./configure --prefix=/usr --sysconfdir=/etc \
    --localstatedir=/var --disable-doc 

  sed -i ' ' -e '/m4.*-P/ s/m4/gm4/' scripts/Makefile
  gmake
}

package() {
  cd $srcdir/$pkgname-$pkgver
  gmake DESTDIR=$pkgdir install

  # install Arch specific stuff
  mkdir -p $pkgdir/etc
  case "$CARCH" in
    i686)
      install -m644 $srcdir/pacman.conf $pkgdir/etc/pacman.conf
      mycarch="i686"
      mychost="i686-archbsd-freebsd"
      myflags="-march=i686"
      ;;
    x86_64)
      install -m644 $srcdir/pacman.conf.x86_64 $pkgdir/etc/pacman.conf
      mycarch="x86_64"
      mychost="x86_64-archbsd-freebsd"
      myflags="-march=x86-64"
      ;;
  esac
  install -m644 $srcdir/makepkg.conf $pkgdir/etc/
  # set things correctly in the default conf file
  sed -i '' -e "s|@CARCH@|$mycarch|g" \
      $pkgdir/etc/makepkg.conf
  sed -i '' -e "s|@CHOST@|$mychost|g" \
      $pkgdir/etc/makepkg.conf
  sed -i '' -e "s|@CARCHFLAGS@|$myflags|g" \
      $pkgdir/etc/makepkg.conf

  # install completion files
  install -dm755 "$pkgdir/etc/bash_completion.d"
  install -m755 contrib/bash_completion "$pkgdir/etc/bash_completion.d/bash_completion.sh"
  for f in makepkg pacman-key; do
    ln -s pacman "$pkgdir/etc/bash_completion.d/${f}.sh"
  done

  install -dm755 $pkgdir/usr/share/zsh/site-functions
  install -m644 contrib/zsh_completion $pkgdir/usr/share/zsh/site-functions/_pacman
}
