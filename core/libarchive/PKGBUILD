# $Id: PKGBUILD 167058 2012-09-25 03:07:21Z allan $
# Maintainer: Dan McGee <dan@archlinux.org>
# Contributor: Wolfgang Bumiller <blub@speed.at>

pkgname=libarchive
pkgver=3.0.4
pkgrel=2
pkgdesc="library that can create and read several streaming archive formats"
arch=('i686' 'x86_64')
url="http://libarchive.googlecode.com/"
license=('BSD')
depends=('libiconv' 'pkgconf')
source=("https://github.com/downloads/libarchive/libarchive/libarchive-${pkgver}.tar.gz"
        libarchive-3.0.x-fix-mtree-writer.patch
        patch-libarchive-archive_read_disk_entry_from_file.c
        )
md5sums=('af443ca9a10ddbcbf00f7ae34ca7fc16'
         '6d36a50a7282db6576bd1fbc23f08055'
         'ea3cf6afe437646b0540029e55e22101')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -p1 -i $srcdir/patch-libarchive-archive_read_disk_entry_from_file.c
  patch -p1 -i $srcdir/libarchive-3.0.x-fix-mtree-writer.patch

  CFLAGS="${CFLAGS} -DHAVE_BSDXML_H=1"
  LDFLAGS="${LDFLAGS} -lbsdxml"

  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --mandir=/usr/share/man \
              --infodir=/usr/share/info \
              --with-openssl --without-nettle \
              --without-expat --without-xml2
  make
}

check() {
  cd "$srcdir/$pkgname-$pkgver"

  make check
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install

  install -d -m755 "$pkgdir"/usr/share/licenses/libarchive/COPYING
  install    -m644 COPYING "$pkgdir"/usr/share/licenses/libarchive/COPYING
}
