# $Id: PKGBUILD 91097 2013-05-19 11:35:37Z arodseth $
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Mateusz Herych <heniekk@gmail.com>
# Contributor: Sergio Jovani Guzman <moret.sjg@gmail.com>
# Contributor: royrocks <royrocks13@gmail.com>

pkgname=tuxpaint
pkgver=0.9.21
pkgrel=1
pkgdesc='Drawing program designed for young children'
arch=('x86_64' 'i686')
url='http://www.tuxpaint.org/'
license=('GPL')
backup=('etc/tuxpaint/tuxpaint.conf')
depends=('python2' 'fribidi' 'sdl_ttf' 'sdl_mixer' 'sdl_image' 'sdl_pango'
         'libpaper' 'librsvg')
makedepends=('optipng')
optdepends=('tuxpaint-stamps: more stamps'
            'tuxpaint-conf: configuration manager')
source=("http://downloads.sourceforge.net/sourceforge/tuxpaint/$pkgname-$pkgver.tar.gz"
        'tuxpaint-0.9.21-libpng1.5.patch' 'patch-Makefile')
sha256sums=('6d4f2b981643b5c937708e5e6cdaf318ae7b5562375327b4b1cb84611e65ede9'
            '6d572467fce710c9323ba53bb3309eb7ed1401b4fee2c1f56533e871558a7f20'
            'c6f85af81e3d1786d882504923304516d220739e41923a3de3afd1fa04d030d9')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  export CC=clang
  export CXX=clang++
  export LDFLAGS="-lpangoft2 ${LDFLAGS} -shared -fPIC"
  gsed -i 's:# fu:fu:' src/tuxpaint.conf

  # python2 fix
  for f in docs/zh_tw/mkTuxpaintIM.py fonts/locale/zh_tw_docs/maketuxfont.py; do
    gsed -i '0,/on/s//on2/' $f
  done

  patch -p0 -i ../patch-Makefile
  # libpng15 fix
  patch -p0 -i ../tuxpaint-0.9.21-libpng1.5.patch
  gsed -i 's:$(ARCH_LINKS):$(ARCH_LINKS) -lintl -lpng -lpaper -lSDL_mixer:' Makefile

}

build() {
  cd "$srcdir/$pkgname-$pkgver"

  gmake PREFIX=/usr
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  gmake \
    PREFIX=/usr \
    DESTDIR="$pkgdir" \
    GNOME_PREFIX=/usr \
    X11_ICON_PREFIX="$pkgdir/usr/share/pixmaps" \
    install || true
  chmod -R 644 "$pkgdir/usr/share/doc/tuxpaint-dev/"*
  chmod 755 "$pkgdir/usr/share/doc/tuxpaint-dev/html"
}

# vim:set ts=2 sw=2 et:
