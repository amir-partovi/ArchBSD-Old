pkgname=xmlrpc-c
pkgver=1.33.03
pkgrel=1
#epoch=1
pkgdesc='XML-RPC for C and C++'
arch=('x86_64' 'i686')
url='http://xmlrpc-c.sourceforge.net/'
license=('custom')
depends=('curl' 'libxml2')
makedepends=('libtool')
options=('!makeflags' '!libtool' '!emptydirs')
source=("$pkgname-advanced-$pkgver.tar.gz::http://xmlrpc-c.svn.sourceforge.net/viewvc/xmlrpc-c/advanced/?view=tar")
sha256sums=('3bd69286da5ac5287071d0f14cf1bd70133c485c53b224d2f9d9dca971915402')

build() {
  cd "$srcdir/advanced"

  [ "$CARCH" != "i686" ] && export CFLAGS="$CFLAGS -fPIC"
	grep -lR '\-lpthread' $srcdir/advanced | xargs sed -i '' -e 's|-lpthread|-pthread|g'
	sed -i '' -e '/#include.*features.h/ s/features.h/sys\/cdefs.h/' $srcdir/advanced/tools/xmlrpc_pstream/xmlrpc_pstream.cpp
	sed -i '' -e '/define _XOPEN_SOURCE 600/ s,^,//,' $srcdir/advanced/src/xmlrpc_server_abyss.c
	sed -i '' -e 's,sys/unistd.h,unistd.h,' $srcdir/advanced/test/cpp/server_abyss.cpp
  ./configure --prefix=/usr \
    --mandir=/usr/share/man \
    --enable-libxml2-backend \
    --disable-cgi-server \
    --disable-libwww-client \
    --disable-wininet-client \
    --enable-abyss \
    --enable-cplusplus
  if [ ! -d include/curl ]; then
    mkdir include/curl
  fi
  touch include/curl/types.h
  LDFLAGS="$LDFLAGS -lz -lm"
  gmake CFLAGS_PERSONAL="$CFLAGS"
  gmake CFLAGS_PERSONAL="$CFLAGS" -C tools
}

package() {
  cd "$srcdir/advanced"

  gmake DESTDIR="$pkgdir" install
  gmake DESTDIR="$pkgdir" -C tools install
  # Clean out build-vars
  sed -i '' -e 's/BLDDIR=.*/BLDDIR=/' "$pkgdir/usr/bin/xmlrpc-c-config"
  sed -i '' -e 's/ABS_SRCDIR=.*/ABS_SRCDIR=/' "$pkgdir/usr/bin/xmlrpc-c-config"

  install -dm755 "$pkgdir/usr/share/licenses/$pkgname/"
  install -Dm644 doc/COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
