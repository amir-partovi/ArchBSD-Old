# $Id: PKGBUILD 64903 2012-02-18 20:04:04Z bpiotrowski $
# Maintainer:  Bart≈Çomiej Piotrowski <nospam@bpiotrowski.pl>
# Contributor: Blue Peppers <bluepeppers@archlinux.us>
# Contributor: Stefan Husmann< stefan-husmann@t-online.de>

pkgname=tint2
pkgver=0.11
pkgrel=1
pkgdesc="A basic, good-looking task manager for WMs"
arch=('i686' 'x86_64')
url="http://code.google.com/p/tint2/"
license=('GPL2')
conflicts=('ttm-svn' 'tint')
replaces=('tint')
depends=('gtk2' 'imlib2')
optdepends=('pygtk: for using tint2conf')
makedepends=('pygtk' 'cmake')
provides=('tint')
source=("http://$pkgname.googlecode.com/files/$pkgname-${pkgver}.tar.bz2"
        'add-power-now-support.patch'
	'patch-src-battery-battery.c')
md5sums=('6fc5731e7425125fa84a2add5cef4bff'
         '6cfcad028f1bd2f69812167f5395f890'
         'c043191693d3dba32f04023d4a1b1092')

prepare(){
  cd "$srcdir/$pkgname-${pkgver}"
  patch -p0 -i "${srcdir}/patch-src-battery-battery.c"

  sed -i '' -e 's#pidof#pgrep#g' "$srcdir/$pkgname-${pkgver}"/src/tint2conf/tintwizard.py
}

build() {
  cd "$srcdir/$pkgname-${pkgver}"

  # add support for power_now battery attribute (fixes FS#21954)
  patch -Np0 -i ../add-power-now-support.patch

  # python2 fix
  gsed -i 's_#!/usr/bin/env python_#!/usr/bin/env python2_' src/tint2conf/tintwizard.py
  gsed -i 's_python _python2 _' src/tint2conf/main.c

  [ -d build ] || mkdir build
  cd build
  cmake -DCMAKE_INSTALL_PREFIX=/usr ../
  gmake 
}

package() {
  cd "$srcdir/$pkgname-${pkgver}/build"
  gmake DESTDIR="$pkgdir" install
}
